# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Contributor: artoo <artoo@manjaro.org>

pkgbase=desktop-settings
pkgname=('manjaro-lxqt-settings'
        'manjaro-lxqt-minimal-settings')
pkgver=20160922
pkgrel=1
arch=('any')
url="https://github.com/manjaro/desktop-settings"
license=('GPL')
makedepends=('git')
source=("git+$url.git")
sha256sums=('SKIP')

_inst_dir(){
    if [[ -d $srcdir/$pkgbase/$1/skel ]];then
        install -d $pkgdir/etc
        cp -r $srcdir/$pkgbase/$1/skel $pkgdir/etc
    fi
}

pkgver() {
    date +%Y%m%d
}

package_manjaro-lxqt-settings() {
    pkgdesc='Manjaro Linux lxqt settings'
    depends=('manjaro-base-skel')
    provides=('manjaro-desktop-settings')
    conflicts=('manjaro-desktop-settings')

    _inst_dir 'community/lxqt'
}

package_manjaro-lxqt-minimal-settings() {
    pkgdesc='Manjaro Linux lxqt-minimal settings'
    depends=('manjaro-base-skel'
            'xcursor-breeze'
            'andromeda-icon-theme'
            'andromeda-wallpaper')
    optdepends=('manjaro-openbox-settings: Manjaro Linux Openbox settings')
    provides=('manjaro-desktop-settings')
    conflicts=('manjaro-desktop-settings')

    _inst_dir 'minimal/lxqt'
}
