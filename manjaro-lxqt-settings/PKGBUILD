# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Contributor: artoo <artoo@manjaro.org>

pkgbase=manjaro-lxqt-settings
pkgname=('manjaro-lxqt-basic-settings'
        'manjaro-lxqt-extra-settings')
pkgver=20170614
pkgrel=1
arch=('any')
url="https://github.com/manjaro/manjaro-lxqt-settings"
license=('GPL')
makedepends=('git')
source=("git+$url.git")
sha256sums=('SKIP')

_inst_dir(){
    install -d $pkgdir/etc
    cp -r $srcdir/$pkgbase/$1/skel $pkgdir/etc
}

pkgver() {
    date +%Y%m%d
}

package_manjaro-lxqt-basic-settings() {
    pkgdesc='Manjaro Linux lxqt basic settings'
    depends=('manjaro-base-skel'
            'xcursor-breeze'
            'breath-icon-theme'
            'breath-wallpaper'
            'gtk-theme-breath')
    conflicts=('manjaro-lxqt-minimal-settings')
    replaces=('manjaro-lxqt-minimal-settings')
    provides=('manjaro-desktop-settings')
    conflicts=('manjaro-desktop-settings')

    _inst_dir 'basic'
}

package_manjaro-lxqt-extra-settings() {
    pkgdesc='Manjaro Linux lxqt extra settings'
    depends=('manjaro-lxqt-basic-settings')
    conflicts=('manjaro-lxqt-settings')
    replaces=('manjaro-lxqt-settings')

    _inst_dir 'extra'
}
