# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Contributor: artoo <artoo@manjaro.org>

pkgbase=manjaro-lxqt-settings
pkgname=('manjaro-lxqt-basic-settings'
        'manjaro-lxqt-extra-settings')
pkgver=20170615
pkgrel=1
arch=('any')
url="https://github.com/manjaro/manjaro-lxqt-settings"
license=('GPL')
makedepends=('git')
source=("git+$url.git#branch=devel")
sha256sums=('SKIP')

_inst_dir(){
    if [[ -d $srcdir/$pkgbase/$1/skel ]]; then
        install -d $pkgdir/etc
        cp -r $srcdir/$pkgbase/$1/skel $pkgdir/etc
    fi

    if [[ -d $srcdir/$pkgbase/$1/scripts ]]; then
        install -d $pkgdir/usr/bin
        cp $srcdir/$pkgbase/$1/scripts/* $pkgdir/usr/bin
    fi

    if [[ -d $srcdir/$pkgbase/$1/schemas ]]; then
        install -d $pkgdir/usr/share/glib-2.0/schemas
        cp $srcdir/$pkgbase/$1/schemas/* $pkgdir/usr/share/glib-2.0/schemas
    fi

    if [[ -d $srcdir/$pkgbase/$1/bashrc.d ]]; then
        install -d $pkgdir/etc/bash/bashrc.d
        cp $srcdir/$pkgbase/$1/bashrc.d/* $pkgdir/etc/bash/bashrc.d
    fi

    if [[ -d $srcdir/$pkgbase/$1/profile.d ]]; then
        install -d $pkgdir/etc/profile.d
        cp $srcdir/$pkgbase/$1/profile.d/* $pkgdir/etc/profile.d
    fi

    if [[ -d $srcdir/$pkgbase/$1/xinitrc.d ]]; then
        install -d $pkgdir/etc/X11/xinit/xinitrc.d
        cp $srcdir/$pkgbase/$1/xinitrc.d/* $pkgdir/etc/X11/xinit/xinitrc.d
    fi
}

pkgver() {
    date +%Y%m%d
}

package_manjaro-lxqt-basic-settings() {
    pkgdesc='Manjaro Linux lxqt basic settings'
    depends=('manjaro-base-skel'
            'xcursor-breeze'
            'breath-icon-theme'
            'breath-wallpaper'
            'gtk-theme-breath')
    provides=('manjaro-desktop-settings')
    conflicts=('manjaro-lxqt-minimal-settings' 'manjaro-desktop-settings')
    replaces=('manjaro-lxqt-minimal-settings')

    _inst_dir 'basic'
}

package_manjaro-lxqt-extra-settings() {
    pkgdesc='Manjaro Linux lxqt extra settings'
    depends=('manjaro-lxqt-basic-settings')
    conflicts=('manjaro-lxqt-settings')
    replaces=('manjaro-lxqt-settings')

    _inst_dir 'extra'
}
