# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Contributor: artoo <artoo@manjaro.org>

pkgbase=desktop-settings
pkgname=('manjaro-lxqt-settings'
	'manjaro-lxqt-minimal-settings')
pkgver=20160319
pkgrel=1
arch=('any')
url="https://github.com/manjaro/desktop-settings"
license=('GPL')
makedepends=('git')
source=("git+$url.git")
sha256sums=('SKIP')

_inst_dir(){
	if [[ -d $srcdir/$pkgbase/$1/skel ]];then
		install -d $pkgdir/etc
		cp -r $srcdir/$pkgbase/$1/skel $pkgdir/etc
	fi
}

pkgver() {
	date +%Y%m%d
}

package_manjaro-lxqt-settings() {
	pkgdesc='Manjaro Linux lxqt settings'
	depends=('manjaro-base-skel')
	conflicts=('manjaro-desktop-settings')
	provides=('manjaro-desktop-settings')

	_inst_dir 'community/lxqt'
}

package_manjaro-lxqt-minimal-settings() {
	pkgdesc='Manjaro Linux lxqt-minimal settings'
# 	depends=('manjaro-openbox-settings' 'compton' 'qterminal'
# 		'pcmanfm-qt' 'lxqt-panel')
	depends=('manjaro-openbox-settings')
	conflicts=('manjaro-desktop-settings')
	provides=('manjaro-desktop-settings')

	_inst_dir 'minimal/lxqt'
}
