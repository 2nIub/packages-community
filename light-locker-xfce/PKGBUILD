# Maintainer : Rob McCathie <rob@manjaro.org>
# Contributor: Maxime Gauduin <alucryd@gmail.com>

_name=light-locker
pkgname=light-locker-xfce
pkgver=1.4.0
pkgrel=1
pkgdesc='A simple session locker, with Xfce-friendly defaults'
arch=('i686' 'x86_64')
url='https://github.com/the-cavalry/light-locker'
license=('GPL2')
depends=('dbus-glib' 'gtk3' 'libxxf86misc' 'lightdm' 'libxss')
makedepends=('gnome-common' 'intltool')
conflicts=("${_name}")
provides=("${_name}=${pkgver}")
source=("https://github.com/the-cavalry/${_name}/releases/download/v${pkgver}/${_name}-${pkgver}.tar.bz2")
sha256sums=('560f20390b24513d5bf7b691bac5d745b76eb21ac3bccd877b73cb7f6d137ec3')

prepare() {
  cd ${_name}-${pkgver}
  
  # We want xfce4-power-manager deciding when to and when not to lock, so we disable light-locker's automatic locking
  sed -i -e 's|Exec=light-locker|Exec=light-locker --no-lock-on-suspend --lock-after-screensaver=0|g' src/light-locker.desktop.in*
}

build () {
  cd ${_name}-${pkgver}

  ./configure --prefix='/usr' --sysconfdir='/etc' --localstatedir='/var' --disable-{schemas-compile,static} --with-{mit-ext,systemd,xf86gamma-ext,x} --without-{console-kit,upower}
  make
}

package() {
  cd ${_name}-${pkgver}

  make DESTDIR="${pkgdir}" install
}
