# Maintainer : Rob McCathie <archaur at rmcc dot com dot au
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Hussam Al-Tayeb ht990332@gmail.com
# Contributor: Khashayar Naderehvandi <khashayar [at] naderehvandi [dot] net>
# Contributor: JJDaNiMoTh <jjdanimoth.aur@gmail.com>
# Contributor: nesl247 <nesl247@gmail.com>
# Contributor: Florian Dejonckheere <florian@floriandejonckheere.be>


pkgname=compiz-xfce
pkgver=0.8.9
pkgrel=14
pkgdesc="Compiz compositing window manager (all required 0.8.x packages merged) - optimized for Xfce"
arch=('i686' 'x86_64')
url="http://cgit.compiz.org/"
license=('GPL' 'LGPL' 'MIT' 'CCPL:by-sa-3.0')

depends=(## compiz-core
         'startup-notification' 'librsvg' 'libgl' 'dbus' 'glu' 'libxslt' 'fuse'
         ## compiz-decorator-gtk
         'gconf' 'libwnck' 'metacity'
         ## compiz-fusion-plugins-main
         #'libjpeg>=7' 'librsvg' 'libxdamage' 'libxcomposite' 'libxinerama' 'startup-notification'
         'libjpeg>=7' 'libxdamage' 'libxcomposite' 'libxinerama'
         ## compiz-fusion-plugins-extra
         'libnotify'
         ## compiz-bcop
         #'libxslt'
         ## libcompizconfig
         #'libxml2' 'libxcomposite' 'libxinerama' 'startup-notification'
         'libxml2'
         ## compizconfig-python
         #'glib2' 'python2' 'libxrandr'
         'glib2' 'python2'
         ## ccsm
         'pygtk' 'protobuf')

makedepends=(## compiz-core and compiz-decorator-gtk
             'intltool' 'gconf' 'libwnck' 'startup-notification' 'librsvg' 'libgl' 'dbus' 'glu' 'libxslt' 'fuse' 'metacity'
             ## compiz-fusion-plugins-main
             #'intltool' 'pkg-config' 'gettext'
             'pkg-config' 'gettext'
             ## compiz-fusion-plugins-extra
             #'intltool' 'pkg-config' 'gettext' 'gconf'
             ## compiz-bcop
             #'intltool' 'pkg-config'
             ## libcompizconfig
             #'intltool' 'libxrandr' 'libice' 'libsm' 'mesa'
             'libxrandr' 'libice' 'libsm' 'mesa'
             ## compizconfig-python
             #'intltool' 'pkg-config'
             'pyrex'
             ## ccsm
             #'intltool'
             ## Make it build the first time
             'compiz-core' 'compiz-bcop' 'libcompizconfig')

optdepends=(# compiz-decorator-gtk
            'gconf-editor: Adjust window decorator settings (/apps/metacity/general/)')

conflicts=(# compiz-core
           'compiz-core' 'compiz' 'compiz-core-git' 'compiz-git'
           # compiz-decorator-gtk
           'compiz-decorator-gtk'
           # compiz-fusion-plugins-main
           'compiz-fusion-plugins-main' 'compiz-fusion-plugins-main-git'
           # compiz-fusion-plugins-extra
           'compiz-fusion-plugins-extra' 'compiz-fusion-plugins-extra-git'
           # compiz-bcop
           'compiz-bcop' 'compiz-bcop-git'
           # libcompizconfig
           'libcompizconfig' 'libcompizconfig-git'
           # compizconfig-python
           'compizconfig-python' 'compizconfig-python-git'
           # ccsm
           'ccsm' 'ccsm-git')

provides=('compiz-core=0.8.9'
          'compiz-decorator-gtk=0.8.9'
          'compiz-fusion-plugins-main=0.8.9'
          'compiz-fusion-plugins-extra=0.8.9'
          'compiz-bcop=0.8.8'
          'libcompizconfig=0.8.8'
          'compizconfig-python=0.8.5'
          'ccsm=0.8.5')

replaces=('compiz-manjaro-xfce'
          'compiz-core'
          'compiz-bcop'
          'libcompizconfig')

install="$pkgname".install

source=(# Compiz-Xfce stuff
        'compiz-xfce-decoratortheme'
        'compiz-xfce-decoratortheme.desktop'
        'compiz-xfce-autostart'
        'compiz-xfce-autostart.desktop'
        # compiz-core and compiz-decorator-gtk
        "http://cgit.compiz.org/compiz/core/snapshot/core-compiz-0.8.9.tar.bz2"
        'compiz-gcc-4.7.patch'
        'compiz-xfce-defaults.patch'
        # compiz-fusion-plugins-main
        "http://cgit.compiz.org/fusion/plugins-main/snapshot/plugins-main-611295a4d4a9dce76316056c79741c387979e730.tar.bz2"
        'expo_defaults.patch'
        # compiz-fusion-plugins-extra
        'notification.patch'
        "http://cgit.compiz.org/fusion/plugins-extra/snapshot/plugins-extra-3cbf4eccecb07fc6f7a2697a072831872d071c06.tar.bz2"
        # compiz-bcop
        "http://releases.compiz.org/0.8.8/compiz-bcop-0.8.8.tar.bz2"
        # libcompizconfig
        "http://releases.compiz.org/0.8.8/libcompizconfig-0.8.8.tar.bz2"
        # compizconfig-python
        "http://cgit.compiz.org/compiz/compizconfig/compizconfig-python/snapshot/compizconfig-python-7d36bcb9d0c44bf1bfc905e60f809934fdee840d.tar.bz2"
        # ccsm
        "http://cgit.compiz.org/compiz/compizconfig/ccsm/snapshot/ccsm-2513000801e72a0e035ba408f5a5fa7458c19c30.tar.bz2"
        # greybird window decorations
        "xfce-theme-greybird.tar.gz::https://github.com/shimmerproject/Greybird/archive/v1.2.2.tar.gz")

sha1sums=(# Compiz-Xfce stuff
          'a82132ce19f3ffb82e28655c3ddd27f857204cca'
          'f8a5f5717e803724036d2bd435bd79e4456adf8e'
          '98cdb40d2fe6c64be8df13ac33584cce00d4e4bf'
          '53076e423aee58c7d78b3e13ae97cf9fcb9afa19'
          # compiz-core and compiz-decorator-gtk
          'f48b7cb7e98efd4e6967f07967ac9078127123c0'
          '2138342ae9f253bae003e96e91fdd0bc9918291a'
          '59f37feafedd0cb949498fcbaebe3a298aab3af6'
          # compiz-fusion-plugins-main
          '1c1b72f3e9b9045ffcaa35958db637ecaf7c048d'
          '6cbff060ef855997d504b27d0f30d4ee775c2ba4'
          # compiz-fusion-plugins-extra
          '11ea02ce12711df9f5e306d96036a7d4b1c4a217'
          '5347c50b823ef2abcc17737d6dc90b2ec5ed5e03'
          # compiz-bcop
          '9210bd2a537480bdb11df0e3cc5f6d4548fd12da'
          # libcompizconfig
          '8ea1f15fd9e1e2eb37a9814646659a31a8018fd8'
          # compizconfig-python
          'e0dd434c9a6d4cd64bf6628aa6ebd38fde23d5a5'
          # ccsm
          '2f79b1130c457a3c48bb3e3bdfa8c5b544e1c3c9'
          # greybird window decorations
          '57a827b34b58f03e692262a6746b394cb70149dc')

prepare() {
  ### compiz-core and compiz-decorator-gtk ###
  cd "$srcdir/core-compiz-0.8.9"
  patch -p0 -i ../compiz-gcc-4.7.patch
  # Some sane defaults for Xfce
  patch -Np1 -i "${srcdir}/compiz-xfce-defaults.patch"
  
  ### compiz-fusion-plugins-main ###
  cd "$srcdir/plugins-main-611295a4d4a9dce76316056c79741c387979e730"
  # Turn off expo reflection
  patch -Np1 -i "${srcdir}/expo_defaults.patch"
  
  ### compiz-fusion-plugins-extra ###
  cd "$srcdir/plugins-extra-3cbf4eccecb07fc6f7a2697a072831872d071c06"
  patch -p1 -i $srcdir/notification.patch
  sed -i 's/iconUri, NULL);/iconUri);/' src/notification/notification.c
  
  ### ccsm ###
  cd "$srcdir/ccsm-2513000801e72a0e035ba408f5a5fa7458c19c30"
  # python2 fix
  for file in $(find . -name '*.py' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done
}

build() {
  ### compiz-core and compiz-decorator-gtk ###
  cd "$srcdir/core-compiz-0.8.9"
  ./autogen.sh   --prefix=/usr \
	    --enable-gtk \
	    --enable-metacity \
	    --enable-gconf \
            --with-gconf-schema-file-dir=/etc/gconf/schemas \
	    --enable-dbus \
	    --enable-librsvg \
	    --disable-gnome \
	    --disable-kde \
	    --disable-kde4 \
	    --with-default-plugins="ccp,decoration,resize,place,move,regex,fade,expo,switcher"
  make QDBUSXML2CPP=/usr/bin/qdbusxml2cpp-qt4
  
  ### compiz-fusion-plugins-main ###
  cd "$srcdir/plugins-main-611295a4d4a9dce76316056c79741c387979e730"
  ./autogen.sh --prefix=/usr --sysconfdir=/etc
  make
  
  ### compiz-fusion-plugins-extra ###
  cd "$srcdir/plugins-extra-3cbf4eccecb07fc6f7a2697a072831872d071c06"
  ./autogen.sh --prefix=/usr
  make
  
  ### compiz-bcop ###
  cd "$srcdir/compiz-bcop-0.8.8"
  ./configure --prefix=/usr
  make
  
  ### libcompizconfig ###
  cd "$srcdir/libcompizconfig-0.8.8"
  CPPFLAGS="$CPPFLAGS -I/usr/include/compiz -I/usr/include/startup-notification-1.0 -I/usr/include/libxml2" ./configure --prefix=/usr --sysconfdir=/etc
  make
  
  ### compizconfig-python ###
  cd "$srcdir/compizconfig-python-7d36bcb9d0c44bf1bfc905e60f809934fdee840d"
  PYTHON=python2 ./autogen.sh --prefix=/usr
  make
}

package() {
  ### Compiz-Xfce stuff ###
  # Place script that sets Metacity theme to Adwaita if it's currently unset and we're on Xfce
  install -Dm755 "${srcdir}"/compiz-xfce-decoratortheme "${pkgdir}"/usr/bin/compiz-xfce-decoratortheme
  install -Dm644 "${srcdir}"/compiz-xfce-decoratortheme.desktop "${pkgdir}"/etc/xdg/autostart/compiz-xfce-decoratortheme.desktop
  install -Dm755 "${srcdir}"/compiz-xfce-autostart "${pkgdir}"/usr/bin/compiz-xfce-autostart
  install -Dm644 "${srcdir}"/compiz-xfce-autostart.desktop "${pkgdir}"/etc/xdg/autostart/compiz-xfce-autostart.desktop
  
  ### compiz-core and compiz-decorator-gtk ###
  cd "$srcdir/core-compiz-0.8.9"
  make DESTDIR="$pkgdir" install
  pushd gtk
  make DESTDIR="$pkgdir" install
  install -Dm644 window-decorator/gwd.schemas "$pkgdir/usr/share/gconf/schemas/gwd.schemas"
  popd
  for i in dbus gconf ini inotify png regex svg glib kconfig
  do
    rm "$srcdir"/core-compiz-0.8.9/metadata/compiz-$i.schemas
  done
  gconf-merge-schema "${pkgdir}"/usr/share/gconf/schemas/compiz-decorator-gtk.schemas "${srcdir}"/core-compiz-0.8.9/metadata/*.schemas
  make DESTDIR="$pkgdir" install
  # install MIT license
  install -Dm644 "$srcdir/core-compiz-0.8.9/COPYING.MIT" "$pkgdir/usr/share/licenses/compiz/COPYING.MIT"
  
  ### compiz-fusion-plugins-main ###
  cd "$srcdir/plugins-main-611295a4d4a9dce76316056c79741c387979e730"
  make DESTDIR="${pkgdir}" install
  
  ### compiz-fusion-plugins-extra ###
  cd "$srcdir/plugins-extra-3cbf4eccecb07fc6f7a2697a072831872d071c06"
  make DESTDIR="${pkgdir}" install
  
  ### compiz-bcop ###
  cd "$srcdir/compiz-bcop-0.8.8"
  make DESTDIR="${pkgdir}" install
  
  ### libcompizconfig ###
  cd "$srcdir/libcompizconfig-0.8.8"
  make DESTDIR="${pkgdir}" install
  
  ### compizconfig-python ###
  cd "$srcdir/compizconfig-python-7d36bcb9d0c44bf1bfc905e60f809934fdee840d"
  make DESTDIR="${pkgdir}" install
  
  ### ccsm ###
  cd "$srcdir/ccsm-2513000801e72a0e035ba408f5a5fa7458c19c30"
  python2 ./setup.py install --prefix=/usr --root="${pkgdir}"
  # Make ccsm icon appear in Xfce settings manager
  sed -i 's|Categories=Compiz;Settings;DesktopSettings;|Categories=Compiz;Settings;DesktopSettings;X-XFCE-SettingsDialog;X-XFCE-OtherSettings;|' "${pkgdir}"/usr/share/applications/ccsm.desktop
  
  # greybird window decorations
  cd "$srcdir/Greybird-1.2.2"
  mkdir -p "$pkgdir/usr/share/themes/compiz-xfce-greybird"
  cp LICENSE* "$pkgdir/usr/share/themes/compiz-xfce-greybird"
  cp -r metacity-1/ "$pkgdir/usr/share/themes/compiz-xfce-greybird"
  rm "$pkgdir/usr/share/themes/compiz-xfce-greybird/metacity-1/metacity-theme-3.xml"
  sed -i 's|Greybird|compiz-xfce-greybird|' "$pkgdir/usr/share/themes/compiz-xfce-greybird/metacity-1/metacity-theme-2.xml"
}
