# Maintainer : Rob McCathie <archaur at rmcc dot com dot au>

_name=compiz
pkgname=compiz-xfce
pkgver=0.8.9
pkgrel=22
arch=('any')
url="http://cgit.compiz.org/"
license=('GPL' 'CCPL:by-sa-3.0')
#makedepends=('')

source=('compiz-xfce-decoratortheme'
        'compiz-xfce-decoratortheme.desktop'
        'compiz-xfce-autostart'
        'compiz-xfce-autostart.desktop'
        "xfce-theme-greybird.tar.gz::https://github.com/shimmerproject/Greybird/archive/v1.2.2.tar.gz")

sha1sums=('cba83df1e13096dfc7decc4f4a619beb2368ccde'
          '9a9b28143e237834e98c82bb677a1cff62dc3af9'
          '46dc02d4033849cda551e930f772dc73fdf12c36'
          'a790b6556695b06af4eff910e7dc409aab85f9a8'
          '57a827b34b58f03e692262a6746b394cb70149dc')

pkgdesc="Compiz meta package for Xfce, including autostart and optimisations"
depends=('compiz-gtk-standalone' 'compiz-fusion-plugins-main' 'compiz-fusion-plugins-extra' 'ccsm')
optdepends=("compiz-fusion-plugins-unsupported: Compiz unsupported plugins")
install="$pkgname".install

package() {
  # Place autostart enablers/scripts
  install -Dm755 "${srcdir}"/compiz-xfce-decoratortheme "${pkgdir}"/usr/bin/compiz-xfce-decoratortheme
  install -Dm644 "${srcdir}"/compiz-xfce-decoratortheme.desktop "${pkgdir}"/etc/xdg/autostart/compiz-xfce-decoratortheme.desktop
  install -Dm755 "${srcdir}"/compiz-xfce-autostart "${pkgdir}"/usr/bin/compiz-xfce-autostart
  install -Dm644 "${srcdir}"/compiz-xfce-autostart.desktop "${pkgdir}"/etc/xdg/autostart/compiz-xfce-autostart.desktop
  
  # greybird window decorations
  cd "$srcdir/Greybird-1.2.2"
  mkdir -p "$pkgdir/usr/share/themes/compiz-xfce-greybird"
  cp LICENSE* "$pkgdir/usr/share/themes/compiz-xfce-greybird"
  cp -r metacity-1/ "$pkgdir/usr/share/themes/compiz-xfce-greybird"
  rm "$pkgdir/usr/share/themes/compiz-xfce-greybird/metacity-1/metacity-theme-3.xml"
  sed -i 's|Greybird|compiz-xfce-greybird|' "$pkgdir/usr/share/themes/compiz-xfce-greybird/metacity-1/metacity-theme-2.xml"
}
