# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: FadeMind <fademind@gmail.com>
# Contributor: Zatherz <zatherz at linux dot pl>

pkgname=qt5ct
pkgver=0.28
pkgrel=1.1
pkgdesc="Qt5 Configuration Utility"
arch=(i686 x86_64)
url="http://qt5ct.sourceforge.net"
license=(BSD)
depends=(qt5-svg)
makedepends=(qt5-tools)
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname-$pkgver.tar.bz2"
        "https://sourceforge.net/p/qt5ct/tickets/30/attachment/qt5ct-plugindir.patch")
sha256sums=('d75216135c112657d10b1c87502ba8c9510340a7b868211770ccd85aa777f0ca'
            '01077a4da1931e03b479a6c368c6bd1ee740817523acefeab69cd24a247061ac')

prepare() {
    cd $pkgname-$pkgver
    patch -p1 -i "$srcdir"/qt5ct-plugindir.patch
}
build() {
    cd $pkgname-$pkgver
    qmake-qt5 $pkgname.pro 
    make
}

package() {
    cd $pkgname-$pkgver
    make INSTALL_ROOT="$pkgdir" install

    install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
} 

