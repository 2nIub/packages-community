# Maintainer : Rob McCathie <rob@manjaro.org>

pkgname=adwaita-manjaro-theme
pkgver=20160411
pkgrel=5
_basegtkver=3.20
_fullgtkver=${_basegtkver}.2
_maiacommit=cec8588ec16090335591189e43411a3f474c3f71
pkgdesc="Adwaita-Manjaro theme"
arch=('any')
url="https://www.gnome.org/"
license=('GPL')
makedepends=('imagemagick')
depends=('gtk-engine-murrine')
source=("https://git.gnome.org/browse/gtk+/snapshot/gtk+-${_fullgtkver}.tar.xz"
        "http://mirror.netzspielplatz.de/manjaro/packages/unstable/extra/x86_64/gnome-themes-standard-${_basegtkver}-1-x86_64.pkg.tar.xz"
        "https://github.com/manjaro/artwork-vertex-maia/archive/${_maiacommit}.zip"
        "index.theme"
        "panel.rc"
        "add-xfce-panel.patch")
sha256sums=('ccfa0bf5509e120b3e249f20cedaa72d47b03ac998a5a98b6dc5ce9c846681dd'
            '940b6d8d501f55a60279b4c0a1b591300e4c7a4083bbf4454b1820f42cd05703'
            '2af9a90a51d6c33c983339c86e958bc8f4755dba99bdf4f387658f184deb4be9'
            '32d051214ab29df129f62a44dc49973f40be131078796182da7619c0cfb58c4a'
            '5f980512053a1694b3a5dc1ac32ad2fa3f67db4eed9d481b8804fc59394c20ee'
            '35fff2f7341c989ad8a18c372e616abd33f6b8e030662183e65081ff175d44a1')
options=('!strip')

package() {
  mkdir -p "${pkgdir}/usr/share/themes/Adwaita-Manjaro"
  cp "${srcdir}/index.theme" "${pkgdir}/usr/share/themes/Adwaita-Manjaro/"
  cp -r "${srcdir}/usr/share/themes/Adwaita/gtk-2.0" "${pkgdir}/usr/share/themes/Adwaita-Manjaro/"
  cp -r "${srcdir}/gtk+-${_fullgtkver}/gtk/theme/Adwaita" "${pkgdir}/usr/share/themes/Adwaita-Manjaro/gtk-3.0"
  
  cp "${srcdir}/panel.rc" "${pkgdir}/usr/share/themes/Adwaita-Manjaro/gtk-2.0/"
  echo 'include "panel.rc"' >> "${pkgdir}/usr/share/themes/Adwaita-Manjaro/gtk-2.0/gtkrc"
  
  cd "${pkgdir}/usr/share/themes/Adwaita-Manjaro/gtk-3.0"
  find . -maxdepth 1 -type f -not -name '*contained*' -exec rm {} \;
  rm *.scss
  mv gtk-contained.css gtk.css
  mv gtk-contained-dark.css gtk-dark.css
  patch -p1 -i "${srcdir}/add-xfce-panel.patch"
  
  cd ..
  sed -i "s/4a90d9/16a085/g" gtk-2.0/gtkrc
  sed -i "s/4a90d9/16a085/g" gtk-3.0/gtk.css
  sed -i "s/4a90d9/16a085/g" gtk-3.0/gtk-dark.css
  
  msg "This next bit might take a little while..."

  ## For all PNG and SVG images swap the blue and green channels, making everything green instead of blue.
  find . -name "*.png" -exec mogrify -channel rbga -separate -swap 1,2 -combine {} \;
  find . -name "*.svg" -exec mogrify -background none -channel rbga -separate -swap 1,2 -combine {} \;
  
  ## Xfwm4 and Metacity decorations
  cp -r "${srcdir}/artwork-vertex-maia-${_maiacommit}/Vertex-Maia-Square/xfwm4" ./
  cp -r "${srcdir}/artwork-vertex-maia-${_maiacommit}/Vertex-Maia-Square/metacity-1" ./
  sed -i "s|<name>Vertex-Maia-SQ-B</name>|<name>Adwaita-Manjaro</name>|g" metacity-1/metacity-theme-2.xml
}
