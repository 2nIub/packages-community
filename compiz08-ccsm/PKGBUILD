# Maintainer : Rob McCathie <rob@manjaro.org>
# Contributor: OS Hazard <oshazard+aur@gmail.com>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: JJDaNiMoTh <jjdanimoth@gmail.com>
# Contributor: nesl247 <nesl247@gmail.com>

pkgname=compiz08-ccsm
_pkgname=ccsm
pkgver=0.8.10
pkgrel=1
pkgdesc="Compizconfig Settings Manager in Python2"
arch=('any')
url="http://www.northfield.ws/projects/compiz/releases/0.8.10/"
license=('GPL')
depends=('compiz08-compizconfig-python>=0.8.10' 'pygtk' 'protobuf' 'gtk-update-icon-cache')
makedepends=('intltool')
replaces=("${_pkgname}")
provides=("${_pkgname}=${pkgver}")
conflicts=("${_pkgname}")
source=("http://www.northfield.ws/projects/compiz/releases/${pkgver}/${_pkgname}.tar.gz")
install="${_pkgname}.install"
sha1sums=('24fc8c0b17d5c93fed82d5ad4bc34cfea0324876')

prepare() {
  cd "${srcdir}/${_pkgname}"
  # python2 fix
  for file in $(find . -name '*.py' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done
}

package() {
  cd "${srcdir}/${_pkgname}"
  python2 ./setup.py install --prefix=/usr --root="${pkgdir}"
  # Make ccsm icon appear in Xfce settings manager
  sed -i 's|Categories=Compiz;Settings;DesktopSettings;|Categories=Compiz;Settings;DesktopSettings;X-XFCE-SettingsDialog;X-XFCE-OtherSettings;|' "${pkgdir}/usr/share/applications/ccsm.desktop"
}
