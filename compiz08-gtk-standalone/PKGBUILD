# Maintainer : Rob McCathie <archaur at rmcc dot com dot au
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Hussam Al-Tayeb ht990332@gmail.com
# Contributor: Khashayar Naderehvandi <khashayar [at] naderehvandi [dot] net>
# Contributor: JJDaNiMoTh <jjdanimoth.aur@gmail.com>

pkgname=compiz08-gtk-standalone
_pkgname=compiz-gtk-standalone
pkgdesc="Compiz compositing window manager with gtk decorator. No specific DE integration. Sane defaults. Good for minimal Compiz setups."
pkgver=0.8.10
pkgrel=1
arch=('i686' 'x86_64')
url="http://cgit.compiz.org/"
license=('GPL' 'LGPL' 'MIT')
depends=('startup-notification' 'librsvg' 'libgl' 'dbus' 'glu' 'libxslt' 'fuse' 'libsm' 
         'gconf' 'libwnck' 'metacity2' 'zenity' 'procps-ng' 'desktop-file-utils')
makedepends=('intltool')
optdepends=("compiz08-fusion-plugins-main: Compiz Main plugins"
            "compiz08-fusion-plugins-extra: Compiz Extra plugins"
            "compiz08-fusion-plugins-unsupported: Unsupported Compiz plugins"
            "compiz08-ccsm: CompizConfig Settings Manager in Python2")
replaces=("${_pkgname}")
provides=("${_pkgname}=${pkgver}" "compiz08-core=${pkgver}" "compiz-core=${pkgver}" 'compiz08-decorator-gtk' 'compiz-decorator-gtk')
conflicts=("${_pkgname}" 'compiz08-core' 'compiz-core' 'compiz08-decorator-gtk' 'compiz-decorator-gtk')
install="${_pkgname}.install"

source=("http://www.northfield.ws/projects/compiz/releases/${pkgver}/core.tar.gz"
        'compiz-standalone-defaults.patch'
        'compiz-gtk-decorator-theme-selector'
        'compiz-gtk-decorator-theme.desktop')

sha1sums=('8487aa83ff46d0fcef6a21591b56f4acdd8016df'
          '59f37feafedd0cb949498fcbaebe3a298aab3af6'
          'fe98e5f52d93166a9966d03486a486420df6f706'
          '9b3dd487e9142e826848dc6b5d95a98fbb648f3c')

prepare() {
  cd "${srcdir}/core"
  # Some sane defaults
  patch -p1 -i "${srcdir}/compiz-standalone-defaults.patch"
}

build() {
  cd "${srcdir}/core"
  ./autogen.sh \
    --prefix=/usr \
    --enable-gtk \
    --enable-metacity \
    --enable-gconf \
    --with-gconf-schema-file-dir=/etc/gconf/schemas \
    --enable-dbus \
    --enable-librsvg \
    --disable-gnome \
    --disable-kde \
    --disable-kde4 \
    --with-default-plugins="ccp,decoration,resize,place,move,regex,fade,expo,switcher"
  make QDBUSXML2CPP=/usr/bin/qdbusxml2cpp-qt4
}

package() {  
  cd "${srcdir}/core"

  install -Dm644 gtk/window-decorator/gwd.schemas "${pkgdir}/usr/share/gconf/schemas/gwd.schemas"

  for i in dbus gconf ini inotify png regex svg glib kconfig
  do
    rm "${srcdir}"/core/metadata/compiz-$i.schemas
  done
  gconf-merge-schema "${pkgdir}"/usr/share/gconf/schemas/compiz-decorator-gtk.schemas "${srcdir}"/core/metadata/*.schemas
  make DESTDIR="${pkgdir}" install

  # install MIT license
  install -Dm644 "${srcdir}/core/COPYING.MIT" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING.MIT"
  
  install -Dm755 "${srcdir}/compiz-gtk-decorator-theme-selector" "${pkgdir}/usr/bin/compiz-gtk-decorator-theme-selector"
  install -Dm644 "${srcdir}/compiz-gtk-decorator-theme.desktop" "${pkgdir}/usr/share/applications/compiz-gtk-decorator-theme.desktop"
}
