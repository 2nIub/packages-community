# Maintainer: Rob McCathie <rob@manjaro.org>

pkgname=manjaro-netbook-system
pkgver=20160427
pkgrel=1
pkgdesc="Manjaro Netbook System - Components and update script"
arch=('any')
url="http://www.manjaro.org"
license=('GPL')
depends=('grub')
install="${pkgname}.install"
source=("manjaro-netbook-compton-autostart"
        "manjaro-netbook-update-helper"
        "compton-manjaro-netbook.desktop"
        "manjaro-netbook-update-helper.desktop"
        "nitrogen-restore-manjaro-netbook.desktop"
        "nitrogen-manjaro-netbook.desktop"
        "close_active-manjaro-netbook.desktop"
        "unmaximize_active-manjaro-netbook.desktop"
        "gnome-keyring-daemon-fix.desktop")
sha1sums=('747cbc5665014693ed219b2ae18c811b35915bce'
          'aff8bd2194ec6c28e150462d85fb4683d9e3d53a'
          '28e67491a40df8fa58ce0945f81ba26a0b060aa5'
          '1c4a380126b85d1f9347164857697f194852eafd'
          '5c108e096d02461efc6f346096a27555a4f2724d'
          '234a8d6826aa6ba2c40b0fcc07bf63e430a2c31e'
          'a01dd414dbb36bd2232bf6188aef043f9ecb4956'
          '8e573c9c6b82decec0af41de4976e9123648443f'
          '9b02f1c6b63889a1ec304b9c217936ba462d96a0')

package() {
  # Compton startup script to not use GLX backend if LLVMpipe is in use
  install -Dm755 "${srcdir}/manjaro-netbook-compton-autostart" "${pkgdir}/usr/bin/manjaro-netbook-compton-autostart"
  
  # Update-helper script
  install -Dm755 "${srcdir}/manjaro-netbook-update-helper" "${pkgdir}/usr/bin/manjaro-netbook-update-helper"
  # Write current version+rel to update helper script
  sed -e "s/_autover=.*/_autover=${pkgver}-${pkgrel}/g" -i "${pkgdir}/usr/bin/manjaro-netbook-update-helper"
  
  # XDG autostart files
  install -Dm644 "${srcdir}/compton-manjaro-netbook.desktop" "${pkgdir}/etc/xdg/autostart/compton-manjaro-netbook.desktop"
  install -Dm644 "${srcdir}/manjaro-netbook-update-helper.desktop" "${pkgdir}/etc/xdg/autostart/manjaro-netbook-update-helper.desktop"
  install -Dm644 "${srcdir}/nitrogen-restore-manjaro-netbook.desktop" "${pkgdir}/etc/xdg/autostart/nitrogen-restore-manjaro-netbook.desktop"
  #install -Dm644 "${srcdir}/gnome-keyring-daemon-fix.desktop" "${pkgdir}/etc/xdg/autostart/gnome-keyring-daemon-fix.desktop"
  
  # XDG launchers
  install -Dm644 "${srcdir}/nitrogen-manjaro-netbook.desktop" "${pkgdir}/usr/share/applications/nitrogen-manjaro-netbook.desktop"
  install -Dm644 "${srcdir}/close_active-manjaro-netbook.desktop" "${pkgdir}/usr/share/applications/close_active-manjaro-netbook.desktop"
  install -Dm644 "${srcdir}/unmaximize_active-manjaro-netbook.desktop" "${pkgdir}/usr/share/applications/unmaximize_active-manjaro-netbook.desktop"
  
  # Update tracking files for userdata update helper script
  mkdir -p "${pkgdir}/usr/share/manjaro-netbook-update-helper"
  touch "${pkgdir}/usr/share/manjaro-netbook-update-helper/20160427-1"
}
