# Maintainer : Philip MÃ¼ller <philm@manjaro.org>
pkgname=sddm-kcm
pkgver=0.1.0.r130.5fab0a1
_pkgver=0.1.0
pkgrel=1
pkgdesc="SDDM configuration module for KDE"
arch=('i686' 'x86_64')
url="https://github.com/sddm/sddm-kcm"
license=("GPL")
depends=('sddm' 'kdelibs4support' 'libxcursor')
makedepends=('git' 'extra-cmake-modules' 'kdoctools' 'qt5-tools')
conflicts=('sddm-kcm' 'kf5-sddm-kcm')
replaces=('kf5-sddm-kcm')
source=('git://anongit.kde.org/sddm-kcm')
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  printf "$_pkgver.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  mkdir -p build
  cd build
  cmake ../${pkgname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}