# Maintainer: Ramon Buld√≥ <rbuldo@gmail.com>

pkgbase=manjaro-xfce-settings
pkgname=('manjaro-xfce-basic-settings'
         'manjaro-xfce-extra-settings')
pkgver=20170614
pkgrel=1
pkgdesc="Manjaro Linux Xfce settings"
arch=('any')
url="https://github.com/manjaro/manjaro-xfce-settings"
license=('GPL')
source=("git+$url.git#branch=devel")
sha256sums=('SKIP')

pkgver() {
	date +%Y%m%d
}

_inst_dir(){
    if [[ -d $srcdir/$pkgbase/$1/skel ]]; then
        install -d $pkgdir/etc
        cp -r $srcdir/$pkgbase/$1/skel $pkgdir/etc
    fi

    if [[ -d $srcdir/$pkgbase/$1/scripts ]]; then
        install -d $pkgdir/usr/bin
        cp $srcdir/$pkgbase/$1/scripts/* $pkgdir/usr/bin
    fi

    if [[ -d $srcdir/$pkgbase/$1/schemas ]]; then
        install -d $pkgdir/usr/share/glib-2.0/schemas
        cp $srcdir/$pkgbase/$1/schemas/* $pkgdir/usr/share/glib-2.0/schemas
    fi

    if [[ -d $srcdir/$pkgbase/$1/bashrc.d ]]; then
        install -d $pkgdir/etc/bash/bashrc.d
        cp $srcdir/$pkgbase/$1/bashrc.d/* $pkgdir/etc/bash/bashrc.d
    fi

    if [[ -d $srcdir/$pkgbase/$1/profile.d ]]; then
        install -d $pkgdir/etc/profile.d
        cp $srcdir/$pkgbase/$1/profile.d/* $pkgdir/etc/profile.d
    fi

    if [[ -d $srcdir/$pkgbase/$1/xinitrc.d ]]; then
        install -d $pkgdir/etc/X11/xinit/xinitrc.d
        cp $srcdir/$pkgbase/$1/xinitrc.d/* $pkgdir/etc/X11/xinit/xinitrc.d
    fi
}

package_manjaro-xfce-basic-settings() {
    pkgdesc='Manjaro Linux xfce-basic settings'
    depends=('breath-wallpaper'
                'manjaro-icons'
                'qt5ct'
                'qt5-styleplugins'
                'vertex-maia-icon-theme'
                'vertex-maia-themes'
                'xcursor-breeze')
    provides=('manjaro-desktop-settings')
    conflicts=('manjaro-desktop-minimal-settings' 'manjaro-desktop-settings')
    replaces=('manjaro-desktop-minimal-settings')

    _inst_dir 'basic'
}

package_manjaro-xfce-extra-settings() {
    pkgdesc='Manjaro Linux xfce-extra settings'
    depends=('manjaro-xfce-basic-settings')
    conflicts=('manjaro-xfce-settings')
    replaces=('manjaro-xfce-settings')


    _inst_dir 'extra'
}
