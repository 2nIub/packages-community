# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-mutter-manjaro
pkgver=3.18.1.1
pkgrel=1
pkgdesc="Base window manager for deepin, fork of gnome mutter"
arch=('i686' 'x86_64')
url="https://gitcafe.com/Deepin/deepin-mutter"
license=('GPL3')
depends=('clutter'
	'dconf'
	'gobject-introspection-runtime'
	'deepin-desktop-schemas-manjaro'
	'libcanberra'
	'startup-notification'
	'zenity'
	'libsm'
	'gnome-desktop'
	'upower'
	'libxkbcommon-x11'
	'libgudev')
makedepends=('gnome-common'
	'gnome-doc-utils'
	'gobject-introspection'
	'gtk-doc'
	'intltool'
	'git')
options=('!emptydirs')
install="${pkgname%-manjaro}.install"
source=("git+https://github.com/linuxdeepin/deepin-mutter.git#tag=$pkgver")
md5sums=('SKIP')

build() {
  cd "${pkgname%-manjaro}"
  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/deepin-mutter \
    --localstatedir=/var \
    --enable-gtk-doc \
    --enable-wayland \
    --enable-native-backend \
    --disable-static \
    --disable-schemas-compile \
    --enable-compile-warnings=minimum
  make
}

package() {
  cd "${pkgname%-manjaro}"
  make DESTDIR="${pkgdir}" install
}
