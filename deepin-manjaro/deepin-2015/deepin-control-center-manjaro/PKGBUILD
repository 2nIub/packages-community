# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Maintainer: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-control-center-manjaro
_pkgname=dde-control-center
pkgver=2.91.2
_commit=f8b2203f51c0ec106f6f6f3d43a170bc32db3ba7
pkgrel=1
pkgdesc='New control center for linux deepin'
arch=('i686' 'x86_64')
url="http://github.com/linuxdeepin/dde-control-center"
license=('GPL3')
depends=('desktop-file-utils'
	'libdui'
	'gtk3')
makedepends=('deepin-dock-manjaro'
	'deepin-gettext-tools'
	'git')
conflicts=('deepin-manjaro-control-center' 'dde-control-center')
install=$_pkgname.install
source=("git+https://github.com/linuxdeepin/$_pkgname.git#commit=$_commit")
sha256sums=('SKIP')

build(){
	cd "${_pkgname}"
	qmake-qt5 PREFIX=/usr WITH_MODULE_GRUB=NO
	make
}

package() {
  depends+=('dde-account-faces'
	'deepin-api-manjaro'
	'deepin-daemon-manjaro'
	'deepin-dbus-factory')
  cd "${_pkgname}"
  make INSTALL_ROOT="${pkgdir}" install
}
