# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-control-center
pkgver=4.2.3
pkgrel=4
pkgdesc='New control center for linux deepin'
arch=('i686' 'x86_64')
url="http://github.com/linuxdeepin/dde-control-center"
license=('GPL3')
depends=('desktop-file-utils' 'deepin-tool-kit' 'deepin-account-faces' 'deepin-api' 'geoip'
         'geoip-database' 'geoip-database-extra' 'deepin-daemon' 'startdde'
         'deepin-qt-dbus-factory' 'deepin-qt5integration')
makedepends=('deepin-dock' 'qt5-tools')
conflicts=('dde-control-center' 'dde-dock-applets')
replaces=('dde-control-center' 'dde-dock-applets')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::http://github.com/linuxdeepin/dde-control-center/archive/$pkgver.tar.gz"
	'rm_update_module.patch')
sha512sums=('d235b0671f9fe63d7b4fef2c64eec160204598c5f0b9c53120ff5d711692f7d4a4fc6ca7c2ecc97605253ce70787d65457b136950431021089ee4cfde47970cf'
            'bdf28ef3ce4cd8aa92b58f2b77c53c65852c65e49df86886dc27872e0b2c00b721179f803bd419cbaa45e3e671fd904ef51927ef570bea7fbd645bafd27ee6a6')

prepare() {
  cd dde-control-center-$pkgver
  patch -p1 -i $srcdir/rm_update_module.patch
}

build() {
  cd dde-control-center-$pkgver
  sed -i '/checkNotify/d' plugins/notify/notifymanager.h
  sed -e '/replyFinished/d' -e '/slotTimeout/d' -i plugins/weather/weatherrequest.h
  qmake-qt5 QMAKE_CFLAGS_ISYSTEM= PREFIX=/usr DISABLE_SYS_UPDATE=YES
  make
}

package() {
  cd dde-control-center-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
