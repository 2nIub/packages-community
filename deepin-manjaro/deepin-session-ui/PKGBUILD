# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-session-ui
pkgver=4.2.0.20180313
pkgrel=1
_commit=aecb143c528d7fe151ceb7886bb6f342cf126025
pkgdesc='Deepin desktop-environment - Session UI module'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-session-ui"
license=('GPL3')
groups=('deepin')
depends=('gsettings-qt' 'deepin-qt5integration' 'liblightdm-qt5' 'qt5-svg' 'deepin-daemon'
        'xorg-server' 'deepin-control-center' 'startdde' 'xorg-xrandr' 'deepin-qt-dbus-factory')
makedepends=('qt5-tools' 'deepin-gettext-tools')
provides=('lightdm-deepin-greeter')
conflicts=('dde-workspace' 'dde-session-ui')
replaces=('dde-workspace' 'dde-session-ui')
source=("$pkgname-$_commit.tar.gz::https://github.com/felixonmars/dde-session-ui/archive/$_commit.tar.gz"
        'rm_unneeded.patch')
sha256sums=('2ae5181ea5b4f511b4cb95065b2627e2d2d175b0e83d1f5e40399212e39b9128'
            '2c15090dee295efa5a9666eee87ea2ab8e3360e8b2272fb86182ad3d9a718bb5')
prepare() {
  cd dde-session-ui-$_commit
  patch -p1 -i $srcdir/rm_unneeded.patch
}

build() {
  cd dde-session-ui-$_commit
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-session-ui-$_commit
  make INSTALL_ROOT="$pkgdir" install
}
