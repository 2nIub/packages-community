# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-session-ui
_pkgname=$pkgname-manjaro
pkgver=4.0.9
pkgrel=1
pkgdesc='Deepin desktop-environment - Session UI module'
arch=('i686' 'x86_64')
url='https://github.com/oberon2007/deepin-session-ui-manjaro'
license=('GPL3')
groups=('deepin')
depends=('deepin-control-center'
    'deepin-daemon'
    'deepin-qt-dbus-factory'
    'deepin-tool-kit'
    'gsettings-qt'
    'gtk2'
    'liblightdm-qt5'
    'qt5-svg'
    'startdde'
    'xorg-xrandr')
makedepends=('qt5-tools' 'deepin-gettext-tools')
provides=('lightdm-deepin-greeter')
conflicts=('dde-workspace' 'dde-session-ui')
replaces=('dde-workspace' 'dde-session-ui')
source=("$url/archive/$pkgver.tar.gz")
sha512sums=('27d5f5a59887257b72ea8194406b4dd34888de567a29e08edd2408a8f29d648d3563dbf7be4ac4592179eb303deaa0f00205f40aabdac57b151889c75ec045d6')

build() {
  cd $_pkgname-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd $_pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
