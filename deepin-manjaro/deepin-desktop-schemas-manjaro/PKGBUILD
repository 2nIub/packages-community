# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

_pkgname=deepin-desktop-schemas
pkgname=$_pkgname-manjaro
pkgver=3.1.13
pkgrel=1
pkgdesc='Manjaro Linux Deepin gschemas'
arch=('any')
url="https://github.com/linuxdeepin/$_pkgname"
license=('GPL3')
groups=('deepin-manjaro')
depends=('dconf'
    'deepin-artwork-themes'
	'deepin-iconthemes-manjaro'
	'deepin-sound-theme'
	'deepin-wallpapers'
	'noto-fonts')
makedepends=('git')
conflicts=('dde-daemon<=2.92.1' "$_pkgname")
provides=("$_pkgname")
source=("git+https://cr.deepin.io/deepin-desktop-schemas.git#tag=$pkgver"
	'manjaro.diff')
sha256sums=('SKIP'
            'ed9bfc355ec4500cbc46f34d34109310372adcceb2455974c1207dbd4efd2076')

prepare() {
  cd $_pkgname
  patch -p1 < ../manjaro.diff
}

build() {
  cd $_pkgname
  make ARCH=x86
}

package() {
  cd $_pkgname
  make DESTDIR=$pkgdir install
}
