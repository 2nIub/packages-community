# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

_pkgname=deepin-desktop-schemas
pkgname=$_pkgname-manjaro
pkgver=3.2.0
pkgrel=1
pkgdesc='Manjaro Linux Deepin gschemas'
arch=('any')
url="https://github.com/linuxdeepin/$_pkgname"
license=('GPL3')
groups=('deepin-manjaro')
depends=('dconf'
    'deepin-artwork-themes'
    'deepin-iconthemes-manjaro'
    'deepin-sound-theme'
    'deepin-wallpapers'
    'noto-fonts')
makedepends=('git')
conflicts=('dde-daemon<=2.92.1' "$_pkgname")
provides=("$_pkgname")
source=("$url/archive/$pkgver.tar.gz"
    'manjaro.diff')
sha256sums=('3f6f6c2b0e081a25593aa416fe510db280680886bc462e65bbb0bc6c14e4bfc7'
            '95173517867a3fe8c1941ed7140675cbb69486267e7a83819a8ef8166b1958d3')

prepare() {
    cd $_pkgname-$pkgver
    patch -p1 < ../manjaro.diff
}

build() {
    cd $_pkgname-$pkgver
    make ARCH=x86
}

package() {
    cd $_pkgname-$pkgver
    make DESTDIR=$pkgdir install
}
