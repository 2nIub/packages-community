# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

_pkgname=deepin-desktop-schemas
pkgname=$_pkgname-manjaro
pkgver=3.1.4
pkgrel=4
pkgdesc='Manjaro Linux Deepin gschemas'
arch=('any')
url="https://github.com/linuxdeepin/$_pkgname"
license=('GPL3')
groups=('deepin-manjaro')
depends=('dconf'
    'deepin-artwork-themes'
	'deepin-iconthemes-manjaro'
	'deepin-sound-theme'
	'deepin-wallpapers'
	'noto-fonts'
	'vertex-maia-icon-theme'
	'vertex-maia-themes'
	'xcursor-breeze')
makedepends=('git')
conflicts=('dde-daemon<=2.92.1' "$_pkgname")
provides=("$_pkgname")
source=("git+https://cr.deepin.io/deepin-desktop-schemas.git#tag=$pkgver"
	'manjaro.diff')
sha256sums=('SKIP'
            'b43324487aace19e735142374803c8e0b143adf9b134cc5692acbae3dd84f3d3')

prepare() {
  cd $_pkgname
  patch -p1 < ../manjaro.diff
}

build() {
  cd $_pkgname
  make
}

package() {
  cd $_pkgname
  make DESTDIR=$pkgdir install
}
