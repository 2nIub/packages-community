# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

_pkgname=deepin-desktop-schemas
pkgname=$_pkgname-manjaro
pkgver=3.2.5
pkgrel=2
pkgdesc='Manjaro Linux Deepin gschemas'
arch=('any')
url="https://github.com/linuxdeepin/$_pkgname"
license=('GPL3')
groups=('deepin-manjaro')
depends=('dconf'
    'deepin-artwork-themes'
    'deepin-iconthemes-manjaro'
    'deepin-sound-theme'
    'deepin-wallpapers'
    'noto-fonts')
makedepends=('git')
conflicts=('dde-daemon<=2.92.1' "$_pkgname")
provides=("$_pkgname")
source=("$url/archive/$pkgver.tar.gz"
    'manjaro.diff')
sha256sums=('a109327e3651462e7cf16896f408c42f1ef443a23e7d782aa247bbb49ae13f52'
            '3e77bb79773831d345825cc190a965d5c249f8becd243acacb5255733d355607')

prepare() {
    cd $_pkgname-$pkgver
    patch -p1 < ../manjaro.diff
}

build() {
    cd $_pkgname-$pkgver
    make ARCH=x86
}

package() {
    cd $_pkgname-$pkgver
    make DESTDIR=$pkgdir install
}
