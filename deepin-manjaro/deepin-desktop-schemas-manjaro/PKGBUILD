# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

_pkgname=deepin-desktop-schemas
pkgname=$_pkgname-manjaro
pkgver=3.2.3
pkgrel=1
pkgdesc='Manjaro Linux Deepin gschemas'
arch=('any')
url="https://github.com/linuxdeepin/$_pkgname"
license=('GPL3')
groups=('deepin-manjaro')
depends=('dconf'
    'deepin-artwork-themes'
    'deepin-iconthemes-manjaro'
    'deepin-sound-theme'
    'deepin-wallpapers'
    'noto-fonts')
makedepends=('git')
conflicts=('dde-daemon<=2.92.1' "$_pkgname")
provides=("$_pkgname")
source=("$url/archive/$pkgver.tar.gz"
    'manjaro.diff')
sha256sums=('1360e223a7d1b0f4bef054d8de1ff1d302dca67ad69d5f0f9846e429fd6247d1'
            '3e77bb79773831d345825cc190a965d5c249f8becd243acacb5255733d355607')

prepare() {
    cd $_pkgname-$pkgver
    patch -p1 < ../manjaro.diff
}

build() {
    cd $_pkgname-$pkgver
    make ARCH=x86
}

package() {
    cd $_pkgname-$pkgver
    make DESTDIR=$pkgdir install
}
