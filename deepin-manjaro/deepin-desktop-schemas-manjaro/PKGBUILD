# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

_pkgname=deepin-desktop-schemas
pkgname=$_pkgname-manjaro
pkgver=3.0.3
pkgrel=3.1
pkgdesc='Desktop-wide gsettings schemas for Manjaro-Deepin'
arch=('any')
url="https://github.com/oberon2007/$pkgname"
license=('GPL3')
depends=('dconf'
	'deepin-artwork-themes-manjaro'
	'numix-themes-archblue'
	'deepin-sound-theme')
makedepends=('git')
conflicts=('dde-daemon<=2.92.1' 'deepin-desktop-schemas')
provides=('deepin-desktop-schemas')
replaces=('deepin-default-gsettings' 'deepin-manjaro-gsettings')
groups=('deepin-manjaro')
source=("git+$url.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  cd "${pkgname}"
  # don't override GNOME defaults
  rm overrides/{org.compiz,org.gnome.desktop,other}.override
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  make DESTDIR=$pkgdir install
}
