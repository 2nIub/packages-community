# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=manjaro-deepin-settings
_pkgver=3.0.9
pkgver=20160903+3.0.9
pkgrel=1
_pkgname=deepin-desktop-schemas
pkgdesc='Manjaro Linux deepin settings and gschemas'
arch=('any')
url="https://github.com/linuxdeepin/$_pkgname"
license=('GPL3')
groups=('deepin-manjaro')
depends=('cantarell-fonts'
	'dconf'
	'deepin-artwork-themes-manjaro'
	'deepin-desktop'
	'deepin-iconthemes-manjaro'
	'deepin-launcher'
	'deepin-session-ui-manjaro'
	'deepin-sound-theme'
	'deepin-wallpapers'
	'manjaro-base-skel'
	'vertex-maia-icon-theme'
	'vertex-maia-themes'
	'xcursor-breeze'
# currently still needed for control-panel mouse-widget: 
	'xf86-input-synaptics')
optdepends=('plymouth-theme-manjaro-deepin-circle'
	'manjaro-backgrounds')
makedepends=('git')
conflicts=('dde-daemon<=2.92.1'
	'deepin-desktop-schemas'
	'deepin-desktop-schemas-manjaro'
	'manjaro-desktop-settings')
provides=('deepin-desktop-schemas'
	'manjaro-desktop-settings')
replaces=('deepin-desktop-schemas-manjaro'
	'deepin-default-gsettings'
	'deepin-manjaro-gsettings')
source=("git+https://cr.deepin.io/deepin-desktop-schemas.git#tag=$_pkgver"
	'manjaro.diff'
	"git+https://github.com/manjaro/desktop-settings.git")
sha256sums=('SKIP'
            'eb99a5d079a7cd71ac0bde833f35d33b9c0fb7e5cdd4ab34bdabddfbd7c50d5a'
            'SKIP')

pkgver() {
  printf "$(date +%Y%m%d)+${_pkgver}"
}

prepare() {
  cd $_pkgname
  patch -p1 < ../manjaro.diff
# don't override GNOME defaults
  rm overrides/x86/org.gnome.desktop.override
}

build() {
  cd $_pkgname
  make
}

package() {
  cd $_pkgname
  make DESTDIR=$pkgdir install
  install -d $pkgdir/etc
  cp -r $srcdir/desktop-settings/community/deepin/skel $pkgdir/etc
}
