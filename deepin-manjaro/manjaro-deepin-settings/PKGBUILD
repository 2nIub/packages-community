# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=manjaro-deepin-settings
pkgver=20160524
pkgrel=1
_pkgname=deepin-desktop-schemas
_pkgver=3.0.5
pkgdesc='Manjaro Linux deepin settings and gschemas'
arch=('any')
url="https://github.com/linuxdeepin/$_pkgname"
license=('GPL3')
groups=('deepin-manjaro')
depends=('dconf'
	'deepin-artwork-themes-manjaro'
	'deepin-desktop'
	'deepin-iconthemes-manjaro'
	'deepin-launcher'
	'deepin-session-ui-manjaro'
	'deepin-sound-theme'
	'deepin-wallpapers'
	'manjaro-base-skel'
	'numix-themes-archblue')
optdepends=('plymouth-theme-manjaro-deepin-circle')
makedepends=('git')
conflicts=('dde-daemon<=2.92.1'
	'deepin-desktop-schemas'
	'deepin-desktop-schemas-manjaro'
	'manjaro-desktop-settings')
provides=('deepin-desktop-schemas'
	'manjaro-desktop-settings')
replaces=('deepin-desktop-schemas-manjaro'
	'deepin-default-gsettings'
	'deepin-manjaro-gsettings')
source=("git+https://cr.deepin.io/deepin-desktop-schemas.git#tag=$_pkgver"
	'manjaro.diff'
	"git+https://github.com/manjaro/desktop-settings.git")
sha256sums=('SKIP'
            'e47b899671fe20d0f0d8c1a63363e185cced12779b9acae0c7a6083a3e001e13'
            'SKIP')

prepare() {
  cd $_pkgname
  patch -p1 < ../manjaro.diff
# don't override GNOME defaults
  rm overrides/x86/org.gnome.desktop.override
}

build() {
  cd $_pkgname
  make
}

package() {
  cd $_pkgname
  make DESTDIR=$pkgdir install
  cp -r $srcdir/desktop-settings/community/deepin/skel $pkgdir/etc
}
