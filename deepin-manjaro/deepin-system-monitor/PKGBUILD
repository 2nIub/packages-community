# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Martin Wimpress <code@flexion.org>

pkgname=deepin-system-monitor
_pkgname=mate-system-monitor
_ver=1.12
pkgver=$_ver.1
pkgrel=2
pkgdesc="A fork of mate-system-monitor for Deepin"
url="http://github.com/oberon2007/$pkgname"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dconf' 'dbus-glib' 'glibmm' 'gtkmm' 'gtkmm3' 'libgtop' 'libwnck'
         'libwnck3' 'librsvg')
makedepends=('automake' 'docbook2x' 'mate-common' 'yelp-tools')
conflicts=('mate-system-monitor')
install=$pkgname.install
source=("http://pub.mate-desktop.org/releases/$_ver/$_pkgname-$pkgver.tar.xz"
	'manjaro-deepin-customize.diff'
	'side.png')
sha1sums=('58fd0ab16ff76a1d0871053698c13cb28f407bf1'
          '695634b854601ab3d13bb039bc3bb76f29f9c753'
          'ecef8feef712a9ff4d5d8af1bf8a5059c11a6ab8')

prepare() {
    cd $srcdir/$_pkgname-$pkgver
    patch -p1 < ../manjaro-deepin-customize.diff
    cd ..
    cp -f side.png $srcdir/$_pkgname-$pkgver/pixmaps/side.png
}

build() {
    cd $_pkgname-$pkgver
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/mate-system-monitor \
        --localstatedir=/var \
        --with-gtk=3.0 \
        --disable-static
    make
}

package() {
    cd $_pkgname-$pkgver
    make DESTDIR="${pkgdir}" install
}
