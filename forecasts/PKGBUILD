# Based on the file created for Arch Linux by:
# Contributor: fancris3 <fancris3 at aol.com>
# Contributor: fbianconi <fbianconi at gmail.com>
# Contributor: Moritz Maxeiner <moritz@ucworks.org>
# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dot]org>

pkgname=forecasts
pkgver=221.a169559
ever=0.18.5
pkgrel=3
pkgdesc="Enlightenment e18 module: A simple and useful weather forecasts"
arch=('i686' 'x86_64')
url="http://git.enlightenment.org/enlightenment/modules/$pkgname.git"
license=('MIT')
depends=("enlightenment=$ever")
makedepends=('git')
provides=("$pkgname" "$pkgname-git")
conflicts=('e-modules-extra' "$pkgname-svn" "$pkgname-git")
source=("$pkgname::git://git.enlightenment.org/enlightenment/modules/$pkgname.git")
md5sums=('SKIP')

pkgver() {
  cd "$pkgname"
  # Use the tag of the last commit
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$pkgname"
  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "$pkgname"
  make DESTDIR="$pkgdir/" install

  install -D -m644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
  install -D -m644 COPYING-PLAIN $pkgdir/usr/share/licenses/$pkgname/COPYING-PLAIN
}
