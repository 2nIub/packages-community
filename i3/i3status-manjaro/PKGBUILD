# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>

_pkgname=i3status
pkgname=$_pkgname-manjaro
pkgver=2.10.r13.ge073cf4
pkgrel=2
pkgdesc='customized status bar for manjaro-i3, dzen2 or xmobar'
arch=('i686' 'x86_64')
groups=('i3-manjaro')
url='http://github.com/i3/i3status'
license=('BSD')
depends=('alsa-lib'
	'confuse'
	'libpulse'
	'libnl'
	'ttf-font-icons'
	'wireless_tools'
	'yajl')
makedepends=('asciidoc'
	'git'
	'pkgconfig')
options=('docs')
conflicts=('i3status')
provides=('i3status')
backup=('etc/i3status.conf')
install=$pkgname.install
source=("git+$url.git"
	'manjaro.diff')
md5sums=('SKIP'
         'cfc11cf43b222943f0ba61d7f6c9fd5d')

pkgver() {
  cd "$_pkgname"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "/$srcdir/$_pkgname"
  patch -p0 < ../manjaro.diff
}

build() {
  cd "$_pkgname"
  make
}

package() {
  cd "$_pkgname"
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/${_pkgname}/LICENSE
  make clean
}
