# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Christoph Siegenthaler <csi@gmx.ch>

pkgname=espeak
pkgver=1.48.15
pkgrel=3
pkgdesc='Text to Speech engine for high quality English, with support for other languages'
arch=('x86_64' 'i686')
url="https://github.com/rhdunn/$pkgname"
license=('GPL')
depends=('portaudio'
	'libpulse'
	'wxgtk')
options=('!emptydirs')
source=("$url/archive/$pkgver.tar.gz")
md5sums=('50108abc0d674ded73edd0e2d83003dc')

build() {
  cd $pkgname-$pkgver
  cp src/portaudio19.h src/portaudio.h

  ./autogen.sh
  ./configure --prefix=/usr --with-extdict-ru --with-extdict-zh --with-extdict-zhy
  make CXXFLAGS="$CXXFLAGS" AUDIO=runtime
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  chmod 644 "$pkgdir/usr/lib/libespeak.a"
}
