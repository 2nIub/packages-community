# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Author: horst3180 @ deviantart

pkgname=vertex-themes
pkgver=20170827
pkgrel=1
pkgdesc='Vertex Gtk2, Gtk3, Metacity, Xfwm, Cinnamon and GNOME Shell themes.'
_gnomever=3.22
_commit=82cff0c4780f59ebbb5f2445d071869d30111b5a
arch=('any')
url='http://horst3180.deviantart.com/art/Vertex-Theme-470663601'
license=('GPL3')
depends=('gtk-engine-murrine')
optdepends=('ff-theme-util: set Firefox and Palemoon themes automatically')
source=("vertex-$_commit.tar.gz::https://github.com/oberon2007/Vertex-theme/archive/$_commit.tar.gz")
sha256sums=('74a4663b78c178d2f1f296a52cd227680ca1d8b606316922c80da816dd0f1738')

pkgver() {
    date +%Y%m%d
}

build() {
  cd vertex-theme-${_commit}
  ./autogen.sh --prefix=/usr --disable-unity --with-gnome=${_gnomever}
}
package() {
  cd vertex-theme-${_commit}

  make DESTDIR="$pkgdir" install
  cd extra
  for name in "Chrome" "Firefox" "Vertex_alt_metacity"; do
    find "$name" -type f -not -name *~ -exec install -Dm644 '{}' "$pkgdir/usr/share/themes/{}" \;
  done
  install -Dm644 Vertex-Plank/dock.theme $pkgdir/usr/share/plank/themes/Vertex/dock.theme
  
  cd ..
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  install -Dm644 README.md "$pkgdir/usr/share/themes/Vertex/README"
}
