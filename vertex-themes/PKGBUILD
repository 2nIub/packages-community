# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Author: horst3180 @ deviantart

pkgname=vertex-themes
pkgver=20161125
pkgrel=1
pkgdesc='Vertex Gtk2, Gtk3, Metacity, Xfwm, Cinnamon and GNOME Shell themes.'
_gnomever=3.20
_commit=59eec1884d7e079ee65e3059832fe4bf31daa05b
arch=('any')
url='http://horst3180.deviantart.com/art/Vertex-Theme-470663601'
license=('GPL3')
depends=('gtk-engine-murrine')
source=("vertex-$_commit.tar.gz::https://github.com/horst3180/Vertex-theme/archive/$_commit.tar.gz")
sha256sums=('8b990e3bd438ad2c51aa472d77d65988bed146066f985b491d571d79f3419493')

build() {
  cd vertex-theme-${_commit}
  ./autogen.sh --prefix=/usr --with-gnome=${_gnomever}
}
package() {
  cd vertex-theme-${_commit}

  make DESTDIR="$pkgdir" install
  cd extra
  for name in "Chrome" "Firefox" "Vertex-Plank" "Vertex_alt_metacity"; do
    find "$name" -type f -not -name *~ -exec install -Dm644 '{}' "$pkgdir/usr/share/themes/{}" \;
  done

  cd ..
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  install -Dm644 README.md "$pkgdir/usr/share/themes/Vertex/README"
}
