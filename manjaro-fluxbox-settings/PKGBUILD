# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Contributor: artoo <artoo@manjaro.org>

_pkgbase=desktop-settings

pkgname=manjaro-fluxbox-settings
pkgver=20160921
pkgrel=1
pkgdesc='Manjaro Linux fluxbox settings'
arch=('any')
url="https://github.com/manjaro/desktop-settings"
license=('GPL')
provides=('manjaro-desktop-settings')
depends=('manjaro-base-skel'
        'cantarell-fonts'
        'fbmenugen'
        'fluxbox-theme-manjaro'
        'fluxbox-wallpapers'
        'menda-themes-dark'
        'numix-reborn-icon-themes'
        'oblogout-manjaro'
        'xcursor-menda'
        'xfce-theme-greenbird')
makedepends=('git')
optdepends=('plymouth-theme-manjaro-circle')
conflicts=('manjaro-desktop-settings')
source=("git+$url.git")
sha256sums=('SKIP')

_inst_dir(){
    if [[ -d $srcdir/$pkgbase/$1/skel ]]; then
        install -d $pkgdir/etc
        cp -r $srcdir/$pkgbase/$1/skel $pkgdir/etc
    fi

    if [[ -d $srcdir/$pkgbase/$1/scripts ]]; then
        install -d $pkgdir/usr/bin
        cp $srcdir/$pkgbase/$1/scripts/* $pkgdir/usr/bin
    fi

    if [[ -d $srcdir/$pkgbase/$1/schemas ]]; then
        install -d $pkgdir/usr/share/glib-2.0/schemas
        cp $srcdir/$pkgbase/$1/schemas/* $pkgdir/usr/share/glib-2.0/schemas
    fi

    if [[ -d $srcdir/$pkgbase/$1/bashrc.d ]]; then
        install -d $pkgdir/etc/bash/bashrc.d
        cp $srcdir/$pkgbase/$1/bashrc.d/* $pkgdir/etc/bash/bashrc.d
    fi

    if [[ -d $srcdir/$pkgbase/$1/profile.d ]]; then
        install -d $pkgdir/etc/profile.d
        cp $srcdir/$pkgbase/$1/profile.d/* $pkgdir/etc/profile.d
    fi

    if [[ -d $srcdir/$pkgbase/$1/xinitrc.d ]]; then
        install -d $pkgdir/etc/X11/xinit/xinitrc.d
        cp $srcdir/$pkgbase/$1/xinitrc.d/* $pkgdir/etc/X11/xinit/xinitrc.d
    fi
}

pkgver() {
    date +%Y%m%d
}

package() {
    _inst_dir 'community/fluxbox'
}
