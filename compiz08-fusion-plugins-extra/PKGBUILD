# Maintainer : Rob McCathie <rob@manjaro.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: JJDaNiMoTh <jjdanimoth@gmail.com>
# Contributor: nesl247 <nesl247@gmail.com>

pkgname=compiz08-fusion-plugins-extra
_pkgname=compiz-fusion-plugins-extra
pkgver=0.8.10
pkgrel=1
pkgdesc="Compiz Fusion Extra plugins"
arch=('i686' 'x86_64')
url="http://www.compiz.org"
license=('GPL')
depends=('compiz08-core>=0.8.10' 'compiz08-bcop' 'compiz08-fusion-plugins-main' 'libnotify' 'glu' 'libxslt' 'startup-notification' 'libsm' 'gtk2')
makedepends=('intltool' 'pkg-config' 'gettext' 'gconf')
replaces=("${_pkgname}")
provides=("${_pkgname}=${pkgver}")
conflicts=("${_pkgname}")
source=("http://www.northfield.ws/projects/compiz/releases/${pkgver}/plugins-extra.tar.gz"
        'notification.patch')
sha1sums=('fd7ff7c6a98f78e5a0e048955be49a97c8ba73bb'
          '11ea02ce12711df9f5e306d96036a7d4b1c4a217')

prepare() {
  cd "${srcdir}/plugins-extra"
  patch -p1 -i "${srcdir}/notification.patch"
  sed -i 's/iconUri, NULL);/iconUri);/' src/notification/notification.c
}

build() {
  cd "$srcdir/plugins-extra"
  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "$srcdir/plugins-extra"
  make DESTDIR="${pkgdir}" install
}
