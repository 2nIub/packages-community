# Maintainer: Stephen Smith <stephen304@gmail.com>

pkgname=lemonbar-xft-clicky
_pkgname=bar
_ver=0
pkgver=${_ver}.r214.a2fad82
pkgrel=1
pkgdesc="A lightweight xcb based bar with ported xft support. Modified to provide more clickable areas."
arch=('i686' 'x86_64')
url="https://github.com/Chrysostomus/bar"
license=('MIT')
depends=('libxcb' 'libxft' 'libx11')
makedepends=('git')
provides=('bar-aint-recursive' 'lemonbar')
conflicts=('bar-aint-recursive' 'lemonbar')
source=("$_pkgname::git+https://github.com/Chrysostomus/bar.git#branch=xft-port")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  printf "$_ver.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$_pkgname"
  make
}

package() {
  cd "$srcdir/$_pkgname"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
