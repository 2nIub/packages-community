# Maintainer: Ramon Buld√≥ <rbuldo@gmail.com>
# Maintainer: Stefano Capitani  <stefanoatmanjarodotorg>

pkgbase=manjaro-gnome-settings
pkgname=('manjaro-gnome-basic-settings' 'manjaro-gnome-extra-settings')
pkgver=20170614
pkgrel=1
arch=('any')
url="https://github.com/manjaro/manjaro-gnome-settings"
license=('GPL')
source=("git+$url.git#branch=devel"
        'PostLogin.Default')
makedepends=('git')
sha256sums=('SKIP'
            '8584bd4c4b8830720210c8938dec715987f4f6a38417fe602ff5181947a497aa')

pkgver() {
	date +%Y%m%d
}

_inst_dir(){
    if [[ -d $srcdir/$pkgbase/$1/skel ]]; then
        install -d $pkgdir/etc
        cp -r $srcdir/$pkgbase/$1/skel $pkgdir/etc
    fi

    if [[ -d $srcdir/$pkgbase/$1/scripts ]]; then
        install -d $pkgdir/usr/bin
        cp $srcdir/$pkgbase/$1/scripts/* $pkgdir/usr/bin
    fi

    if [[ -d $srcdir/$pkgbase/$1/schemas ]]; then
        install -d $pkgdir/usr/share/glib-2.0/schemas
        cp $srcdir/$pkgbase/$1/schemas/* $pkgdir/usr/share/glib-2.0/schemas
    fi

    if [[ -d $srcdir/$pkgbase/$1/bashrc.d ]]; then
        install -d $pkgdir/etc/bash/bashrc.d
        cp $srcdir/$pkgbase/$1/bashrc.d/* $pkgdir/etc/bash/bashrc.d
    fi

    if [[ -d $srcdir/$pkgbase/$1/profile.d ]]; then
        install -d $pkgdir/etc/profile.d
        cp $srcdir/$pkgbase/$1/profile.d/* $pkgdir/etc/profile.d
    fi

    if [[ -d $srcdir/$pkgbase/$1/xinitrc.d ]]; then
        install -d $pkgdir/etc/X11/xinit/xinitrc.d
        cp $srcdir/$pkgbase/$1/xinitrc.d/* $pkgdir/etc/X11/xinit/xinitrc.d
    fi
}

package_manjaro-gnome-basic-settings() {
    pkgdesc='Manjaro Linux Gnome basic settings'
    depends=('gnome-shell'
            'manjaro-gnome-extension-settings'
            'gnome-wallpapers'
            'arc-themes-maia'
            'arc-maia-icon-theme'
            'xcursor-breeze'
            'qt5-styleplugins'
            'qt5ct')
    optdepends=('moka-icon-theme: Fallback icon theme for Arc Maia Icon theme')
    provides=('manjaro-desktop-settings')
    conflicts=('manjaro-gnome-settings' 'manjaro-desktop-settings')
    replaces=('manjaro-gnome-settings')
    backup=('etc/gdm/PostLogin/Default')

    _inst_dir 'basic'

    install -dm755 ${pkgdir}/etc/gdm/PostLogin
    install -m644 ${srcdir}/PostLogin.Default ${pkgdir}/etc/gdm/PostLogin/Default
}

package_manjaro-gnome-extra-settings() {
    pkgdesc='Manjaro Linux Gnome extra settings'
    depends=('manjaro-gnome-basic-settings'
            'gnome-shell-extension-taskbar'
            'gnome-shell-extensions'
            'gnome-shell-extension-openweather'
            'gnome-shell-extension-gnomenu'
            'gnome-shell-extension-dash-to-dock'
            'gnome-shell-extension-topicons-plus')
    optdepends=('firefox-arc-maia-settings: Firefox theme'
            'plymouth-theme-manjaro-gnome-17.0: Plymouth theme'
            'chrome-gnome-shell: Native connector for integration with extensions.gnome.org')
    conflicts=('manjaro-gnome-extension-settings')
    replaces=('manjaro-gnome-extension-settings')
    install=dash-to-dock.install

    _inst_dir 'extra'
}
