# Maintainer: Chrysostomus @forum.manjaro.org
# Maintainer: Bernhard Landauer <oberon@manjaro.org>

pkgbase=manjaro-architect
pkgname=('manjaro-architect' 'manjaro-architect-launcher')
pkgver=0.8.12
_commit=aa7bd1dd679ab7fd61c810745499f3e1135ba55a
pkgrel=3
pkgdesc="Manjaro CLI net-installer, forked from the Archlinux Architect"
arch=(any)
url="https://github.com/Chrysostomus/$pkgbase"
license=(GPL2)
depends=('bash'
         'fzf'
         'dialog')
optdepends=('maia-console'
            'terminus-font')
source=("$url/archive/$_commit.tar.gz")
md5sums=('e3d9fa88ab1ad7a6d568c0c383cd0a83')

prepare() {
    cd $pkgbase-$_commit
    sed -i "s/Version .*/Version = $pkgver/" Makefile
}

build() {
    cd $pkgbase-$_commit
	make PREFIX=/usr
}

package_manjaro-architect() {
    depends+=('f2fs-tools'
         'gptfdisk'
         'manjaro-architect-launcher'
         'manjaro-tools-base'
         'mhwd'
         'nilfs-utils'
         'pacman-mirrorlist'
         'parted')
    cd $pkgbase-$_commit
	make PREFIX=/usr DESTDIR=${pkgdir} install
}

package_manjaro-architect-launcher() {
    cd $pkgbase-$_commit
    install -Dm755 bin/setup $pkgdir/usr/bin/setup
}
