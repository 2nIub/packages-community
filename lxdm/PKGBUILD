# $Id: PKGBUILD 74703 2012-08-02 08:45:29Z bpiotrowski $
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: kiefer <jorgelmadrid@gmail.com>
# Maintainer: Roland Singer <roland@manjaro.org>

pkgname=lxdm
pkgver=0.4.1
pkgrel=30
pkgdesc='Lightweight X11 Display Manager'
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/lxdm/"
license=('GPL')
groups=('lxde')
depends=('gtk2' 'xorg-server' 'iso-codes' 'adwaita-manjaro-themes' 'lxdm-manjaro-theme')
optdepends=('lxpolkit: to run lxdm-config directly from menu'
            'gtk-engines: default GTK+ theme'
            'librsvg: display the default background')
makedepends=('intltool')
install=${pkgname}.install
backup=('etc/lxdm/lxdm.conf' 'etc/pam.d/lxdm' 'etc/lxdm/Xsession'
        'etc/lxdm/PreLogin' 'etc/lxdm/LoginReady' 'etc/lxdm/PostLogin'
        'etc/lxdm/PostLogout' 'etc/lxdm/PreReboot' 'etc/lxdm/PreShutdown')
source=(http://downloads.sourceforge.net/lxde/$pkgname-$pkgver.tar.gz
        default-config.patch
        git-fixes.patch
        lxdm.pam
        Xsession
        #####################################
        ## Custom Manjaro Login Pane Patch ##
        #####################################
        lxdm.login_pane.patch
        org.manjaro.pkexec.lxdm-config.policy
        lxdm-config.desktop)

prepare(){
    cd "$srcdir/$pkgname-$pkgver"

    # Apply various fixes from git
    patch -Np1 -i ../git-fixes.patch

    # Adjust Manjaro-specific settings
    patch -Np1 -i ../default-config.patch

    # Use our custom pam and Xsession files
    cp ../lxdm.pam pam/lxdm
    cp ../Xsession data/Xsession

    #####################################
    ## Custom Manjaro Login Pane Patch ##
    #####################################
    patch -Np0 -i $srcdir/lxdm.login_pane.patch
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    autoreconf -fi
    ./configure --prefix=/usr --sbindir=/usr/bin --libexecdir=/usr/lib/lxdm \
                --sysconfdir=/etc --localstatedir=/var
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
    chmod 644 "$pkgdir/etc/lxdm/lxdm.conf"

    # Home directory
    install -dm 755 "$pkgdir/var/lib/lxdm"
    echo 'GDK_CORE_DEVICE_EVENTS=true' > "$pkgdir"/var/lib/lxdm/.pam_environment
    chown -R 121:121 "$pkgdir/var/lib/lxdm"

    ############################
    ## Custom Manjaro Changes ##
    ############################

    # Fix logout behavior
    echo '#!/bin/sh' > "${pkgdir}/etc/lxdm/PostLogout"
    echo '' >> "${pkgdir}/etc/lxdm/PostLogout"
    echo '# Kills all your processes when you log out.' >> "${pkgdir}/etc/lxdm/PostLogout"
    echo 'killall --user $USER -TERM' >> "${pkgdir}/etc/lxdm/PostLogout"
    echo '' >> "${pkgdir}/etc/lxdm/PostLogout"
    echo '# Sets the desktop background to solid black. Useful if you have multiple monitors.' >> "${pkgdir}/etc/lxdm/PostLogout"
    echo 'xsetroot -solid black' >> "${pkgdir}/etc/lxdm/PostLogout"

    # Install policy file
    install -Dm644 "${srcdir}/org.manjaro.pkexec.lxdm-config.policy" "${pkgdir}/usr/share/polkit-1/actions/org.manjaro.pkexec.lxdm-config.policy"

    # Install launcher script
    echo '#!/bin/sh' > ${pkgdir}/usr/bin/lxdm-config-pkexec
    echo 'user=$(whoami)' >> ${pkgdir}/usr/bin/lxdm-config-pkexec
    echo 'pkexec "/usr/bin/lxdm-config" --user "$user" "$@"' >> ${pkgdir}/usr/bin/lxdm-config-pkexec
    chmod 755 ${pkgdir}/usr/bin/lxdm-config-pkexec

    # Install desktop file
    install -Dm644 $srcdir/${pkgname}-config.desktop $pkgdir/usr/share/applications/${pkgname}-config.desktop
}

sha256sums=('9e0d0a5672fcf31a18de8178ce73eab1723d6ae7097dfe41e9fe2c46e180cf08'
            '20e67bbc9dcb930d5127591918da64c516d9252371c0a3a57e565f7912ddb430'
            'd8d7955ea6a5c11766b7016ae7aa57c444c9c5309087776466964d581ba9190f'
            'e8c4c5fd3b801a390d201166fd1fb9730e78a5c62928768103b870b6bd980ea0'
            '8c7e42ff271c8d0f7f3cfe11aca7f92f411bf5be1d86135f558b8e413e53dd30'
            '078b624d05a9a97414c48075c267fba4f7cfc21fec7f983de05fcc75a186d0c9'
            '21288b1de0354dfa3ee45c9c91552c3abb1e58aba48c70eb7b604b6e07e33ea1'
            'ced6936b00b8e7cfd67b7be960e5e0f447581a03c1b19e34783257ed4b602db7')
