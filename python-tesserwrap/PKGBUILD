# Maintainer: Jerome Leclanche <jerome@leclan.ch>

_pkgname=tesserwrap
pkgname=python-$_pkgname
pkgver=0.1.6
pkgrel=1
pkgdesc="Basic python bindings to the Tesseract C++ API"
arch=("any")
license=("APACHE")
url="https://pypi.python.org/pypi/tesserwrap"
depends=("python" "tesseract")
makedepends=('sed')
source=("https://pypi.python.org/packages/source/${_pkgname:0:1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
md5sums=('c756deadf262ac507ceb6b77f9a66393')

build() {
	cd "$srcdir/$_pkgname-$pkgver"
	sed -i '/self.set_page_seg_mode(PageSegMode.PSM_SINGLE_BLOCK)/d' ./tesserwrap/__init__.py #remove some bug that is not fixed upstream right now (https://github.com/gregjurman/tesserwrap/issues/20)
	python setup.py build
}

package() {
	cd "$srcdir/$_pkgname-$pkgver"
	python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 --skip-build
}
