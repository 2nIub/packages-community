# Maintainer:  Rob McCathie <rob@manjaro.org>
# Contributor: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Padfoot <padfoot@exemail.com.au>

_name=lightdm-gtk-greeter
pkgname=lightdm-gtk3-greeter
pkgver=1.9.0
pkgrel=0.2
epoch=1
pkgdesc='GTK3 greeter for LightDM'
arch=('i686' 'x86_64')
url="https://launchpad.net/${_name}"
license=('GPL3' 'LGPL3')
depends=('gtk3' 'lightdm')
makedepends=('exo' 'gnome-doc-utils' 'gobject-introspection' 'gtk2' 'gtk3' 'intltool' 'lightdm')
backup=("etc/lightdm/${_name}.conf")
install="${pkgname}.install"
conflicts=("${_name}")
provides=("${_name}=${pkgver}")
source=("${url}/${pkgver%.?}/${pkgver}/+download/${_name}-${pkgver}.tar.gz")
sha256sums=('a9103b4aa161feae99a2a62b716a157cde3817e93cee7f73cde2796a12396938')

build() {
  cd "${_name}-${pkgver}"

  ./configure --prefix='/usr' --sbindir='/usr/bin' --sysconfdir='/etc' \
              --libexecdir='/usr/lib/lightdm' --disable-static
  make
}

package() {
  cd "${_name}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
