# Maintainer: FadeMind <fademind@gmail.com>
# Contributor: X0rg
# Contributor: eloaders <eloaders at linux dot pl>

pkgname=libcpuid
pkgver=0.2.2.r23.gdb6f3ab
pkgrel=1
pkgdesc="Provides CPU identification for x86 (and x86_64)"
arch=('i686' 'x86_64')
url="https://github.com/anrieff/libcpuid"
license=('BSD-2-Clause')
depends=('glibc')
makedepends=('git' 'libtool' 'autoconf')
conflicts=('libcpuid-git')
source=("git://github.com/anrieff/${pkgname}.git")
sha256sums=('SKIP')

pkgver() {
    cd ${pkgname}
    git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd ${pkgname}
    msg2 "Run 'libtoolize'..."
    libtoolize

    msg2 "Run 'autoreconf --install'..."
    autoreconf --install

    msg2 "Run './configure'..."
    ./configure --prefix=/usr

    msg2 "Run 'make'..."
    make
}

package() {
    cd ${pkgname}
    make DESTDIR="$pkgdir" install

    msg2 "Install license..."
    install -Dvm644 "$srcdir/libcpuid/COPYING" "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
