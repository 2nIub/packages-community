# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: darkcoder <mario_vazq@hotmail.com>

pkgname=os-prober
pkgver=1.75
pkgrel=1
pkgdesc="Utility to detect other OSes on a set of drives"
url="http://joey.kitenet.net/code/os-prober/"
arch=('i686' 'x86_64')
license=('GPL3')
depends=('sh')
source=(http://http.debian.net/debian/pool/main/o/${pkgname}/${pkgname}_${pkgver}.tar.xz
	intel-ucode.patch)
md5sums=('acf4f8818af3cee051aa6f927a451e55'
         '843e32f3c266fc4d3e5bea03f4a8cb4e')
sha256sums=('f4ef620455c5ffc3545daf4f32861640a48b0b3b6edda72491eecc1818653446'
            'd65213e30c571d31186af7cd521d2712dbc2f7fe90ca34cdfbed6178194eedee')

prepare() {
  cd "$pkgname-$pkgver"

  patch -p1 -i "$srcdir"/intel-ucode.patch

  # adjust lib dir to allow detection of 64-bit distros
  sed -i -e "s:/lib/ld\*\.so\*:/lib*/ld*.so*:g" os-probes/mounted/common/90linux-distro

  rm -f Makefile
}

build() {
  cd "$pkgname-$pkgver"

  make newns
}

package() {
  cd "$pkgname-$pkgver"

  install -Dm755 linux-boot-prober "$pkgdir"/usr/bin/linux-boot-prober
  install -Dm755 os-prober "$pkgdir"/usr/bin/os-prober
  install -Dm755 newns "$pkgdir"/usr/lib/os-prober/newns
  install -Dm755 common.sh $pkgdir/usr/share/os-prober/common.sh  

  for dir in os-probes os-probes/mounted os-probes/init linux-boot-probes linux-boot-probes/mounted; do
    install -dm755 "$pkgdir/usr/lib/$dir"
    install -m755 -t "$pkgdir/usr/lib/$dir" "$dir"/common/*
    [[ -d "$dir"/x86 ]] && cp -r "$dir"/x86/* "$pkgdir/usr/lib/$dir"
  done

  install -Dm755 os-probes/mounted/powerpc/20macosx "$pkgdir"/usr/lib/os-probes/mounted/20macosx

  install -dm755 "$pkgdir"/var/lib/os-prober
}
