# Maintainer: A Rojas
# Contributor: Alexander Mezin <mezin.alexander@gmail.com>

pkgname=kcm-touchpad-frameworks
pkgver=1.1.r320.2319d35
_pkgver=1.1
pkgrel=1
pkgdesc='KCM, daemon and applet for touchpad'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/playground/utils/kcm-touchpad'
license=('GPL')
depends=('xf86-input-synaptics' 'xcb-util-cursor' 'plasma-framework' 'kcmutils' 'kinit' 'knotifyconfig' 'kded' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'xorg-server-devel' 'git')
conflicts=('kcm-touchpad')
install=$pkgname.install
source=('git://anongit.kde.org/kcm-touchpad.git#branch=frameworks')
md5sums=('SKIP')

pkgver() {
  cd kcm-touchpad
  printf "${_pkgver}.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kcm-touchpad \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
 
