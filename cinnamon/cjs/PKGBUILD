# $Id$
# Maintainer: Alexandre Filgueira <alexfilgueira@antergos.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=cjs
pkgver=3.4.0
pkgrel=0.1
pkgdesc="Javascript Bindings for Cinnamon"
arch=(i686 x86_64)
url="https://github.com/linuxmint/$pkgname"
license=(GPL)
depends=(gtk3 gobject-introspection-runtime js dbus-glib)
makedepends=(gnome-common python2 gobject-introspection)
source=("${pkgname}-${pkgver}.tar.gz::$url/archive/${pkgver}.tar.gz")
options=(!emptydirs)
sha256sums=('5d89febe8374dc189dc3dcc1b4bec798e604d82c89bd5d6b1ffeb7e152d46523')

build() {
  cd $pkgname-$pkgver

  ./autogen.sh --prefix=/usr --disable-static --libexecdir=/usr/lib

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgname-$pkgver
  # Needs a display
  make -k check || :
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
