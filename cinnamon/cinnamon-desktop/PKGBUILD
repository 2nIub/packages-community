# $Id$
# Maintainer: Alexandre Filgueria <alexfilgueira@antergos.com>
# Contributor: Jan de Groot <jan@archlinux.org>

pkgname=cinnamon-desktop
pkgver=3.4.1
pkgrel=0.1
pkgdesc="Library with common API for various Cinnamon modules"
arch=(i686 x86_64)
license=(GPL LGPL)
depends=(accountsservice gtk3 libxkbfile python2 glib2 libgsystem libpulse)
makedepends=(intltool gobject-introspection gnome-common python2)
url="https://github.com/linuxmint/$pkgname"
source=("${pkgname}-${pkgver}.tar.gz::$url/archive/${pkgver}.tar.gz"
        "config.patch")
sha256sums=('1a7b5b4a1b0dcc807a79e69c9d221a27d7d34806161b541139faa3e7b125eb1d'
            'e2c20c35df78aaedcd2d2e4965369bc519f51072ab88d4ac9b01be9a48cf8cfd')
            
prepare() {
  cd $pkgname-$pkgver
  # Adjust configuration
  patch -Np1 -i ../config.patch
}

build() {
  cd $pkgname-$pkgver
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --disable-static

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
