# $Id$
# Maintainer: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: Ner0

pkgname=nemo
pkgver=3.4.1
pkgrel=1.1
pkgdesc="Cinnamon file manager (Nautilus fork)"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/nemo"
_snapshot=77ff741db809455251cb2a3fce6716e6dffb5f2f
license=('GPL')
depends=('cinnamon-desktop'
    'cinnamon-translations'
    'dconf'
    'desktop-file-utils'
    'exempi'
    'gvfs'
    'libexif'
    'libnotify'
    'libxml2'
    'python')
optdepends=('ffmpegthumbnailer: support for video thumbnails')
makedepends=('gnome-common'
    'gobject-introspection'
    'gtk-doc'
    'intltool'
    'python2-gobject'
    'python-gobject'
    'python-polib')
options=('!emptydirs')
#source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxmint/nemo/tarball/$pkgver")
source=("$pkgname-$_snapshot.tar.gz::$url/archive/$_snapshot.tar.gz"
)
sha256sums=('81ea18de58fb67e8aabd1a76d7394ab9d1d3cdf0cecef137d0dadf10002d2af1')

prepare() {
#  cd linuxmint-nemo-*
  cd nemo-$_snapshot
  # Rename 'Files' app name to avoid having the same as nautilus
  sed -i 's/^Name\(.*\)=.*/Name\1=Nemo/' data/nemo.desktop.in.in
}

build() {
#  cd linuxmint-nemo-*
  cd nemo-$_snapshot

  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/nemo \
      --disable-update-mimedb \
      --disable-tracker \
      --disable-gtk-doc-html \
      --disable-schemas-compile \
      --enable-selinux=no

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
#  cd linuxmint-nemo-*
  cd nemo-$_snapshot
  make DESTDIR="$pkgdir" install

  # Remove D-Bus activation file to avoid conflict with nautilus-desktop
  rm "$pkgdir/usr/share/dbus-1/services/org.nemo.freedesktop.FileManager1.service"
}
