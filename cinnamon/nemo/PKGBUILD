# Maintainer: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: Ner0

pkgname=nemo
pkgver=3.4.3
pkgrel=0.1
pkgdesc="Cinnamon file manager (Nautilus fork)"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/nemo"
license=('GPL')
depends=('cinnamon-desktop'
    'cinnamon-translations'
    'dconf'
    'desktop-file-utils'
    'exempi'
    'gvfs'
    'libexif'
    'libnotify'
    'libxml2'
    'python')
optdepends=('ffmpegthumbnailer: support for video thumbnails')
makedepends=('gnome-common'
    'gobject-introspection'
    'gtk-doc'
    'intltool'
    'python2-gobject'
    'python-gobject'
    'python-polib')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxmint/nemo/tarball/$pkgver")
#source=("$pkgname-$_snapshot.tar.gz::$url/archive/$_snapshot.tar.gz")
sha256sums=('5a8d701d35ad9329e591bc612f97aa584255581ea685725b49a27d8319e27129')

prepare() {
  cd linuxmint-nemo-*
  # Rename 'Files' app name to avoid having the same as nautilus
  sed -i 's/^Name\(.*\)=.*/Name\1=Nemo/' data/nemo.desktop.in.in
}

build() {
  cd linuxmint-nemo-*

  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/nemo \
      --disable-update-mimedb \
      --disable-tracker \
      --disable-gtk-doc-html \
      --disable-schemas-compile \
      --enable-selinux=no

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd linuxmint-nemo-*
  make DESTDIR="$pkgdir" install

  # Remove D-Bus activation file to avoid conflict with nautilus-desktop
  rm "$pkgdir/usr/share/dbus-1/services/org.nemo.freedesktop.FileManager1.service"
}
