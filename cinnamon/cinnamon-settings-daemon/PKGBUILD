# $Id$
# Maintainer: Alexandre Filgueira (faidoc) <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-settings-daemon
pkgver=3.4.2
pkgrel=0.1
pkgdesc="The Cinnamon Settings daemon"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/$pkgname"
#_snapshot=486035ad6bd6f05549b9327f4c0117471c32081f
license=('GPL')
depends=('cinnamon-desktop' 'libcanberra-pulse' 'librsvg' 'nss'
         'pulseaudio-alsa' 'upower' 'libnotify' 'libgnomekbd' 'libwacom'
         'libgudev' 'dbus-glib' 'colord' 'libcups' 'polkit')
makedepends=('autoconf-archive' 'intltool' 'docbook-xsl' 'xf86-input-wacom')
options=('!emptydirs')
source=("${pkgname}-${pkgver}.tar.gz::$url/archive/${pkgver}.tar.gz")
#source=("$pkgname-$_snapshot.tar.gz::$url/archive/$_snapshot.tar.gz")
sha256sums=('b30a3b64e4263e4dbe2678c0cff5cef97944282fabb8f4b6fa3ca6c65c8f1522')

build() {
  cd $pkgname-$pkgver
#  cd $pkgname-$_snapshot
  ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --libexecdir="/usr/lib/${pkgname}" \
        --enable-systemd \
        --enable-polkit

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver
#  cd $pkgname-$_snapshot
  make DESTDIR="$pkgdir" install
}
