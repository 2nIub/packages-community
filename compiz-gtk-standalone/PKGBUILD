# Maintainer : Rob McCathie <archaur at rmcc dot com dot au
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Hussam Al-Tayeb ht990332@gmail.com
# Contributor: Khashayar Naderehvandi <khashayar [at] naderehvandi [dot] net>
# Contributor: JJDaNiMoTh <jjdanimoth.aur@gmail.com>

_name=compiz
pkgname=compiz-gtk-standalone
pkgdesc="Compiz compositing window manager with gtk decorator. No specific DE integration. Sane defaults. Good for minimal Compiz setups."
pkgver=0.8.9
pkgrel=4
arch=('i686' 'x86_64')
url="http://cgit.compiz.org/"
license=('GPL' 'LGPL' 'MIT')
depends=('startup-notification' 'librsvg' 'libgl' 'dbus' 'glu' 'libxslt' 'fuse' 'libsm' 'gconf' 'libwnck' 'zenity')
makedepends=('intltool' 'gconf' 'libwnck' 'startup-notification' 'librsvg' 'libgl' 'dbus' 'glu' 'libxslt' 'fuse' 'metacity')
optdepends=("metacity: IMPORTANT - Unless you plan to install Emerald or do not want window decorations, install Metacity now."
            "compiz-fusion-plugins-main: Compiz Main plugins"
            "compiz-fusion-plugins-extra: Compiz Extra plugins"
            "compiz-fusion-plugins-unsupported: Unsupported Compiz plugins"
            "ccsm: CompizConfig Settings Manager in Python2"
            "gconf-editor: Adjust GTK window decorator settings (/apps/metacity/general/)"
            "emerald: Emerald window decorator"
            "emerald-themes: Themes for Emerald")
conflicts=('compiz-core' 'compiz-decorator-gtk')
provides=("compiz-core=$pkgver" 'compiz-decorator-gtk')
install="$pkgname".install

source=("http://cgit.compiz.org/compiz/core/snapshot/core-compiz-0.8.9.tar.bz2"
        'compiz-gcc-4.7.patch'
        'compiz-standalone-defaults.patch'
        'compiz-gtk-decorator-theme-selector'
        'compiz-gtk-decorator-theme.desktop')

sha1sums=('f48b7cb7e98efd4e6967f07967ac9078127123c0'
          '2138342ae9f253bae003e96e91fdd0bc9918291a'
          '59f37feafedd0cb949498fcbaebe3a298aab3af6'
          '4218e7f8d1792d6971ef73364af34c51feb73daf'
          '9b3dd487e9142e826848dc6b5d95a98fbb648f3c')

prepare() {
  cd "$srcdir/core-$_name-$pkgver"
  patch -p0 -i "$srcdir/compiz-gcc-4.7.patch"
  # Some sane defaults
  patch -Np1 -i "$srcdir/compiz-standalone-defaults.patch"
}

build() {
  cd "$srcdir/core-$_name-$pkgver"
  ./autogen.sh   --prefix=/usr \
	    --enable-gtk \
	    --enable-metacity \
	    --enable-gconf \
            --with-gconf-schema-file-dir=/etc/gconf/schemas \
	    --enable-dbus \
	    --enable-librsvg \
	    --disable-gnome \
	    --disable-kde \
	    --disable-kde4 \
	    --with-default-plugins="ccp,decoration,resize,place,move,regex,fade,expo,switcher"
  make QDBUSXML2CPP=/usr/bin/qdbusxml2cpp-qt4
}

package() {  
  cd "$srcdir/core-$_name-$pkgver"
  make DESTDIR="$pkgdir" install
  pushd gtk
  make DESTDIR="$pkgdir" install
  install -Dm644 window-decorator/gwd.schemas "$pkgdir/usr/share/gconf/schemas/gwd.schemas"
  popd
  for i in dbus gconf ini inotify png regex svg glib kconfig
  do
    rm "$srcdir"/core-compiz-0.8.9/metadata/compiz-$i.schemas
  done
  gconf-merge-schema "${pkgdir}"/usr/share/gconf/schemas/compiz-decorator-gtk.schemas "${srcdir}"/core-compiz-0.8.9/metadata/*.schemas
  make DESTDIR="$pkgdir" install
  # install MIT license
  install -Dm644 "$srcdir/core-compiz-0.8.9/COPYING.MIT" "$pkgdir/usr/share/licenses/compiz/COPYING.MIT"
  
  install -Dm755 "$srcdir/compiz-gtk-decorator-theme-selector" "$pkgdir/usr/bin/compiz-gtk-decorator-theme-selector"
  install -Dm644 "$srcdir/compiz-gtk-decorator-theme.desktop" "$pkgdir/usr/share/applications/compiz-gtk-decorator-theme.desktop"
}
