# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Scott Furry <scott.wl.furry@gmail.com>

pkgname=guayadeque
pkgver=0.4.3
pkgrel=2
pkgdesc='Lightweight music player'
arch=('i686' 'x86_64')
url='http://guayadeque.org/'
license=('GPL3')
depends=('curl'
        'dbus'
        'glib2'
        'gst-plugins-base'
        'gst-plugins-good'
        'gst-plugins-bad'
        'gst-plugins-ugly'
        'gstreamer'
        'libgpod'
        'taglib'
        'wxsqlite3')
makedepends=('cmake')
optdepends=('gvfs: Support for external devices')
source=("https://github.com/anonbeat/${pkgname}/archive/v${pkgver}.tar.gz"
        '0001-use-cmake-lib-search-procedure-for-wxsqlite3-from-mu.patch')
md5sums=('131c33d5844c66c3c07c3a1b64fac586'
         'a50e0ed6c1661f58831f220866c70151')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ../0001-use-cmake-lib-search-procedure-for-wxsqlite3-from-mu.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./buildt
  cmake . \
    -DCMAKE_CXX_STANDARD='11' \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX=${pkgdir} \
    -DwxWidgets_wxrc_EXECUTABLE='/usr/bin/wxrc' \
    -DwxWidgets_CONFIG_EXECUTABLE='/usr/bin/wx-config' \
    -DwxWidgets_INCLUDE_DIRS='/usr/include/wx/'
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
