# $Id$
# Maintainer: Ramon Buld√≥ <rbuldo@gmail.com>

pkgname=gtk-theme-maia
pkgver=5.6.0
pkgrel=2
_gitcommit=f4ea22a4fb0b0da038c0d256ebf1205f3e9b69e0
pkgdesc="Maia widget theme for GTK 2 and 3"
arch=(any)
url='https://github.com/manjaro/artwork-maia-gtk'
license=(LGPL)
makedepends=(extra-cmake-modules)
conflicts=(gnome-themes-maia)
source=("maia-$pkgver-$pkgrel.tar.gz::$url/archive/$_gitcommit.tar.gz")
md5sums=('dc592a8fc6bcbd1a969501cc8e40683b')

prepare(){
  mv $srcdir/artwork-maia-gtk-$_gitcommit $srcdir/$pkgname
  mkdir -p build
}

build(){
  cd build
  cmake ../$pkgname \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  
  rm -rf $pkgdir/usr/share/themes/Maia/gtk-3.0
}
 
