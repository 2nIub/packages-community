# $Id$
# Maintainer: Ramon Buld√≥ <rbuldo@gmail.com>

pkgname=gtk-theme-maia
pkgver=5.5.2
pkgrel=1
_gitcommit=a0a2a340517417720a16047d66d95ccd3ca24ada
pkgdesc="Maia widget theme for GTK 2 and 3"
arch=(any)
url='https://github.com/manjaro/artwork-maia-gtk'
license=(LGPL)
makedepends=(extra-cmake-modules)
conflicts=(gnome-themes-maia)
source=("maia-$pkgver-$pkgrel.tar.gz::$url/archive/$_gitcommit.tar.gz")
md5sums=('731fc5bda4b6d8a56930490b05db62eb')

prepare(){
  mv $srcdir/artwork-maia-gtk-$_gitcommit $srcdir/$pkgname
  mkdir -p build
}

build(){
  cd build
  cmake ../$pkgname \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
 
