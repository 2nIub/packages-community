# $Id$
# Maintainer: Ramon Buld√≥ <rbuldo@gmail.com>

pkgname=gtk-theme-maia
pkgver=5.5.0
pkgrel=1
_gitcommit=7f7cd50ac1bfad6d7f7d5a8c07b51d2f6a29ec81
pkgdesc="Maia widget theme for GTK 2 and 3"
arch=(any)
url='https://github.com/manjaro/artwork-maia-gtk'
license=(LGPL)
makedepends=(extra-cmake-modules)
conflicts=(gnome-themes-maia)
source=("maia-$pkgver-$pkgrel.tar.gz::$url/archive/$_gitcommit.tar.gz")
md5sums=('4057389b81c1353732edf86aa35a071c')

prepare(){
  mv $srcdir/artwork-maia-gtk-$_gitcommit $srcdir/$pkgname
  mkdir -p build
}

build(){
  cd build
  cmake ../$pkgname \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
 
