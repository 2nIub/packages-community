# Maintainer : Rob McCathie <rob@manjaro.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: JJDaNiMoTh <jjdanimoth@gmail.com>
# Contributor: nesl247 <nesl247@gmail.com>

pkgname=emerald08
_pkgname=emerald
pkgver=0.8.10
pkgrel=1
pkgdesc="Emerald window decorator"
arch=('i686' 'x86_64')
url="http://www.compiz.org"
license=('GPL')
depends=('compiz08-core>=0.8.10' 'libwnck' 'gtk2' 'libxres' 'shared-mime-info'
         'xdg-utils' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool' 'pkg-config' 'gettext')
replaces=("${_pkgname}")
provides=("${_pkgname}=${pkgver}")
conflicts=("${_pkgname}")
install="${_pkgname}.install"
source=("http://www.northfield.ws/projects/compiz/releases/${pkgver}/emerald.tar.gz")
sha1sums=('01544a6e97acc954ea924403904f32c7c556302f')

build() {
  cd "${srcdir}/${_pkgname}"

  LIBS+="-lm -ldl" ./autogen.sh --prefix=/usr

  make
}

package() {
  cd "${srcdir}/${_pkgname}"
  make DESTDIR="${pkgdir}" install
}
