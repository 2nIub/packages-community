# Maintainer : Rob McCathie <rob@manjaro.org>
# Maintainer : Chrysostomus

pkgname=adwaita-maia-gtk3
pkgver=20160429
pkgrel=1
_basegtkver=3.20
_fullgtkver=${_basegtkver}.2
_axcommit=cc3ffa01f27aa0b6e2f431034832d62fd65aac14
_metacityver=3.18.3
_vertexcommit=6c6cfdbaadb04fb2ccf3c8cf11ffcee84873af2f
pkgdesc="Adwaita theme with maia colors, gtk3-only version"
arch=('any')
url="https://www.gnome.org/"
license=('GPL')
makedepends=('imagemagick')
source=("https://git.gnome.org/browse/gtk+/snapshot/gtk+-${_fullgtkver}.tar.xz"
        "http://mirror.netzspielplatz.de/manjaro/packages/unstable/extra/x86_64/gnome-themes-standard-${_basegtkver}-1-x86_64.pkg.tar.xz"
        "index.theme")
sha256sums=('ccfa0bf5509e120b3e249f20cedaa72d47b03ac998a5a98b6dc5ce9c846681dd'
            '940b6d8d501f55a60279b4c0a1b591300e4c7a4083bbf4454b1820f42cd05703'
            'c9f05e7d94f828a3876cab9bc9118fcfeed095a82c2e84867fd0760aed219b5c')
options=('!strip')

package() {
  mkdir -p "${pkgdir}/usr/share/themes/Adwaita-Maia"
  cp "${srcdir}/index.theme" "${pkgdir}/usr/share/themes/Adwaita-Maia/"
  cp -r "${srcdir}/usr/share/themes/Adwaita/gtk-2.0" "${pkgdir}/usr/share/themes/Adwaita-Maia"
  cp -r "${srcdir}/gtk+-${_fullgtkver}/gtk/theme/Adwaita" "${pkgdir}/usr/share/themes/Adwaita-Maia/gtk-3.0"
  
  cd "${pkgdir}/usr/share/themes/Adwaita-Maia/gtk-3.0"
  find . -maxdepth 1 -type f -not -name '*contained*' -exec rm {} \;
  rm *.scss
  mv gtk-contained.css gtk.css
  mv gtk-contained-dark.css gtk-dark.css
  
  cd ..

  ## Main highlight colour  blue->green
  sed -i "s/4a90d9/16a085/g" gtk-2.0/gtkrc
  sed -i "s/4a90d9/16a085/g" gtk-3.0/gtk{,-dark}.css
  
  ## Lower half of GTK3 progress bars  blue->green
  sed -i "s/2a76c6/008672/g" gtk-3.0/gtk{,-dark}.css
  
  ## Button borders  blue->green
  sed -i "s/184472/00541e/g" gtk-3.0/gtk{,-dark}.css

  ## Window frame color to match window border
  sed -i "s/e8e8e7/ededed/g" gtk-2.0/gtkrc
  sed -i "s/e8e8e7/ededed/g" gtk-3.0/gtk.css
  
  ## All the below are from the "button.suggested-action" section  blue->green
  sed -i "s/1c5187/006133/g" gtk-3.0/gtk{,-dark}.css
  sed -i "s/3986d5/059681/g" gtk-3.0/gtk{,-dark}.css
  sed -i "s/63a0de/2fb08a/g" gtk-3.0/gtk{,-dark}.css
  sed -i "s/276fba/007f66/g" gtk-3.0/gtk{,-dark}.css
  sed -i "s/3583d5/019381/g" gtk-3.0/gtk{,-dark}.css
  sed -i "s/5094db/1ca487/g" gtk-3.0/gtk{,-dark}.css
  sed -i "s/8db9e8/59c994/g" gtk-3.0/gtk{,-dark}.css
  
  ## Replace some other blue with green
  sed -i "s/215d9c/1ABB9B/g" gtk-3.0/gtk{,-dark}.css  

  ## Shift the hightlight-hue of all image files from blue to maia-greenish
  find . ! -type l -name "*.png" -exec mogrify -modulate 100,100,71 {} \;
  find . ! -type l -name "*.svg" -exec mogrify -background none -modulate 100,100,71 {} \;
}
