# $Id: PKGBUILD 113627 2014-06-26 13:34:57Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: TDY <tdy@gmx.com>

pkgname=parole
pkgver=0.7.0
_git=b71bd86995d476049b72d08d90636d5b0617db19
pkgrel=2
pkgdesc="A modern media player based on the GStreamer framework"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/applications/parole/"
license=('GPL')
depends=('gstreamer' 'gst-plugins-base-libs' 'hicolor-icon-theme' 'libnotify'
         'libxfce4ui>=4.11.0' 'taglib' 'desktop-file-utils' 'gst-plugins-good'
         'clutter-gtk')
makedepends=('pkgconfig>=0.9.0' 'xfce4-dev-tools')
optdepends=('xulrunner: browser plugin'
	    'gst-libav'
	    'gst-plugins-bad'
	    'gst-plugins-base'
	    'gst-plugins-ugly')
conflicts=('parole-media-player-git')
install=parole.install
source=(http://git.xfce.org/apps/parole/snapshot/parole-$_git.tar.bz2)
sha256sums=('14d3cab4d26731dc525e6c50b25406ecbc5fc257e19c80a936ac4ee89ce1a5ea')

build() {
  cd "$srcdir/$pkgname-$_git"
  ./autogen.sh --prefix=/usr --libexecdir=/usr/lib \
    --enable-taglib --enable-libnotify --with-gstreamer=1.0 \
    --enable-clutter
  make
}

package() {
  cd "$srcdir/$pkgname-$_git"
  make DESTDIR="$pkgdir" install
}
