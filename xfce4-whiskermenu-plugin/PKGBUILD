# Maintainer: Philip Mueller <philm|at|manjaro|dot|org>
# Contributor: Graeme Gott <graeme@gottcode.org>
# Contributor: Diego Principe <cdprincipe@at@gmail@dot@com>

pkgname=xfce4-whiskermenu-plugin
pkgver=1.3.1
pkgrel=1
pkgdesc="Alternate Xfce menu"
arch=('i686' 'x86_64')
url="http://gottcode.org/${pkgname}/"
license=('GPL2')
depends=('xfce4-panel')
options=('!makeflags')
install=${pkgname}.install
_git=no

if [ "$_git" == "yes" ]; then
   makedepends=('cmake' 'git')
   source=(git://github.com/gottcode/xfce4-whiskermenu-plugin.git)
   sha256sums=('SKIP')
else
   makedepends=('cmake')
   source=(http://gottcode.org/${pkgname}/${pkgname}-${pkgver}-src.tar.bz2)
   sha256sums=('f393ddf8015061d26fbef72c1a99ff59752c65ae5a12ec62bb2b3ee7ddbd687c')
fi

pkgver() {
  cd ${srcdir}/${pkgname}
  if [ "$_git" == "yes" ]; then
     echo 1.3.0.$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
  else
     echo "$pkgver"
  fi
}

build() {
  if [ "$_git" == "yes" ]; then
     cd ${srcdir}/${pkgname}
  else
     cd ${srcdir}/${pkgname}-${pkgver}
  fi
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  if [ "$_git" == "yes" ]; then
     cd ${srcdir}/${pkgname}
  else
     cd ${srcdir}/${pkgname}-${pkgver}
  fi
  make install DESTDIR=${pkgdir}
}
