# Maintainer: Fabian Bornschein <plusfabi@gmail.com>
pkgname=('mhwd-addon-amdgfx-hwe' 'amdgpu-experimental')
pkgver=20170604 #date +%Y%m%d
pkgrel=1
arch=('any')
url="https://github.com/Tids/mhwd-addon-amdgfx-hwe"
license=('GPL3')
makedepends=('curl')
conflicts=('mhwd-addon-amdgpu-hwe')
source=('amdgpu-experimental.install'
        'mhwd-addon-amdgfx-hwe.install'
        '10-amdgpu-experimental.conf'
        'amdgpu.conf'
        'radeon_blacklist.conf'
        'amdgpu-exp_hw_support.conf'
        'amdgpu-experimental.hook'
        'MHWDCONFIG_video-amdgpu-experimental+')
sha512sums=('d15f690fac33b1ed33677ff40bd998c137bb2b65b41c8c0c3a8634977aee83379ebf7e8d81951c4673b46f7fb810147936612f59360f3140100d8d2a147f8d9f'
            'e9db80cddd6daec4352c08d062856f73ff9827a8c4069847c2b3ecf669cfb3355c26f418c491d1a2224b4ac71e119e16359b86b322d22c936639facfa3e96e50'
            'ce6c1e475e7f06aea45d2ce7a2529a511484b2e06917873d1ea10cf820945a5a3d69853d49e8aba07fcb0d01466c66130887139b9ad9cd8b21b9e39a6a760486'
            '5a02d330aec5f6a5aa7876b4d0ce768409284afda996ad310ec3811ebc862f78501fb1e35621b7e216b4e972b772a4f72d1f31c47a791604b7963981feff2ee8'
            'f409269608148c8c420be5584e8bf47fdc87c8861ea6847ec1431c2a1441fba6f06577f85878f01fad3c3fba51d23256eed4bcada48db44d27b834fb628408fe'
            'ca4643ab1a189f62eff027d472af431b1e3767c5fad948b99b3e11f480a464836f95d8122e574b37da495b0f653b90f2bfcbadb88e3c88073e5423d1006681f0'
            '42106abe5bfedd63f7c2e06e2e362ab75060ced6f1e3a86502870ff0248a40d404fd415742f2a0bbfa1c55c479af9dee4c08c9191793b86bd7a99d06f518abc3'
            '8fd86d41d309d7d8fccd75f35afe70fa1305d66090385a9e99ab641fdaf2f7307271f4323c15cedaf5cee4077275d36f38e6075d44464d9bab5d886eabcc5c88')

package_mhwd-addon-amdgfx-hwe()
{
    pkgdesc="Enables experimental AMDGPU support for Sea Islands(GCN1.1) and Southern Islands(GCN 1.0)"
    depends=('bash')
    optdepends=('radeonjet-git: AMDGPU overclocking'
                'radeontop: Radeon cards monitoring utility')
    install='mhwd-addon-amdgfx-hwe.install'

    cd "${srcdir}"

    # Install AMDGPU-experimental config file
    install -D -m644 "${srcdir}/MHWDCONFIG_video-amdgpu-experimental+" \
    "${pkgdir}/var/lib/mhwd/db/pci/graphic_drivers/video-amdgpu-experimental+/MHWDCONFIG"
}

package_amdgpu-experimental()
{
    pkgdesc="Enables experimental AMDGPU support for Sea Islands(GCN1.1) and Southern Islands(GCN 1.0)."
    depends=('linux>=4.9')
    conflicts=('xf86-video-ati')
    install='amdgpu-experimental.install'

    cd "${srcdir}"

    ##################################################
    ##
    ## install module-load file,
    ## radeon blacklist,
    ## exp_hw_support,
    ## pacman hook,
    ## amdgpu-experimental xorg.config
    ##
    ##################################################
    install -D -m644 "${srcdir}/amdgpu.conf" \
    "${pkgdir}/usr/lib/modules-load.d/amdgpu.conf"
    install -D -m644 "${srcdir}/radeon_blacklist.conf" \
    "${pkgdir}/usr/lib/modprobe.d/radeon_blacklist.conf"
    install -D -m644 "${srcdir}/amdgpu-exp_hw_support.conf" \
    "${pkgdir}/usr/lib/modprobe.d/amdgpu-exp_hw_support.conf"
    install -D -m644 "${srcdir}/amdgpu-experimental.hook" \
    "${pkgdir}/usr/share/libalpm/hooks/98-amdgpu-experimental.hook"
    install -D -m644 "${srcdir}/10-amdgpu-experimental.conf" \
    "${pkgdir}/usr/share/X11/xorg.conf.d/10-amdgpu-experimental.conf"
}
