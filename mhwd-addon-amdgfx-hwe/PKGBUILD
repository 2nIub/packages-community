# Maintainer: Fabian Bornschein <plusfabi@gmail.com>
pkgname=('mhwd-addon-amdgfx-hwe' 'amdgpu-experimental')
pkgver=20161231 #date +%Y%m%d
pkgrel=1
arch=('any')
url="https://github.com/Tids/mhwd-addon-amdgfx-hwe"
license=('GPL3')
conflicts=('mhwd-addon-amdgpu-hwe')
source=('amdgpu-experimental.install'
        'mhwd-addon-amdgfx-hwe.install'
        'amdgfx-hwe-amdgpu.conf'
        'amdgfx-hwe-radeon_blacklist.conf'
        'amdgpu-exp_hw_support.conf'
        'amdgpu-experimental-pci.ids'
        'amdgpu-experimental.hook'
        'amdgpu-pci.ids'
        'MHWDCONFIG_video-amdgpu+'
        'MHWDCONFIG_video-amdgpu-experimental+'
        'MHWDCONFIG_video-radeon+'
        'catalystuninstall')
md5sums=('b3eaf63c766deaa863616d87d69fd712'
         '2d42c040ccdc55159e78f78854a867f3'
         '8eb145fd23b417694b36ef9e6113cb84'
         '9ad03a78a863bc0b9f279b330669bd4c'
         'af2fee339acad81dea161f93110c30ed'
         '58eb8d0213f03cc19a5ed74f4831c7bb'
         '5ea12c299007a61f7f2b866ea94f5b44'
         '392be8d374439f6759f827cf8a1d1afc'
         'eff72bfea4a63554e3d646be0216de52'
         'ec6db4d4efbbbbeb14b6d717dc1e6bb4'
         'c30858640e806b7abb54b4e6d5b322b8'
         'dfa749c51d8fb44594450892b34246ef')

package_mhwd-addon-amdgfx-hwe()
{
    pkgdesc="Enables experimental AMDGPU support for Sea Islands(GCN1.1) and Southern Islands(GCN 1.0)"
    depends=('bash')
    optdepends=('radeonjet-git: AMDGPU overclocking'
                'radeontop: Radeon cards monitoring utility')
    install='mhwd-addon-amdgfx-hwe.install'

    cd "${srcdir}"

    # Install AMDGPU-experimental config file
    install -D -m644 "${srcdir}/MHWDCONFIG_video-amdgpu-experimental+" \
    "${pkgdir}/var/lib/mhwd/db/pci/graphic_drivers/video-amdgpu-experimental+/MHWDCONFIG"

    # Install PCI-ID file for AMDGPU-experimental Cards
    install -D -m644 "${srcdir}/amdgpu-experimental-pci.ids" \
    "${pkgdir}/var/lib/mhwd/ids/pci/amdgpu-experimental-pci.ids"


    # Install AMDGPU config file
    install -D -m644 "${srcdir}/MHWDCONFIG_video-amdgpu+" \
    "${pkgdir}/var/lib/mhwd/db/pci/graphic_drivers/video-amdgpu+/MHWDCONFIG"

    # Install PCI-ID file for AMDGPU Cards
    install -D -m644 "${srcdir}/amdgpu-pci.ids" \
    "${pkgdir}/var/lib/mhwd/ids/pci/amdgpu-pci.ids"


    # Install ATI/Radeon config file
    install -D -m644 "${srcdir}/MHWDCONFIG_video-radeon+" \
    "${pkgdir}/var/lib/mhwd/db/pci/graphic_drivers/video-radeon+/MHWDCONFIG"

    # install catalystuninstaller
    install -D -m755 "${srcdir}/catalystuninstall" \
    "${pkgdir}/usr/bin/catalystuninstall"
}

package_amdgpu-experimental()
{
    pkgdesc="Enables experimental AMDGPU support for Sea Islands(GCN1.1) and Southern Islands(GCN 1.0)"
    depends=('linux>=4.9')
    install='amdgpu-experimental.install'

    cd "${srcdir}"

    ##################################################
    ##
    ## install module-load file,
    ## radeon blacklist,
    ## exp_hw_support,
    ## pacman hook
    ##
    ##################################################
    install -D -m644 "${srcdir}/amdgfx-hwe-amdgpu.conf" \
    "${pkgdir}/etc/modules-load.d/amdgfx-hwe-amdgpu.conf"
    install -D -m644 "${srcdir}/amdgfx-hwe-radeon_blacklist.conf" \
    "${pkgdir}/etc/modprobe.d/amdgfx-hwe-radeon_blacklist.conf"
    install -D -m644 "${srcdir}/amdgpu-exp_hw_support.conf" \
    "${pkgdir}/etc/modprobe.d/amdgpu-exp_hw_support.conf"
    install -D -m644 "${srcdir}/amdgpu-experimental.hook" \
    "${pkgdir}/usr/share/libalpm/hooks/98-amdgpu-experimental.hook"
}
