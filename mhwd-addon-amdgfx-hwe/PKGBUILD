# Maintainer: Fabian Bornschein <plusfabi@gmail.com>
pkgname=('mhwd-addon-amdgfx-hwe' 'amdgpu-experimental')
pkgver=20161221 #date +%Y%m%d
pkgrel=1
arch=('any')
url="https://github.com/Tids/mhwd-addon-amdgfx-hwe"
license=('GPL3')
conflicts=('mhwd-addon-amdgpu-hwe')
source=('amdgpu-experimental.install'
        'mhwd-addon-amdgfx-hwe.install'
        'amdgfx-hwe-amdgpu.conf'
        'amdgfx-hwe-radeon_blacklist.conf'
        'amdgpu-exp_hw_support.conf'
        'amdgpu-experimental-pci.ids'
        'amdgpu-experimental.hook'
        'amdgpu-pci.ids'
        'MHWDCONFIG_video-amdgpu+'
        'MHWDCONFIG_video-amdgpu-experimental+'
        'MHWDCONFIG_video-radeon+'
        'catalystuninstall')
md5sums=('67acec5caa9d5c46d3458c57b5bf738f'
         '928a667f96be7ae1609c82d8d6f86c67'
         '8eb145fd23b417694b36ef9e6113cb84'
         '292741875989f13a508fc67a0ca60b0b'
         '09d943b31ab898c2b45ba597f9e9cf94'
         '58eb8d0213f03cc19a5ed74f4831c7bb'
         '5ea12c299007a61f7f2b866ea94f5b44'
         '6a5beabef000cf05bb6aa6668aa60613'
         'e8e61bd1915fcb606c089de8dae8f019'
         '4910bfea628bee099c7c84f1fc1476c7'
         'e11cbb13d5acabdc5d6db2ddfc9a1df0'
         'dfa749c51d8fb44594450892b34246ef')

package_mhwd-addon-amdgfx-hwe()
{
    pkgdesc="Enables experimental AMDGPU support for Sea Islands(GCN1.1) and Southern Islands(GCN 1.0)"
    depends=('bash')
    optdepends=('radeonjet-git: AMDGPU overclocking'
                'radeontop: Radeon cards monitoring utility')
    install='mhwd-addon-amdgfx-hwe.install'
    
    cd "${srcdir}"
    
    # Install AMDGPU-experimental config file
    install -D -m644 "${srcdir}/MHWDCONFIG_video-amdgpu-experimental+" \
    "${pkgdir}/var/lib/mhwd/db/pci/graphic_drivers/video-amdgpu-experimental+/MHWDCONFIG"
    
    # Install PCI-ID file for AMDGPU-experimental Cards
    install -D -m644 "${srcdir}/amdgpu-experimental-pci.ids" \
    "${pkgdir}/var/lib/mhwd/ids/pci/amdgpu-experimental-pci.ids"
    
    
    # Install AMDGPU config file
    install -D -m644 "${srcdir}/MHWDCONFIG_video-amdgpu+" \
    "${pkgdir}/var/lib/mhwd/db/pci/graphic_drivers/video-amdgpu+/MHWDCONFIG"
    
    # Install PCI-ID file for AMDGPU Cards
    install -D -m644 "${srcdir}/amdgpu-pci.ids" \
    "${pkgdir}/var/lib/mhwd/ids/pci/amdgpu-pci.ids"
    
    
    # Install ATI/Radeon config file
    install -D -m644 "${srcdir}/MHWDCONFIG_video-radeon+" \
    "${pkgdir}/var/lib/mhwd/db/pci/graphic_drivers/video-radeon+/MHWDCONFIG"
    
    # install catalystuninstaller
    install -D -m755 "${srcdir}/catalystuninstall" \
    "${pkgdir}/usr/bin/catalystuninstall"
}

package_amdgpu-experimental()
{
    pkgdesc="Enables experimental AMDGPU support for Sea Islands(GCN1.1) and Southern Islands(GCN 1.0)"
    depends=('linux>=4.9')
    install='amdgpu-experimental.install'
    
    cd "${srcdir}"

    ##################################################
    ##
    ## install module-load file 
    ## and radeon blacklist
    ## and exp_hw_support
    ## and hook
    ##
    ##################################################
    install -D -m644 "${srcdir}/amdgfx-hwe-amdgpu.conf" \
    "${pkgdir}/etc/modules-load.d/amdgfx-hwe-amdgpu.conf"
    install -D -m644 "${srcdir}/amdgfx-hwe-radeon_blacklist.conf" \
    "${pkgdir}/etc/modprobe.d/amdgfx-hwe-radeon_blacklist.conf"
    install -D -m644 "${srcdir}/amdgpu-exp_hw_support.conf" \
    "${pkgdir}/etc/modprobe.d/amdgpu-exp_hw_support.conf"
    install -D -m644 "${srcdir}/amdgpu-experimental.hook" \
    "${pkgdir}/usr/share/libalpm/hooks/98-amdgpu-experimental.hook"
}
