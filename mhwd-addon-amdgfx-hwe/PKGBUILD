# Maintainer: Fabian Bornschein <plusfabi@gmail.com>
pkgname=('mhwd-addon-amdgfx-hwe' 'amdgpu-experimental')
pkgver=20170520 #date +%Y%m%d
pkgrel=1
arch=('any')
url="https://github.com/Tids/mhwd-addon-amdgfx-hwe"
license=('GPL3')
makedepends=('curl')
conflicts=('mhwd-addon-amdgpu-hwe')
source=('amdgpu-experimental.install'
        'mhwd-addon-amdgfx-hwe.install'
        '10-amdgpu-experimental.conf'
        'amdgfx-hwe-amdgpu.conf'
        'amdgfx-hwe-radeon_blacklist.conf'
        'amdgpu-exp_hw_support.conf'
        'amdgpu-experimental.hook'
        'MHWDCONFIG_video-amdgpu+'
        'MHWDCONFIG_video-amdgpu-experimental+'
        'MHWDCONFIG_video-radeon+'
        'MHWDCONFIG_video-intel+'
        'https://raw.githubusercontent.com/pciutils/pciids/master/pci.ids')
sha512sums=('d15f690fac33b1ed33677ff40bd998c137bb2b65b41c8c0c3a8634977aee83379ebf7e8d81951c4673b46f7fb810147936612f59360f3140100d8d2a147f8d9f'
            'e9db80cddd6daec4352c08d062856f73ff9827a8c4069847c2b3ecf669cfb3355c26f418c491d1a2224b4ac71e119e16359b86b322d22c936639facfa3e96e50'
            'ce6c1e475e7f06aea45d2ce7a2529a511484b2e06917873d1ea10cf820945a5a3d69853d49e8aba07fcb0d01466c66130887139b9ad9cd8b21b9e39a6a760486'
            'f56b5293f88727bb4fcceb2a347527402dd0938e96403461b1e2a6821cc00a4e680f58770addd7ba1ded20a187f91ccfd6270bc8bd28c6eae75cc075952361ca'
            '04ef02734903c5b13d1a808025f104ce324f30bafb6bd42c31abbbfc43fefdfff7a8d4f910590717f7ca4bae21d02062b54a15d2e47c2174c59ed975022cf0f7'
            '770b812001ac43a0f06ebfb981cb076d0291f7da2ca6ffd469defae37c37f950659b59492c292cdf6b15597409796ecdcf65c3a168a47ea8b33dae0d49032f7d'
            '42106abe5bfedd63f7c2e06e2e362ab75060ced6f1e3a86502870ff0248a40d404fd415742f2a0bbfa1c55c479af9dee4c08c9191793b86bd7a99d06f518abc3'
            'dac0521893caa7f4f6a72b939d4c44e147a0f5522630d49d5d0a054de414b8a2a5a31da921db91042da9bf6736b712dac75466ce818d898fac5f6a356a0bc418'
            'c23a0454b48bec4789dbf1336675138d11fd1e701a07abe508d76ffbb5708ee1eec05396c83ac04e9799b1a1c2df2ab980f6a8b003fea7fbba50f2156b2a8f26'
            '0888e47dd475f83a45cc6367e12681c4e865bfc347caf22089ba76843b893541be6ab5a783454113aedc924c4594bd6a083a7b1c815300c8ec6abed240694757'
            'a45cc9c40103b540567c16311f38b96f9ffb95da65401165ffc5e5efcb743272378ea472818e789e44d4db1706940a6d3bc08d7aebe00c780a1ca594c6840498'
            'af6ed7557b9129eb7d4fcc50fc4bb530095299fe510e61abd89899801081df95561292e84a95b9e3864e75bc7f987b06627751a15f14c8a9990b52b29ac3ae41')

prepare() {
    AMDGPUPCIFILE="amdgpu-pci.ids"
    # Names of supported hardware generations
    AMDGPUNAMES=("Bristol Ridge"
                    "Raven Ridge"
                    "Stoney Ridge"
                    "Fiji"
                    "Tonga"
                    "Amethyst"
                    "Polaris"
                    "Polaris10"
                    "Polaris11"
                    "Polaris12"
                    "Baffin"
                    "Ellesmere"
                    "Lexa"
                    "Vega")

    # Names of experimental supported hardware generations
    XAMDGPUPCIFILE="amdgpu-experimental-pci.ids"
    XAMDGPUNAMES=("Chelsea"
                    "Cape Verde"
                    "Heathrow"
                    "Wimbledon"
                    "Pitcairn"
                    "Sun"
                    "Oland"
                    "Mars"
                    "Venus"
                    "Neptune"
                    "Jet Pro"
                    "Iceland"
                    "Opal"
                    "Topaz"
                    "Exo Pro"
                    "Meso"
                    "Strato Pro"
                    "Bonaire"
                    "Saturn"
                    "Strato"
                    "Roland"
                    "Tropo"
                    "Hawaii"
                    "Vesuvius"
                    "Tahiti"
                    "New Zealand"
                    "Malta"
                    "Kaveri"
                    "Kabini"
                    "Temash"
                    "Beema"
                    "Mullins"
                    "Curacao"
                    "Grenada")

    # Check for file
    if [ ! -f "${srcdir}/pci.ids" ] || ! grep "List of PCI ID's" "${srcdir}/pci.ids" > /dev/null
        then
            echo "Missing or wrong source PCIID-file"
            exit 1
        fi

    ## Extract AMD part
    # Remove everything before ^1002
    sed -i -n '/^1002/,$p' "${srcdir}/pci.ids"
    # Remove everything below ^1003
    sed -i '/^1003/,$d' "${srcdir}/pci.ids"
    # Remove comments
    sed -i '/^#/ d' "${srcdir}/pci.ids"
    # Leading tab
    sed -i 's/^\t//g' "${srcdir}/pci.ids"
    # Remove sub-IDs
    sed -i '/^\t/ d' "${srcdir}/pci.ids"

    # AMDGPU
    # Create the final output file
    echo -e "# List generated by mhwd. Do not edit manually." > "${srcdir}/${AMDGPUPCIFILE}"

    # Loop this part for every Hardware generation
    for (( i=0; i<${#AMDGPUNAMES[@]}; i++ ));
    do
        # Ignore generation without entry
        if grep -wi " ${AMDGPUNAMES[i]}" "${srcdir}/pci.ids" > /dev/null
            then
                echo -e "\n\n# ${AMDGPUNAMES[i]}\n" >> "${srcdir}/${AMDGPUPCIFILE}"
                # Add all the ID's
                grep -wi " ${AMDGPUNAMES[i]}" "${srcdir}/pci.ids" | grep -vi "audio" | grep -v "#" | cut -d" " -f1 >> "${srcdir}/${AMDGPUPCIFILE}"
        fi
    done

    # XAMDGPU
    # Create the final output file
    echo -e "# List generated by mhwd. Do not edit manually." > "${srcdir}/${XAMDGPUPCIFILE}"

    # Loop this part for every Hardware generation
    for (( i=0; i<${#XAMDGPUNAMES[@]}; i++ ));
    do
        # Ignore generation without entry
        if grep -wi " ${XAMDGPUNAMES[i]}" "${srcdir}/pci.ids" > /dev/null
            then
                echo -e "\n\n# ${XAMDGPUNAMES[i]}\n" >> "${srcdir}/${XAMDGPUPCIFILE}"
                # Add all the ID's
                grep -wi " ${XAMDGPUNAMES[i]}" "${srcdir}/pci.ids" | grep -vi "audio" | grep -v "#" | cut -d" " -f1 >> "${srcdir}/${XAMDGPUPCIFILE}"
        fi
    done

    # Cleanup
    if [ -f "${srcdir}/pci.ids" ]
        then
            rm "${srcdir}/pci.ids"
    fi
}

package_mhwd-addon-amdgfx-hwe()
{
    pkgdesc="Enables experimental AMDGPU support for Sea Islands(GCN1.1) and Southern Islands(GCN 1.0)"
    depends=('bash')
    optdepends=('radeonjet-git: AMDGPU overclocking'
                'radeontop: Radeon cards monitoring utility')
    install='mhwd-addon-amdgfx-hwe.install'

    cd "${srcdir}"

    # Install AMDGPU-experimental config file
    install -D -m644 "${srcdir}/MHWDCONFIG_video-amdgpu-experimental+" \
    "${pkgdir}/var/lib/mhwd/db/pci/graphic_drivers/video-amdgpu-experimental+/MHWDCONFIG"

    # Install PCI-ID file for AMDGPU-experimental Cards
    install -D -m644 "${srcdir}/amdgpu-experimental-pci.ids" \
    "${pkgdir}/var/lib/mhwd/ids/pci/amdgpu-experimental-pci.ids"


    # Install AMDGPU config file
    install -D -m644 "${srcdir}/MHWDCONFIG_video-amdgpu+" \
    "${pkgdir}/var/lib/mhwd/db/pci/graphic_drivers/video-amdgpu+/MHWDCONFIG"

    # Install PCI-ID file for AMDGPU Cards
    install -D -m644 "${srcdir}/amdgpu-pci.ids" \
    "${pkgdir}/var/lib/mhwd/ids/pci/amdgpu-pci.ids"


    # Install ATI/Radeon config file
    install -D -m644 "${srcdir}/MHWDCONFIG_video-radeon+" \
    "${pkgdir}/var/lib/mhwd/db/pci/graphic_drivers/video-radeon+/MHWDCONFIG"

    # Install Intel config file
    install -D -m644 "${srcdir}/MHWDCONFIG_video-intel+" \
    "${pkgdir}/var/lib/mhwd/db/pci/graphic_drivers/video-intel+/MHWDCONFIG"
}

package_amdgpu-experimental()
{
    pkgdesc="Enables experimental AMDGPU support for Sea Islands(GCN1.1) and Southern Islands(GCN 1.0)."
    depends=('linux>=4.9')
    conflicts=('xf86-video-ati')
    install='amdgpu-experimental.install'

    cd "${srcdir}"

    ##################################################
    ##
    ## install module-load file,
    ## radeon blacklist,
    ## exp_hw_support,
    ## pacman hook,
    ## amdgpu-experimental xorg.config
    ##
    ##################################################
    install -D -m644 "${srcdir}/amdgfx-hwe-amdgpu.conf" \
    "${pkgdir}/usr/lib/modules-load.d/amdgfx-hwe-amdgpu.conf"
    install -D -m644 "${srcdir}/amdgfx-hwe-radeon_blacklist.conf" \
    "${pkgdir}/usr/lib/modprobe.d/amdgfx-hwe-radeon_blacklist.conf"
    install -D -m644 "${srcdir}/amdgpu-exp_hw_support.conf" \
    "${pkgdir}/usr/lib/modprobe.d/amdgpu-exp_hw_support.conf"
    install -D -m644 "${srcdir}/amdgpu-experimental.hook" \
    "${pkgdir}/usr/share/libalpm/hooks/98-amdgpu-experimental.hook"
    install -D -m644 "${srcdir}/10-amdgpu-experimental.conf" \
    "${pkgdir}/usr/share/X11/xorg.conf.d/10-amdgpu-experimental.conf"
}
