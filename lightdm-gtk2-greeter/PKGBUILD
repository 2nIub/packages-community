# Maintainer : Rob McCathie <rob@manjaro.org>
# Contributor: Alucryd  <alucryd at gmail dot com>

pkgname=lightdm-gtk2-greeter
pkgver=1.8.6
pkgrel=0.3
epoch=1
pkgdesc='GTK+ greeter for LightDM'
arch=('i686' 'x86_64')
url='https://launchpad.net/lightdm-gtk-greeter'
license=('GPL3' 'LGPL3')
depends=('gtk2' 'lightdm')
makedepends=('exo' 'gnome-common' 'gnome-doc-utils' 'gobject-introspection' 'intltool')
backup=('etc/lightdm/lightdm-gtk-greeter.conf')
install="${pkgname}.install"
source=("${pkgname}-${pkgver}.tgz::http://bazaar.launchpad.net/~lightdm-gtk-greeter-team/lightdm-gtk-greeter/trunk/tarball/299"
        'panel-at-bottom-option.patch')
# stupid launchpad, we still have to SKIP even when we want a specific commit
sha1sums=('SKIP'
          '38d5a8defefe99ad2bd06ae31e6b015fcd12cf4c')

prepare() {
  cd "~lightdm-gtk-greeter-team/lightdm-gtk-greeter/trunk"

  patch -p1 -i "${srcdir}/panel-at-bottom-option.patch"
}

build() {
  cd "~lightdm-gtk-greeter-team/lightdm-gtk-greeter/trunk"

  ./autogen.sh --prefix='/usr' --sbindir='/usr/bin' --sysconfdir='/etc' --libexecdir='/usr/lib/lightdm' --disable-static --with-gtk2
  make
}

package() {
  cd "~lightdm-gtk-greeter-team/lightdm-gtk-greeter/trunk"

  make DESTDIR="${pkgdir}" install
}
