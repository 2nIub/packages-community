# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Contributor: artoo <artoo@manjaro.org>

_profiles=('kde'
	'kde-minimal'
	'xfce'
	'xfce-minimal'
	'bspwm'
	'budgie'
	'cinnamon'
	'deepin'
	'gnome'
	'i3'
	'lxde'
	'lxqt'
	'lxqt-minimal'
	'mate'
	'netbook'
	'pantheon')

pkgbase=desktop-settings
pkgname=('manjaro-bspwm-settings'
	'manjaro-budgie-settings'
	'manjaro-cinnamon-settings'
	'manjaro-deepin-settings'
	'manjaro-gnome-settings'
	'manjaro-i3-settings'
	'manjaro-lxde-settings'
	'manjaro-lxqt-settings'
	'manjaro-mate-settings'
	'manjaro-netbook-settings'
	'manjaro-pantheon-settings')
pkgver=20160223
pkgrel=1
arch=('any')
url="https://github.com/manjaro/desktop-settings"
license=('GPL')
depends=('manjaro-base-skel')
makedepends=('git')
source=("git+$url.git")
sha256sums=('SKIP')
install=$pkgbase.install

_inst_dir(){
	if [[ -d $srcdir/$pkgbase/$1/skel ]];then
		install -d $pkgdir/etc
		cp -r $srcdir/$pkgbase/$1/skel $pkgdir/etc
	fi

	if [[ -d $srcdir/$pkgbase/$1/scripts ]]; then
		install -d $pkgdir/usr/bin
		cp $srcdir/$pkgbase/$1/scripts/* $pkgdir/usr/bin
	fi

	if [[ -d $srcdir/$pkgbase/$1/schemas ]]; then
		install -d $pkgdir/usr/share/glib-2.0/schemas
		cp $srcdir/$pkgbase/$1/schemas/* $pkgdir/usr/share/glib-2.0/schemas
	fi
}

_get_conflicts(){
	local c=()
	for p in ${_profiles[@]};do
		if [[ "$pkgname" != "manjaro-$p-settings" ]];then
			c+=("manjaro-$p-settings")
		fi
	done
	echo ${c[@]}
}

pkgver() {
	date +%Y%m%d
}

package_manjaro-bspwm-settings() {
	pkgdesc='Manjaro Linux bdspwm settings'
	conflicts=( $(_get_conflicts) )
	depends=('bspwm-wallpapers'
	'menda-circle-icon-theme'
	'gtk-theme-maia'
	'xcursor-breeze'
	'feh'
	'rootmenu'
	'mygtkmenui'
	'limepanel'
	'bspwm-manjaro'
	'bspwm-scripts'
	'sxhkd')
	_inst_dir 'community/bspwm'
}

package_manjaro-budgie-settings() {
	pkgdesc='Manjaro Linux budgie settings'
	conflicts=( $(_get_conflicts) )

	_inst_dir 'community/budgie'
}

package_manjaro-cinnamon-settings() {
	pkgdesc='Manjaro Linux cinnamon settings'
	depends=('artwork-cinnamon-minimal'
	'cantarell-fonts'
	'maia-icon-theme'
	'reset-gsettings'
	'vertex-maia-themes'
	'xcursor-simpleandsoft')
	conflicts=( $(_get_conflicts) )

	_inst_dir 'community/cinnamon'
}

package_manjaro-deepin-settings() {
	pkgdesc='Manjaro Linux deepin settings'
	groups=('deepin-manjaro')
	conflicts=( $(_get_conflicts) )

	_inst_dir 'community/deepin'
}

package_manjaro-gnome-settings() {
	pkgdesc='Manjaro Linux gnome settings'
	conflicts=( $(_get_conflicts) )

	_inst_dir 'community/gnome'
}

package_manjaro-i3-settings() {
	pkgdesc='Manjaro Linux i3 settings'
	conflicts=( $(_get_conflicts) )

	_inst_dir 'community/i3'
}

package_manjaro-lxde-settings() {
	pkgdesc='Manjaro Linux lxde settings'
	conflicts=( $(_get_conflicts) )

	_inst_dir 'community/lxde'
}

package_manjaro-lxqt-settings() {
	pkgdesc='Manjaro Linux lxqt settings'
	conflicts=( $(_get_conflicts) )

	_inst_dir 'community/lxqt'
}

package_manjaro-mate-settings() {
	pkgdesc='Manjaro Linux mate settings'
	conflicts=( $(_get_conflicts) )

	_inst_dir 'community/mate'
}

package_manjaro-netbook-settings() {
	pkgdesc='Manjaro Linux netbook settings'
	conflicts=( $(_get_conflicts) )

	_inst_dir 'community/netbook'
}

package_manjaro-pantheon-settings() {
	pkgdesc='Manjaro Linux pantheon settings'
	conflicts=( $(_get_conflicts) )

	_inst_dir 'community/pantheon'
}
