# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Contributor: Philip MÃ¼ller <philm@manjaro.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas

pkgname=breeze-maia-icon-themes
_pkgname=breeze-icons
pkgver=5.21.0
pkgrel=6
pkgdesc='Breeze icon themes with Maia colors'
arch=('any')
url='https://community.kde.org/Frameworks'
_commit=8984eb645635cd934c6bbc9a90fad2386fd4dc4a
license=('LGPL')
makedepends=('extra-cmake-modules' 'qt5-base')
source=("$_pkgname.tar.gz::https://github.com/KDE/$_pkgname/archive/$_commit.tar.gz")
md5sums=('0939351d5cdd7d7f514fe776e13d2f0c')

prepare() {
  mkdir build
  cd $_pkgname-$_commit
}

build() {
  cd $srcdir/build
  cmake ../$_pkgname-$_commit \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  msg "Adjusting icon colors..."
  cd $pkgdir/usr/share/icons
  find . ! -type l -name "*.svg" -exec sed -i "s/147eb8/338676/g;s/3daee9/16a085/g;s/6cc1ef/32B39A/g;s/1272a5/0c7869/g" '{}' \;
  find . -name "index.*" -exec sed -i "s/Breeze/Breeze-Maia/g;s/Maia Dark/Maia-Dark/g" '{}' \;
  mv breeze Breeze-Maia
  mv breeze-dark Breeze-Maia-Dark
}
