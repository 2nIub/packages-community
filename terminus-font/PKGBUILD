# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Kristoffer Fossgård <kfs1@online.no>
# Contributor: clonejo <clonejo@shakik.de>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Chrysostomus

pkgname=terminus-font
pkgver=4.46
pkgrel=1.1
pkgdesc='Monospace bitmap font (for X11 and console)'
arch=('any')
url='http://sourceforge.net/projects/terminus-font/'
license=('GPL2' 'custom:OFL')
makedepends=('python3' 'xorg-bdftopcf' 'xorg-mkfontdir' 'xorg-mkfontscale')
optdepends=('xorg-fonts-alias')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-font-utils')
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname-$pkgver/$pkgname-$pkgver.tar.gz"
        33-TerminusPcfFont.conf)
sha256sums=('4e29433e5699b76df1f5c9a96f1228cccf8ea8a16791cfef063f2b8506c75bcd'
            '34c75681702cbb9743efc32d51c61dcca9d4f05e8caa000107e694c6350d5d46')

prepare() {
  chmod +x "$pkgname-$pkgver/configure"
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --x11dir=/usr/share/fonts/misc \
    --psfdir=/usr/share/kbd/consolefonts
  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/$pkgname-$pkgver/75-yes-terminus.conf" \
    "$pkgdir/etc/fonts/conf.avail/75-yes-terminus.conf"
  install "$srcdir/33-TerminusPcfFont.conf" \
    "$pkgdir/etc/fonts/conf.avail/33-TerminusPcfFont.conf"
  install -Dm644 "$srcdir/$pkgname-$pkgver/OFL.TXT" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  
  install -d "$pkgdir/etc/fonts/conf.d"
  cd "$pkgdir/etc/fonts/conf.d"
  ln -s ../conf.avail/75-yes-terminus.conf .
  ln -s ../conf.avail/33-TerminusPcfFont.conf .
}
