# $Id$
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: K900 <k0009000@gmail.com>
# Manjaro implementation: Alexandru Ianu <alexandru@manjaro.org>
pkgname=steam-manjaro
_pkgname=steam
pkgver=1.0.0.50
pkgrel=1
pkgdesc="Digital distribution client bootstrap package. Implementation for bleeding edge Arch-based rolling systems."
arch=("i686" "x86_64")
url="http://steampowered.com/"
license=('custom')
install=steam.install
source=("http://repo.steampowered.com/$_pkgname/pool/$_pkgname/s/$_pkgname/${_pkgname}_$pkgver.tar.gz"
        'lib32-flashplugin-path.patch'
        'alsa_sdl_audiodriver.patch'
        'lib32_path_and_steam_runtime.patch'
        'lib_path_and_steam_runtime.patch'
        'steam.desktop'
        'steam-valve.desktop'
        'steam-clean.desktop'
        'steam-clean')
sha256sums=('be6388c0ab36799d6173276ac75b4d92c6623a53d901395c9fc1963cfd245573'
            '87b9e9f74709a43fb8424d7bb3074fb576f72c56d78a045e3b4ed4719600b0fa'
            '174a110eda1c9d5b1c92a4490b266b31918559bbf8292a94905221c92da4bc0e'
            '33a5fd9858449af19fecf37ca85c805e1d1e800715485795e7326dc182182c36'
            'c26b486b0b4780cf13c712e23873850b65ffad824c4e735dc9e3ddc35170db0d'
            '4cd23352ffd402830e47e824282b6dbdd51b2ae4c902d71e4f1909bf5a080477'
            '9c4c59ad68895ca9ec5d174ec68f2e39d8ac9ec31e5f5e4a1b25100e8c40bbc1'
            '3273349686e2d80420f060e089456884913fd6ed469eb7f03805b6aa2a271be9'
            '8bbcbd2f29ef1ac62b49d9c2770be9140a447301dcdeb83c94d1f3800228b360')

provides=('steam' 'steam-native')
replaces=('steam-native')

if [[ "$CARCH" == "x86_64" ]]; then
prepare() {
    patch -d "$_pkgname" -Np1 -i "$srcdir/lib32-flashplugin-path.patch"
    patch -d "$_pkgname" -Np1 -i "$srcdir/alsa_sdl_audiodriver.patch"
    patch -d "$_pkgname" -Np1 -i "$srcdir/lib32_path_and_steam_runtime.patch"
}
else
prepare() {
    patch -d "$_pkgname" -Np1 -i "$srcdir/alsa_sdl_audiodriver.patch"
    patch -d "$_pkgname" -Np1 -i "$srcdir/lib_path_and_steam_runtime.patch"
}
fi

package() {
    depends=(
      'bash' 'desktop-file-utils' 'hicolor-icon-theme' 'curl'
      'dbus' 'freetype2'  'gdk-pixbuf2' 'ttf-font' 'zenity'
      'libindicator-gtk2' 'libindicator-gtk3' 'dhcp-client'
      'networkmanager' 'wpa_supplicant' 'mono' 'mono-addins'
      )

    if [[ "$CARCH" == x86_64 ]]; then
        depends+=('lib32-libgl' 'lib32-gcc-libs' 'lib32-libx11')
        #Steam native deps
        depends+=('iproute2' 'lib32-cracklib' 'lib32-dbus-glib' 'lib32-gconf' 'lib32-glew1.10'
                  'lib32-gdk-pixbuf2' 'lib32-gtk2' 'lib32-js17' 'lib32-libappindicator'
                  'lib32-libgcrypt15' 'lib32-libjpeg6' 'lib32-libnl' 'lib32-libnm-glib46'  
                  'lib32-nss' 'lib32-libpng12' 'lib32-libpulse' 'lib32-libsoup' 'lib32-libtirpc'
                  'lib32-libudev0' 'lib32-libusb' 'lib32-libvpx' 'lib32-libxinerama' 
                  'lib32-openal' 'lib32-pam' 'lib32-pango' 'lib32-polkit')
        #Common game deps
        depends+=('lib32-sdl' 'lib32-sdl_image' 'lib32-sdl_mixer' 'lib32-sdl2' 'lib32-sdl2_image'
                  'lib32-tcp_wrappers' 'lib32-speex' 'lib32-gperftools' 'lib32-libcurl-gnutls')
        optdepends=(
            'lib32-mesa: for open source driver users'
            'lib32-catalyst-utils: for AMD Catalyst users'
            'lib32-nvidia-utils: for NVIDIA proprietary blob users'
            'lib32-flashplugin: for flash video'
            'lib32-alsa-plugins: for pulseaudio on some games'
        )
    else
        #Steam native deps
        depends+=('cracklib' 'dbus-glib' 'gconf' 'gtk2' 'js17' 'libgcrypt15' 'libnl' 'nss'
                  'libpng12' 'libpulse' 'libsoup' 'libtirpc' 'libudev0' 'libusb' 'libvpx'
                  'libxinerama' 'openal' 'pam' 'pango' 'polkit')
        #Common game deps
        depends+=('sdl' 'sdl_image' 'sdl_mixer' 'sdl2' 'sdl2_image' 'tcp_wrappers' 'speex' 'gperftools' 'libcurl-gnutls')
        optdepends=('flashplugin: for flash videos')
    fi

    make -C "$_pkgname" DESTDIR="$pkgdir" install

    # Install license
    install -Dm644 "$pkgdir/usr/share/doc/steam/steam_install_agreement.txt" "$pkgdir/usr/share/licenses/steam/LICENSE"

    # blank steamdeps because apt-get
    ln -sf /bin/true "$pkgdir/usr/bin/steamdeps"

    # Replace orig launcher
    rm "$pkgdir/usr/share/applications/steam.desktop"
    cp "$srcdir/steam.desktop" "$pkgdir/usr/share/applications/steam.desktop"
    chmod +x "$pkgdir/usr/share/applications/steam.desktop"

    cp "$srcdir/steam-valve.desktop" "$pkgdir/usr/share/applications/steam-valve.desktop"
    chmod +x "$pkgdir/usr/share/applications/steam-valve.desktop"
    cp "$srcdir/steam-clean.desktop" "$pkgdir/usr/share/applications/steam-clean.desktop"
    chmod +x "$pkgdir/usr/share/applications/steam-clean.desktop"
    mkdir -p "$pkgdir/usr/bin"
    cp "$srcdir/steam-clean" "$pkgdir/usr/bin/steam-clean"
    chmod +x "$pkgdir/usr/bin/steam-clean"
}
