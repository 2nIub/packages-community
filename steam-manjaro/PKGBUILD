# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: K900 <k0009000@gmail.com>
# Manjaro implementation: Alexandru Ianu <alexandru@manjaro.org>
# Native config contribution: Manuel HÃ¼sers <manuel.huesers@uni-ol.de>
pkgname=steam-manjaro
_pkgname=steam
pkgver=1.0.0.53
pkgrel=1
pkgdesc="Digital distribution client bootstrap package. Includes common game depends. See https://forum.manjaro.org/index.php?topic=16054 and https://bugs.manjaro.org/index.php?do=details&task_id=45"
arch=("i686" "x86_64")
url="http://steampowered.com/"
license=('custom')

depends=('bash' 'desktop-file-utils' 'hicolor-icon-theme' 'curl'
         'dbus' 'freetype2'  'gdk-pixbuf2' 'ttf-font' 'zenity'
         'libindicator-gtk2' 'libindicator-gtk3' 'dhcp-client'
         'networkmanager' 'wpa_supplicant' 'mono' 'mono-addins')
depends_x86_64=('lib32-libgl' 'lib32-gcc-libs' 'lib32-libx11')

#Common game deps
depends+=('wqy-microhei' 'sdl' 'sdl_image' 'sdl_mixer' 'sdl2' 'sdl2_image' 'tcp_wrappers' 
         'speex' 'gperftools' 'libcurl-gnutls' 'rtmpdump' 'gnutls28' 'nettle4')
depends_x86_64+=('lib32-sdl' 'lib32-sdl_image' 'lib32-sdl_mixer' 'lib32-sdl2' 'lib32-sdl2_image'
         'lib32-tcp_wrappers' 'lib32-speex' 'lib32-gperftools' 'lib32-libcurl-gnutls' 
         'lib32-rtmpdump' 'lib32-libgnutls28' 'lib32-nettle4')


optdepends_i686=('flashplugin: for flash videos')
optdepends_x86_64=('lib32-mesa: for open source driver users'
         'lib32-catalyst-utils: for AMD Catalyst users'
         'lib32-nvidia-utils: for NVIDIA proprietary blob users'
         'lib32-flashplugin: for flash video'
         'lib32-alsa-plugins: for pulseaudio on some games')

provides=('steam')
conflicts=('steam')
install=steam.install
source=("http://repo.steampowered.com/${_pkgname}/pool/${_pkgname}/s/${_pkgname}/${_pkgname}_${pkgver}.tar.gz"
        'lib32-flashplugin-path.patch'
        'alsa_sdl_audiodriver.patch'
        'lib32_path_and_steam_runtime.patch'
        'lib_path_and_steam_runtime.patch' #removed steam-clean and steam-clean.desktop
        'steam.desktop'
        '51-steam.rules'
        '80-steam-controller-permission.rules'
	'mesa_gfx_lpreload.patch')
sha256sums=('10932f2e58b0d0288e0820b8e97e6f3112409f770ecb64c3cd44e87450c1c944'
            '87b9e9f74709a43fb8424d7bb3074fb576f72c56d78a045e3b4ed4719600b0fa'
            '174a110eda1c9d5b1c92a4490b266b31918559bbf8292a94905221c92da4bc0e'
            '449532fd8e8d9c55d1b0f98b98855829453d1ec9ea53e82a00e065e17d23bf58'
            '3cdd0888fc84dc04b198bbe1082909f4ed8f220458d5d16d95927efcb1289398'
            'd5b1e77861e92bb43405f25394c0b5d4e5e0cb04b8527ad3e813af9595468756'
            '12d6d0a96e66f10eaeee94ddb6d1048c0e40a8132521fa7b00b844d6454d641c'
            '99123198a5c15b23102e8b0f8349de498b25331f6803b2f8c02f804f26470588'
            '547633065f5d8ab3f02a1653d9bbb5d7aa06c3ae1921b8a3391b988a7c6ce629')

if [[ "$CARCH" == "x86_64" ]]; then
prepare() {
    patch -d "${_pkgname}" -Np1 -i "${srcdir}/lib32-flashplugin-path.patch"
    patch -d "${_pkgname}" -Np1 -i "${srcdir}/alsa_sdl_audiodriver.patch"
    patch -d "${_pkgname}" -Np1 -i "${srcdir}/lib32_path_and_steam_runtime.patch"
    patch -d "${_pkgname}" -Np1 -i "${srcdir}/mesa_gfx_lpreload.patch"
}
else
prepare() {
    patch -d "${_pkgname}" -Np1 -i "${srcdir}/alsa_sdl_audiodriver.patch"
    patch -d "${_pkgname}" -Np1 -i "${srcdir}/lib_path_and_steam_runtime.patch"
    patch -d "${_pkgname}" -Np1 -i "${srcdir}/mesa_gfx_lpreload.patch"
}
fi

package() {
    make -C "${_pkgname}" DESTDIR="${pkgdir}" install

    # Install license
    install -Dm644 "${pkgdir}/usr/share/doc/steam/steam_install_agreement.txt" "${pkgdir}/usr/share/licenses/steam-manjaro/LICENSE"

    # blank steamdeps because apt-get
    ln -sf /bin/true "${pkgdir}/usr/bin/steamdeps"

    install -Dm644 ${srcdir}/80-steam-controller-permission.rules $pkgdir/usr/lib/udev/rules.d/80-steam-controller-permission.rules

    # Replace orig launcher
    rm "${pkgdir}/usr/share/applications/steam.desktop"
    install -Dm644 'steam.desktop' "${pkgdir}/usr/share/applications/steam.desktop"
    #install -Dm644 'steam-clean.desktop' "${pkgdir}/usr/share/applications/steam-clean.desktop"

    # Library cleaner script (to be used if needed)
    #install -Dm755 'steam-clean' "${pkgdir}/usr/bin/steam-clean"

    # Polkit rules for LibNM
    mkdir -p "${pkgdir}/etc/polkit-1/rules.d"
    chmod 750 "${pkgdir}/etc/polkit-1/rules.d"
    install -Dm644 "${srcdir}/51-steam.rules" "${pkgdir}/etc/polkit-1/rules.d/51-steam.rules"
}
