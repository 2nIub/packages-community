# Maintainer: Ramon Buld√≥ <rbuldo at gmail.com>

pkgname=blueman
pkgver=2.0.rc1
pkgrel=1
pkgdesc="A GTK+ Bluetooth Manager (BlueZ 5)"
arch=('i686' 'x86_64')
license=('GPL')
url="https://github.com/blueman-project/blueman"
depends=('bluez' 'bluez-libs' 'dbus-glib' 'gconf' 'gdk-pixbuf2' 'glib2' 
         'gtk3' 'notification-daemon' 'libnotify' 'polkit' 'pango' 
         'python-cairo' 'python-dbus' 'python-gobject')
optdepends=('dnsmasq: DHCP server for Network Access Point'
            'pulseaudio: for some audio support')
makedepends=('cython' 'intltool' 'libtool')
conflicts=('blueman-git' 'blueman-bzr')
source=("https://github.com/blueman-project/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('4373e21e26260bb53a1180b1e9a476b9d91543c3bf291eb0d867ba20facb28e8')
install=${pkgname}.install

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/${pkgname} \
        --disable-static \
        --disable-schemas-compile
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
