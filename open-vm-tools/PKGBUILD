# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Krzysztof Raczkowski <raczkow@gmail.com>

pkgname=open-vm-tools
epoch=4
pkgver=9.4.6
_pkgsubver=1770165
pkgrel=1
pkgdesc="The Open Virtual Machine Tools (open-vm-tools) are the open source implementation of VMware Tools"
arch=('i686' 'x86_64')
url="http://open-vm-tools.sourceforge.net/"
license=('LGPL')
depends=('libdnet' 'icu' 'procps-ng' 'uriparser' 'libsigc++' 'libxss'
         'iproute2' 'fuse' 'lsb-release')
makedepends=('chrpath' 'doxygen' 'gtkmm' 'fuse' 'libxtst')
optdepends=('gtkmm: DnD/CP plugin'
	    'libxtst: DnD/CP, resolution set plugins'
	    'netctl: suspend-resume network state'
	    'networkmanager: suspend-resume network state')
backup=('etc/pam.d/vmware-guestd')
options=('docs')
install=$pkgname.install

source=(http://downloads.sourceforge.net/$pkgname/$pkgname-${pkgver}-${_pkgsubver}.tar.gz
	network-script
	tools.conf
	vmtoolsd.service
	vmware-guestd
	xautostart.conf
	)

sha256sums=('54d7a83d8115124e4b809098b08d7017ba50828801c2f105cdadbc85a064a079'
            'c4add159c534384eb07e5e9a0694a60c253341916eca928f85e75f77c95d2e95'
            'f39403b884d897933d43cdd1093f308016963cf08162b21717240e37f7fd02ab'
            '5a9403f78356873258521644068f2c9639b994d5954e5ad953934136822b2689'
            '53dbc915fb145fd8fbabe0b8be6c5bc25bfc9f40e1be28740fdc0b8e99889b26'
            '6ca56abad77f7d7c0507f2eb5cfb7bdfb14f34f2d392816fb7b384520f12436f')


build() {
  cd "$srcdir/$pkgname-${pkgver}-${_pkgsubver}"
  autoreconf -i
  export CFLAGS="-DGLIB_DISABLE_DEPRECATION_WARNINGS ${CFLAGS}"
  export CUSTOM_PROCPS_NAME="procps"
  sed -i 's|-Werror||g' configure{,.ac}
  ./configure --prefix=/usr --without-kernel-modules --sbindir=/usr/bin
  make
}

package() {
  cd "$srcdir/$pkgname-${pkgver}-${_pkgsubver}"

  make install DESTDIR="$pkgdir"
  install -Dm0644 vmware-user-suid-wrapper/vmware-user.desktop $pkgdir/usr/share/applications/vmware-user.desktop

  # vmware-user XDG autostart
  mkdir -p $pkgdir/etc/xdg/autostart
  ln -s /usr/share/applications/vmware-user.desktop $pkgdir/etc/xdg/autostart/vmware-user.desktop

  install -D -m 755 scripts/common/vmware-xdg-detect-de "$pkgdir"/usr/bin/vmware-xdg-detect-de
  chmod 07755 "$pkgdir"/usr/bin/vmware-user-suid-wrapper

  install -Dm0755 "$srcdir"/network-script $pkgdir/etc/vmware-tools/scripts/vmware/network
  install -Dm0644 "$srcdir"/tools.conf "$pkgdir"/etc/vmware-tools/tools.conf
  install -Dm0644 "$srcdir"/xautostart.conf "$pkgdir"/etc/vmware-tools/xautostart.conf
  install -Dm0644 "$srcdir"/vmware-guestd "$pkgdir"/etc/pam.d/vmware-guestd
  rm -rf "$pkgdir"/usr/etc

  # We don't want a symlink in /sbin
  rm "$pkgdir"/sbin/mount.vmhgfs
  rmdir "$pkgdir"/sbin

  cd "$pkgdir" && find -type f -exec sh -c "file {} | grep ELF >/dev/null && echo {} && chrpath -d {}" \;
  install -Dm644 ${srcdir}/vmtoolsd.service ${pkgdir}/usr/lib/systemd/system/vmtoolsd.service

  rm -f $pkgdir/etc/vmware-tools/scripts/vmware/*.orig
}
