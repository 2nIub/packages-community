# Maintainer: Ramon Buld√≥ <ramon@manjaro.org>

pkgbase=artwork-andromeda
#pkgname=('andromeda-icon-theme' 'plasma5-themes-andromeda' 'sddm-andromeda-theme' 'andromeda-wallpaper')
pkgname=('plasma5-themes-andromeda' 'sddm-andromeda-theme' 'andromeda-wallpaper')
pkgver=0.1.0
pkgrel=1
_gitcommit=749d4d7a263ea83141b594b1a9115de593b8a1f1
url=https://github.com/manjaro/artwork-andromeda/
arch=('any')
license=('LGPL')
makedepends=('extra-cmake-modules' 'plasma-framework')

source=("andromeda-$pkgver-$pkgrel.tar.gz::$url/archive/$_gitcommit.tar.gz")
md5sums=('77963507b7996f17f37e83030594440b')

prepare() {
  mv $srcdir/artwork-andromeda-$_gitcommit $srcdir/andromeda
  mkdir -p build
  # until we have icons
  sed -i '/add_subdirectory(icons)/'d $srcdir/andromeda/CMakeLists.txt
}

build() {
  cd build
  cmake ../andromeda \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package_andromeda-wallpaper() {
  pkgdesc='Andromeda wallpaper'
  install -D ${srcdir}/andromeda/wallpapers/Andromeda/contents/images/1920x1080.png ${pkgdir}/usr/share/backgrounds/andromeda.png
}

package_andromeda-icon-theme() {
  pkgdesc='Andromeda icon theme'
  cd build
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr/share/color-schemes"
  rm -rf "${pkgdir}/usr/share/kservices5"
  rm -rf "${pkgdir}/usr/share/plasma"
  rm -rf "${pkgdir}/usr/share/sddm"
  rm -rf "${pkgdir}/usr/share/wallpapers"
}

package_plasma5-themes-andromeda() {
  pkgdesc='Andromeda theme for KDE Plasma 5'
  depends=('breeze' 'andromeda-icon-theme')
  replaces=('andromeda-themes')
  cd build
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr/share/sddm"
  rm -rf "${pkgdir}/usr/share/icons"
}

package_sddm-andromeda-theme() {
  pkgdesc="Andromeda theme for SDDM"
  depends=('plasma-framework' 'plasma-workspace')
  cd build
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr/share/color-schemes"
  rm -rf "${pkgdir}/usr/share/icons"
  rm -rf "${pkgdir}/usr/share/kservices5"
  rm -rf "${pkgdir}/usr/share/plasma"
  rm -rf "${pkgdir}/usr/share/wallpapers"
}
