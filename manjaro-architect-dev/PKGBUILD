# Maintainer: Chrysostomus @forum.manjaro.org
# Maintainer: Bernhard Landauer <oberon@manjaro.org>

pkgbase=manjaro-architect-dev
pkgname=('manjaro-architect-dev' 'manjaro-architect-dev-launcher')
_pkgname=manjaro-architect
pkgver=0.7.4.r68.g3f0fba7
pkgrel=1
pkgdesc="Manjaro CLI net-installer, forked from the Archlinux Architect"
arch=(any)
url="https://github.com/Chrysostomus/$_pkgname"
license=(GPL2)
depends=('bash'
         'dialog')
makedepends=('git')
conflicts=("$_pkgname")
provides=("$_pkgname")
source=("git+$url.git#branch=master")
md5sums=('SKIP')

prepare() {
    cd $_pkgname
    sed -i "s/architect --/architect-dev --/" bin/setup
}

pkgver() {
    cd $_pkgname
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd $_pkgname
	make PREFIX=/usr
}

package_manjaro-architect-dev() {
    depends+=('f2fs-tools'
             'gptfdisk'
             'manjaro-tools-base'
             'mhwd'
             'nilfs-utils'
             'pacman'
             'pacman-mirrorlist'
             'parted')
    cd $_pkgname
	make PREFIX=/usr DESTDIR=${pkgdir} install
}

package_manjaro-architect-dev-launcher() {
    cd $_pkgname
    install -Dm755 bin/setup $pkgdir/usr/bin/setup
}

