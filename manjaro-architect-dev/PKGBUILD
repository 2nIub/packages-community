# Maintainer: Chrysostomus @forum.manjaro.org
# Maintainer: Bernhard Landauer <oberon@manjaro.org>

pkgbase=manjaro-architect-dev
pkgname=('manjaro-architect-dev' 'manjaro-architect-dev-launcher')
_pkgname=manjaro-architect
_branch=master
#_commit=1036002b2ee35be9b47aa27940f872b45a1644bf
pkgver=0.8.2.r17.g4b944fd
pkgrel=1
pkgdesc="Manjaro CLI net-installer, forked from the Archlinux Architect"
arch=(any)
url="https://github.com/Chrysostomus/$_pkgname"
license=(GPL2)
depends=('bash'
         'dialog')
makedepends=('git')
optdepends=('maia-console'
            'terminus-font')
source=("git+$url.git#branch=$_branch")
#source=("git+$url.git#commit=$_commit")
md5sums=('SKIP')

prepare() {
    cd $_pkgname
    sed -i "s/architect --/architect-dev --/" bin/setup
    sed -i "s/architect)/architect-dev)/" lib/util.sh
}

pkgver() {
    cd $_pkgname
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd $_pkgname
	make PREFIX=/usr
}

package_manjaro-architect-dev() {
    depends+=('f2fs-tools'
             'gptfdisk'
             'manjaro-tools-base'
             'mhwd'
             'nilfs-utils'
             'pacman'
             'pacman-mirrorlist'
             'parted')
    conflicts=("$_pkgname")
    provides=("$_pkgname")
    cd $_pkgname
	make PREFIX=/usr DESTDIR=${pkgdir} install
}

package_manjaro-architect-dev-launcher() {
    conflicts=("${_pkgname}-launcher")
    provides=("${_pkgname}-launcher")
    cd $_pkgname
    install -Dm755 bin/setup $pkgdir/usr/bin/setup
}

