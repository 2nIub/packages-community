# Maintainer: Chrysostomus @forum.manjaro.org
# Maintainer: Bernhard Landauer <oberon@manjaro.org>

_pkgname=manjaro-architect
pkgname=$_pkgname-dev
__pkgname=aif-dev
pkgver=0.7.4.r22.gfabef61
pkgrel=1
pkgdesc="A clone of architect installer modified for manjaro"
arch=(any)
url="https://github.com/Chrysostomus/$__pkgname"
license=(GPL2)
depends=('bash'
        'dialog'
        'f2fs-tools'
        'gptfdisk'
        'manjaro-tools-base'
        'mhwd'
        'nilfs-utils'
        'pacman'
        'pacman-mirrorlist'
        'parted')
makedepends=('git')
conflicts=("$_pkgname")
source=("git+$url.git")
md5sums=('SKIP')

pkgver() {
    cd $__pkgname
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

package () {

    _bindir=$pkgdir/usr/bin
    _sharedir=$pkgdir/usr/share
    _datadir=$_sharedir/aif
    _launchdir=$pkgdir/usr/share
    
    cd $__pkgname
    install -Dm755 $_pkgname $_bindir/$_pkgname
    install -m755 setup $_bindir/setup
    install -m755 ma-launcher $_bindir/ma-launcher
    mkdir -p $_datadir/{package-lists,translations}
    cp -r package-lists $_datadir
    cp -r translations $_datadir
    install -Dm644 $_pkgname.desktop $_sharedir/applications/$_pkgname.desktop
    install -Dm644 $_pkgname.png $_sharedir/icons/hicolor/48x48/apps/$_pkgname.png
    install -Dm644 ma-launcher.desktop $pkgdir/etc/skel/.config/autostart/ma-launcher.desktop
}
