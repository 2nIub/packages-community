# Maintainer: Manuel Hüsers <manuel.huesers@uni-ol.de>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Balló György <ballogyor+arch@gmail.com>
# Contributor: Branchini Massimo <max.bra.gtalk@gmail.com

pkgbase=libdbusmenu
pkgname=("${pkgbase}-gtk2" "${pkgbase}-gtk3" "${pkgbase}-glib")
_actual_ver=12.10.3
_extra_ver=+15.04.20150410.2
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=1
pkgdesc="A library for passing menus over DBus"
arch=('i686' 'x86_64')
url="https://launchpad.net/${pkgbase}"
license=('GPL3')
makedepends=('gnome-common' 'gnome-doc-utils' 'gobject-introspection' 'intltool' 'vala' 'gcc' gtk{2,3})
options=('!emptydirs')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgbase}_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('c15b79464bc6498cb1e912efbe648606b7bf3b5c521c4d4e5c7decf002b4e8362444177922a5abd8a057a803a8cc00e72de6e36209189eb255efb83e2ded0b06')

prepare() {
	cd "${srcdir}"
	rm -rf ${pkgbase}-gtk{2,3} &> /dev/null
	cp -r "${pkgbase}-${_actual_ver}${_extra_ver}" "${pkgbase}-gtk2"
	mv    "${pkgbase}-${_actual_ver}${_extra_ver}" "${pkgbase}-gtk3"
}

build() {
	export HAVE_VALGRIND_TRUE='#'
	export HAVE_VALGRIND_FALSE=''

	cd "${srcdir}/${pkgbase}-gtk2"
	./autogen.sh --prefix='/usr' --sysconfdir='/etc' --localstatedir='/var' \
	             --disable-{dumper,static,tests} --with-gtk=2
	make -j1

	cd "${srcdir}/${pkgbase}-gtk3"
	./autogen.sh --prefix='/usr' --sysconfdir='/etc' --localstatedir='/var' \
	             --disable-{dumper,static,tests} --with-gtk=3
	make -j1
}

package_libdbusmenu-gtk2() {
	pkgdesc+=" (GTK+ 2 library)"
	depends=('gtk2' "${pkgbase}-glib")

	cd "${srcdir}/${pkgname}"
	make -j1 -C "${pkgbase}-glib" DESTDIR="${pkgdir}" install
	make -j1 -C "${pkgbase}-gtk"  DESTDIR="${pkgdir}" install
	make -j1 -C "${pkgbase}-glib" DESTDIR="${pkgdir}" uninstall
}

package_libdbusmenu-gtk3() {
	pkgdesc+=" (GTK+ 3 library)"
	depends=('gtk3' "${pkgbase}-glib")

	cd "${srcdir}/${pkgname}"
	make -j1 -C "${pkgbase}-glib" DESTDIR="${pkgdir}" install
	make -j1 -C "${pkgbase}-gtk"  DESTDIR="${pkgdir}" install
	make -j1 -C "${pkgbase}-glib" DESTDIR="${pkgdir}" uninstall
}

package_libdbusmenu-glib() {
	depends=('glib2' "${pkgbase}-glib")

	cd "${srcdir}/${pkgbase}-gtk3"
	make -j1 -C "${pkgbase}-glib" DESTDIR="${pkgdir}" install
}
