# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# AUR-Maintainer: Nate Simon <aurpkg (at natesimon.net)>

pkgname=xed
pkgver=1.4.3
pkgrel=1
pkgdesc="A small and lightweight text editor. X-Apps Project"
arch=('i686' 'x86_64')
license=('GPL')
groups=('x-apps')
depends=('desktop-file-utils'
    'enchant'
    'gtksourceview3'
    'libpeas'
    'libsm')
makedepends=('gnome-common'
    'gobject-introspection'
    'iso-codes')
url="https://github.com/linuxmint/${pkgname}"
source=("${url}/archive/${pkgver}.tar.gz")
md5sums=('a1d14cb06fe0b0b95ae1f259fad80f0f')

prepare() {
    cd ${srcdir}/${pkgname}-${pkgver}
    # make launcher distinguishable from gedit
    sed -i 's/Text Editor/Xed Text Editor/' data/xed.desktop.in.in
}

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    ./autogen.sh --with-gtk=3.0 --prefix="/usr" \
         --localstatedir="/var" \
         --libexecdir="/usr/lib/xed"
    make
}

package(){
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR="$pkgdir/" install
}
