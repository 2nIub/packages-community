# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=bluedevil1
_pkgname=bluedevil
pkgver=1.3.2
pkgrel=1
pkgdesc='KDE bluetooth framework'
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/extragear/base/bluedevil"
license=('GPL')
depends=('kdebase-workspace' 'libbluedevil1' 'obex-data-server' 'obexd-client')
makedepends=('cmake' 'automoc4')
conflicts=('bluedevil')
install="${_pkgname}.install"
source=("http://download.kde.org/stable/${_pkgname}/${pkgver}/src/${_pkgname}-${pkgver}.tar.xz")
md5sums=('a43e644fe75bd090c47c060dcbdfdb27')

build(){
  mkdir build
  cd build
  cmake ../${_pkgname}-v${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}

