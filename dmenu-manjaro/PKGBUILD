# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Thorsten Töpper <atsutane-tu@freethoughts.de>
# Contributor: Thayer Williams <thayer@archlinux.org>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>

pkgname=dmenu-manjaro
pkgver=4.5
pkgrel=6
pkgdesc="customized recency-aware dmenu for manjaro-i3 with mouse, xft and width-xy support"
arch=('i686' 'x86_64')
license=('MIT')
depends=('sh' 'libxinerama' 'libxft' 'cantarell-fonts')
conflicts=('dmenu' 'dmenu-xft')
provides=('dmenu' 'dmenu-xft')
source=(dmenu-$pkgver.tar.gz
	dmenu-4.5-xft-width-xy.diff
	dmenu-4.5-mouse-support.diff
	dmenu_recency)
md5sums=('9c46169ed703732ec52ed946c27d84b4'
	'8940e6146e71188b6ac0bb2c4414b4e6'
	'eeec3e11ff68f27ebbc3133ad6549f56'
	'7fc92e2dbcf095c4d15f202a91b4a14a')
prepare() {
	cd $srcdir/dmenu-$pkgver
	patch -p1 < ../dmenu-4.5-xft-width-xy.diff
	patch -p1 < ../dmenu-4.5-mouse-support.diff
}
build() {
	cd $srcdir/dmenu-$pkgver
	make \
	X11INC=/usr/include/X11 \
	X11LIB=/usr/lib/X11
}
package() {
	cd $srcdir/dmenu-$pkgver
	make PREFIX=/usr DESTDIR=$pkgdir install
	install -Dm755 "${srcdir}/dmenu_recency" "${pkgdir}/usr/bin/dmenu_recency"
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
