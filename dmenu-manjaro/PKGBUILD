# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Thorsten Töpper <atsutane-tu@freethoughts.de>
# Contributor: Thayer Williams <thayer@archlinux.org>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Contributor: Bharani Deepan <bharanideepan at_ gmail >
pkgname=dmenu-manjaro
pkgver=4.5
pkgrel=5
pkgdesc="customized dmenu for manjaro-i3 with recency-awareness and xft-support"
arch=('i686' 'x86_64')
license=('MIT')
depends=('sh' 'libxinerama' 'libxft' 'cantarell-fonts')
conflicts=('dmenu' 'dmenu-xft')
provides=('dmenu' 'dmenu-xft')
source=(dmenu-$pkgver.tar.gz
        dmenu-4.5-xft.diff
        break-fix.diff
	dmenu_recency)
sha256sums=('082cd698d82125ca0b3989006fb84ac4675c2a5585bf5bb8af0ea09cfb95a850'
            '2bb738db3c4ad2e2841db17f6bfd748eba957f0c147059966d85b94c2b152389'
            '4c14ff3bc6bd6f4b1987c244bb3359beb716dab441088a1ab8fff81285636608'
	    'c8a753185002984642adebae39f11f3ab2b076b04758ca3bdaabb183bee3408d')
prepare() {
	cd $srcdir/dmenu-$pkgver
	patch -p1 < ../dmenu-$pkgver-xft.diff
	patch -p1 < ../break-fix.diff
}
build() {
	cd $srcdir/dmenu-$pkgver
	make
}
package()
{
    cd "$srcdir/dmenu-$pkgver"
    make DESTDIR=$pkgdir PREFIX=/usr install
    install -Dm755 "${srcdir}/dmenu_recency" "${pkgdir}/usr/bin/dmenu_recency"
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
