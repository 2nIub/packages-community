# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Contributor: artoo <artoo@manjaro.org>

pkgbase=desktop-settings
pkgname=('manjaro-openbox-minimal-settings'
	'manjaro-lxqt-minimal-settings'
	'manjaro-bspwm-settings'
	'manjaro-budgie-settings'
	'manjaro-cinnamon-settings'
	'manjaro-deepin-settings'
	'manjaro-gnome-settings'
	'manjaro-i3-settings'
	'manjaro-lxde-settings'
	'manjaro-lxqt-settings'
	'manjaro-mate-settings'
	'manjaro-netbook-settings'
	'manjaro-pantheon-settings')
pkgver=20160308
pkgrel=1
arch=('any')
url="https://github.com/manjaro/desktop-settings"
license=('GPL')
makedepends=('git')
source=("git+$url.git")
sha256sums=('SKIP')
install=$pkgbase.install

_inst_dir(){
	if [[ -d $srcdir/$pkgbase/$1/skel ]];then
		install -d $pkgdir/etc
		cp -r $srcdir/$pkgbase/$1/skel $pkgdir/etc
	fi

	if [[ -d $srcdir/$pkgbase/$1/scripts ]]; then
		install -d $pkgdir/usr/bin
		cp $srcdir/$pkgbase/$1/scripts/* $pkgdir/usr/bin
	fi

	if [[ -d $srcdir/$pkgbase/$1/schemas ]]; then
		install -d $pkgdir/usr/share/glib-2.0/schemas
		cp $srcdir/$pkgbase/$1/schemas/* $pkgdir/usr/share/glib-2.0/schemas
	fi
}

pkgver() {
	date +%Y%m%d
}

package_manjaro-openbox-minimal-settings() {
	pkgdesc='Manjaro Linux openbox-minimal settings'
	depends=('manjaro-base-skel')
# 	depends=('manjaro-base-skel' 'obmenu-generator' 'tint2' 'feh' 'openbox-themes')

	_inst_dir 'minimal/openbox'
}

package_manjaro-lxqt-minimal-settings() {
	pkgdesc='Manjaro Linux lxqt-minimal settings'
# 	depends=('manjaro-openbox-minimal-settings' 'compton' 'qterminal'
# 		'pcmanfm-qt' 'lxqt-panel')
	depends=('manjaro-openbox-minimal-settings')
	conflicts=('manjaro-desktop-settings')
	provides=('manjaro-desktop-settings')

	_inst_dir 'minimal/lxqt'
}

package_manjaro-bspwm-settings() {
	pkgdesc='Manjaro Linux bdspwm settings'
	conflicts=('manjaro-desktop-settings')
	provides=('manjaro-desktop-settings')
	depends=('bspwm-wallpapers'
		'menda-circle-icon-theme'
		'gtk-theme-maia'
		'xcursor-breeze'
		'feh'
		'rootmenu'
		'mygtkmenui'
		'limepanel'
		'bspwm-manjaro'
		'bspwm-scripts'
		'sxhkd')
	_inst_dir 'community/bspwm'
}

package_manjaro-budgie-settings() {
	pkgdesc='Manjaro Linux budgie settings'
        depends=('arc-themes'
		'arc-firefox-theme'
		'maia-icon-theme'
		'budgie-wallpapers'
		'deepin-icon-theme')
	conflicts=('manjaro-desktop-settings')
	provides=('manjaro-desktop-settings')

	_inst_dir 'community/budgie'
}

package_manjaro-cinnamon-settings() {
	pkgdesc='Manjaro Linux cinnamon settings'
	depends=('artwork-cinnamon-minimal'
		'cantarell-fonts'
		'maia-icon-theme'
		'vertex-maia-themes'
		'xcursor-simpleandsoft')
	conflicts=('manjaro-desktop-settings')
	provides=('manjaro-desktop-settings')

	_inst_dir 'community/cinnamon'
}

package_manjaro-deepin-settings() {
	pkgdesc='Manjaro Linux deepin settings'
	groups=('deepin-manjaro')
	depends=('deepin-artwork-themes-manjaro'
		'deepin-desktop-schemas-manjaro'
		'deepin-iconthemes-manjaro'
		'deepin-session-ui-manjaro'
		'deepin-wallpapers'
		'manjaro-base-skel')
	optdepends=('plymouth-theme-manjaro-deepin-circle')
	conflicts=('manjaro-desktop-settings')
	provides=('manjaro-desktop-settings')

	_inst_dir 'community/deepin'
}

package_manjaro-gnome-settings() {
	pkgdesc='Manjaro Linux gnome settings'
	depends=('gnome-wallpapers')
	conflicts=('manjaro-desktop-settings')
	provides=('manjaro-desktop-settings')

	_inst_dir 'community/gnome'
}

package_manjaro-i3-settings() {
	pkgdesc='Manjaro Linux i3 settings'
	groups=('i3-manjaro')
	depends=('artwork-i3'
		'awoken-icon-theme-light'
		'conky-i3'
		'dmenu-manjaro'
		'gtk-theme-numix-solarized'
		'i3-gaps'
		'i3exit'
		'i3-help'
		'i3status-manjaro'
		'manjaro-base-skel'
		'xcursor-chameleon-pearl')
	optdepends=('awoken-icon-theme: full set of AwOken icons with white and dark variations of the theme')
	conflicts=('manjaro-desktop-settings')
	provides=('manjaro-desktop-settings')

	_inst_dir 'community/i3'
}

package_manjaro-lxde-settings() {
	pkgdesc='Manjaro Linux lxde settings'
	depends=('manjaro-base-skel')
	conflicts=('manjaro-desktop-settings')
	provides=('manjaro-desktop-settings')

	_inst_dir 'community/lxde'
}

package_manjaro-lxqt-settings() {
	pkgdesc='Manjaro Linux lxqt settings'
	depends=('manjaro-base-skel')
	conflicts=('manjaro-desktop-settings')
	provides=('manjaro-desktop-settings')

	_inst_dir 'community/lxqt'
}

package_manjaro-mate-settings() {
	pkgdesc='Manjaro Linux mate settings'
	depends=('manjaro-base-skel')
	conflicts=('manjaro-desktop-settings')
	provides=('manjaro-desktop-settings')

	_inst_dir 'community/mate'
}

package_manjaro-netbook-settings() {
	pkgdesc='Manjaro Linux netbook settings'
	depends=('manjaro-base-skel')
	conflicts=('manjaro-desktop-settings')
	provides=('manjaro-desktop-settings')

	_inst_dir 'community/netbook'
}

package_manjaro-pantheon-settings() {
	pkgdesc='Manjaro Linux pantheon settings'
	depends=('manjaro-base-skel')
	conflicts=('manjaro-desktop-settings')
	provides=('manjaro-desktop-settings')

	_inst_dir 'community/pantheon'
}
