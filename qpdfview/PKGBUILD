# Maintainer: Adam Reichold <adam.reichold@t-online.de>

pkgname=qpdfview
pkgver=0.4.14
pkgrel=1
pkgdesc='A tabbed PDF viewer using the poppler library.'
arch=('i686' 'x86_64')
url='https://launchpad.net/qpdfview'
license=('GPL2')
depends=('libcups' 'poppler-qt5' 'qt5-svg' 'desktop-file-utils' 'hicolor-icon-theme' 'libspectre' 'djvulibre')
makedepends=('qt5-tools')
install='qpdfview.install'
source=("http://launchpad.net/$pkgname/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz")
md5sums=('f87802101e2a888f86b45ee28b802d2f')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    lrelease-qt5 qpdfview.pro
    qmake-qt5 "CONFIG+=$config" qpdfview.pro
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make "INSTALL_ROOT=$pkgdir" install
}