# Maintainer: Ramon Buld√≥ ramon@manjaro.org
# Contributor: Antonio Rojas

pkgname=lximage-qt
pkgver=0.3.0
pkgrel=0.3
pkgdesc="LXQt image viewer"
arch=('i686' 'x86_64')
url='http://lxqt.org/'
license=('LGPL')
groups=('lxqt')
depends=('pcmanfm-qt')
makedepends=('cmake' 'qt5-tools')
install=$pkgname.install
source=("http://lxqt.org/downloads/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz")
sha256sums=('0a5a5114eb1014a4e24af17d4a5d77676bac6a5d25458082e8e2eb45c87f2984')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_QT5=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
