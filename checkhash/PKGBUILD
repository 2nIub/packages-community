# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dot]org>

pkgname=checkhash
pkgver=1.0.1
pkgrel=1
pkgdesc="Check Hash is a GUI wrapper of GNU Coreutils sha1sum"
arch=('i686' 'x86_64')
url="https://github.com/filipe-marques/check-hash"
license=('GPL')
depends=('coreutils' 'qt5-base')
makedepends=('qt5-tools')
source=("$url/archive/$pkgname-$pkgver.tar.gz"
        "$pkgname.desktop")
sha256sums=('3246fa2a4b362a1db172ae0e9456ecd8cea49cbd20c3988be6aab05ff4f2b245'
            'a57170359c74bd3fcb58f26fb715320001c43dd00e397bf5b14f845f1fbb0074')

build() {
  cd $srcdir/check-hash-$pkgname-$pkgver
  qmake-qt5 checkhash.pro
  make
}

package() {
  cd $srcdir/check-hash-$pkgname-$pkgver
  install -Dm755 "Check_Hash" "${pkgdir}/usr/bin/checkhash"
  install -Dm644 "$srcdir/checkhash.desktop" "${pkgdir}/usr/share/applications/checkhash.desktop"
  install -Dm644 "icons/ch24.png" "${pkgdir}/usr/share/icons/checkhash.png"
  install -Dm644 "icons/ch24.png" "${pkgdir}/usr/share/icons/hicolor/24x24/apps/checkhash.png"
  install -Dm644 "icons/ch128.png" "${pkgdir}/usr/share/icons/hicolor/128x128/apps/checkhash.png"
}
