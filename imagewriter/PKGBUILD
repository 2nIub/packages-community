# Maintainer : Ramon Buld√≥ <ramon@manjaro.org>
# Contributor: shimi <shimi.chen@gmail.com>

pkgname=imagewriter
pkgver=1.10.1420800585.134a9b3
pkgrel=2
pkgdesc="A graphical utility for writing raw disk images & hybrid isos to USB keys"
arch=('i686' 'x86_64')
url='https://github.com/openSUSE/imagewriter'
license=('GPL2')
depends=('qt5-base' 'udisks2' 'desktop-file-utils' 'hicolor-icon-theme')
_gitcommit='1d253d960a28f9de4468b0b091c231083f03c1c8'
source=("imagewriter-$pkgver-$pkgrel.tar.gz::$url/archive/$_gitcommit.tar.gz")
sha256sums=('7761ee45b16b5841c6b62b091c7f9d0b8c64b14b79d77e9909ca813e1dc9885d')
install=$pkgname.install

build() {
  cd "$pkgname-$_gitcommit"
  CFLAGS="$CFLAGS -DKIOSKHACK"
  qmake-qt5 PREFIX="$pkgdir/usr" DEFINES=USEUDISKS2 $pkgname.pro
  make
}

package() {
  cd "$pkgname-$_gitcommit"
  make DESTDIR="$pkgdir/" install
}
