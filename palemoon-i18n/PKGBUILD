# Maintainer: Stefano Capitani <stefano@manjaro.org>

pkgbase=palemoon-i18n
pkgver=27.1.0
pkgrel=1
pkgdesc="Language pack for Palemoon"
arch=('any')
url="http://www.palemoon.org/langpacks.shtml"
license=('MPL' 'GPL')
depends=("palemoon>=27")

_languages=(
#'ar    "Arabic"'
'cs    "Czech"'
#'da    "Danish"'
'de    "German"'
#'el    "Greek (Modern)"'
'en-GB "English (U.K.)"'
#'eo    "Esperanto"'
'es-AR "Spanish (Argentina)"'
'es-ES "Spanish (Spain)"'
'es-MX "Spanish (Mexico)"'
#'fi    "Finnish"'
'fr    "French"'
#'gl-ES "Galician (Spain)"'
#'hr    "Croatian"'
'hu    "Hungarian"'
#'is    "Icelandic"'
'it    "Italian"'
'ja    "Japanese"'
#'kn    "Kannada (India)"'
'ko    "Korean"'
#'nl    "Dutch"'
'pl    "Polish"'
#'pt-BR "Portuguese (Brazil)"'
#'pt-PT "Portuguese (Portugal)"'
#'ro    "Romanian/Moldavian"'
'ru    "Russian"'
#'sk    "Slovak"'
#'sl    "Slovenian"'
#'sr    "Serbian"'
#'sv-SE "Swedish"'
#'tr    "Turkish"'
#'vi    "Vietnamese"'
'zh-CN "Chinese (Simplified)"'
#'zh-TW "Chinese (Traditional)"'
)

pkgname=()
source=()
_url="http://relmirror.palemoon.org/langpacks/${pkgver%.*.*}.x"

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=palemoon-i18n-${_locale,,}

  pkgname+=($_pkgname)
  source+=("palemoon-i18n-$pkgver-$_locale.xpi::$_url/$_locale.xpi")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

# Don't extract anything
noextract=(${source[@]%%::*})

_package() {
  pkgdesc="$2 language pack for Palemoon"
  install -Dm644 palemoon-i18n-$pkgver-$1.xpi \
    "$pkgdir/usr/lib/palemoon/browser/extensions/langpack-$1@palemoon.org.xpi"
}

md5sums=('fa9e34faf16ec5956ff8250933fe37d5'
         '8edacd8812aa53b987f622e1a237bd29'
         '62db3ef798a2ad4bd7a09f60fb591894'
         '0ba729ee4f7e32761f1cce5a6861862c'
         '2fff91864f601eb4ad5d507a2969224a'
         'ec05830eca50e72aad4cdca9c435d79d'
         'e9e54c318e793e6dc63a3030d9763601'
         'b2a47a4f2eb4e277b0c4d608094ec110'
         '4d78e2ac755b06acde613555a7b4401a'
         '69435c5e26ec0348013380b3b32fc602'
         'b3ec5575c28dc624c9e2090acb5dc85d'
         '00aea2f6dc88f0430315b91075b3b6a9'
         '54734b90185851aa711fcc2a8328a121'
         '88bd806012ccd0cc2ebad60f2422938d')
