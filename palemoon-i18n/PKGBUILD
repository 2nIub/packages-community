# Maintainer: Stefano Capitani <stefano@manjaro.org>

pkgbase=palemoon-i18n
pkgver=27.0.0
pkgrel=0.3
_rc=RC14
pkgdesc="Language pack for Palemoon"
arch=('any')
url="http://www.palemoon.org/langpacks.shtml"
license=('MPL' 'GPL')
depends=("palemoon>=$pkgver")

_languages=(	
#'ar    "Arabic"'
'cs    "Czech"'
#'da    "Danish"'
'de    "German"'
#'el    "Greek (Modern)"'
#'en-GB "English (U.K.)"'
'es-AR "Spanish (Argentina)"'
'es-ES "Spanish (Spain)"'
'es-MX "Spanish (Mexico)"'
#'fi    "Finnish"'
'fr    "French"'
#'gl-ES "Galician (Spain)"'
#'hr    "Croatian"'
'hu    "Hungarian"'
#'is    "Icelandic"'
'it    "Italian"'
'ja    "Japanese"'
#'kn    "Kannada (India)"'
#'ko    "Korean"'
#'nl    "Dutch"'
'pl    "Polish"'
#'pt-BR "Portuguese (Brazil)"'
#'pt-PT "Portuguese (Portugal)"'
#'ro    "Romanian/Moldavian"'
'ru    "Russian"'
#'sk    "Slovak"'
#'sl    "Slovenian"'
#'sr    "Serbian"'
#'sv-SE "Swedish"'
#'tr    "Turkish"'
#'vi    "Vietnamese"'
'zh-CN "Chinese (Simplified)"'
#'zh-TW "Chinese (Traditional)"'
)

pkgname=()
source=()
#_url=http://relmirror.palemoon.org/langpacks/${pkgver%.*}.x
_url="https://github.com/JustOff/pale-moon-localization/releases/download/${pkgver}_$_rc"

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=palemoon-i18n-${_locale,,}

  pkgname+=($_pkgname)
  source+=("palemoon-i18n-$pkgver-$_locale.xpi::$_url/$_locale.xpi")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

# Don't extract anything
noextract=(${source[@]%%::*})

_package() {
  pkgdesc="$2 language pack for Palemoon"
  install -Dm644 palemoon-i18n-$pkgver-$1.xpi \
    "$pkgdir/usr/lib/palemoon/browser/extensions/langpack-$1@palemoon.org.xpi"
}

md5sums=('2ae0049027765808bd5177b205f15014'
         '0cce607098111fda650ff77990e016ee'
         'aa640496a79048a2445dda21424f33a0'
         'cfec1a6360a011d3e47534cb24978bbf'
         'b4c5eeff972a9f0d7d62bc80efb8c35e'
         '8a2cb2200073ac639b3e7f292b5af793'
         'c4d3e51d817a7cd61c2bd8ca4849f77a'
         'bb3bc2f83fc79cfbc4e528702e15157e'
         '1b18da75afe1ce2a4d42c0376bfae582'
         'a12a2d704ce3bf7343cb2715a1e85c6f'
         '6d06b932de92ef030198804b8e805500'
         'de531537423d4d6037c9ef9dc5426569')
