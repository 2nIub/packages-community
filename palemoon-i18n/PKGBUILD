# Maintainer: Stefano Capitani <stefano@manjaro.org>

pkgbase=palemoon-i18n
pkgver=27.0.0
pkgrel=0.1
_rc=RC11
pkgdesc="Language pack for Palemoon"
arch=('any')
url="http://www.palemoon.org/langpacks.shtml"
license=('MPL' 'GPL')
depends=("palemoon>=$pkgver")

_languages=(	
#'ar    "Arabic"'
'cs    "Czech"'
#'da    "Danish"'
'de    "German"'
#'el    "Greek (Modern)"'
#'en-GB "English (U.K.)"'
'es-AR "Spanish (Argentina)"'
'es-ES "Spanish (Spain)"'
'es-MX "Spanish (Mexico)"'
#'fi    "Finnish"'
'fr    "French"'
#'gl-ES "Galician (Spain)"'
#'hr    "Croatian"'
'hu    "Hungarian"'
#'is    "Icelandic"'
'it    "Italian"'
'ja    "Japanese"'
'kn    "Kannada (India)"'
#'ko    "Korean"'
#'nl    "Dutch"'
'pl    "Polish"'
#'pt-BR "Portuguese (Brazil)"'
#'pt-PT "Portuguese (Portugal)"'
#'ro    "Romanian/Moldavian"'
'ru    "Russian"'
#'sk    "Slovak"'
#'sl    "Slovenian"'
#'sr    "Serbian"'
#'sv-SE "Swedish"'
#'tr    "Turkish"'
'vi    "Vietnamese"'
'zh-CN "Chinese (Simplified)"'
#'zh-TW "Chinese (Traditional)"'
)

pkgname=()
source=()
#_url=http://relmirror.palemoon.org/langpacks/${pkgver%.*}.x
_url="https://github.com/JustOff/pale-moon-localization/releases/download/${pkgver}_$_rc"

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=palemoon-i18n-${_locale,,}

  pkgname+=($_pkgname)
  source+=("palemoon-i18n-$pkgver-$_locale.xpi::$_url/$_locale.xpi")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

# Don't extract anything
noextract=(${source[@]%%::*})

_package() {
  pkgdesc="$2 language pack for Palemoon"
  install -Dm644 palemoon-i18n-$pkgver-$1.xpi \
    "$pkgdir/usr/lib/palemoon/browser/extensions/langpack-$1@palemoon.org.xpi"
}

md5sums=('7d9e591c3fec8edd30457654c66f4227'
         '7644db4c266c3e3b45a70bc06f9acc10'
         '5e054e6e5837e56ab613e747a2375770'
         '24e9bbada077845eff57ea3a4a9398ac'
         '7eb903153c0ab82f390dd20305847698'
         '0af2ca265a94bd8aa2753788d4cd959e'
         '279e03eb0a88f411df8bfc88b8330cd2'
         '439c9c599d199de8efb23eee51170b22'
         'e8e04666823f3c80f3134720af681505'
         '8f5c0b6b3abc1ceb9898ec4a550bdac8'
         'd8d42d59c0ff87372ea63855d0f1a869'
         'df9d474fa71e50c780355328282e669e'
         'c8ef3ecc57b9c38cd78ebf03cce296aa'
         '0cf40792a2d4314fb437f74ce4806165')
