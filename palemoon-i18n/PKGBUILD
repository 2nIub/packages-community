# Maintainer: Stefano Capitani <stefano@manjaro.org>

pkgbase=palemoon-i18n
pkgver=27.4.1
pkgrel=1
pkgdesc="Language pack for Palemoon"
arch=('any')
url="http://www.palemoon.org/langpacks.shtml"
license=('MPL' 'GPL')
depends=("palemoon>=27")

_languages=(
#'ar    "Arabic"'
'cs    "Czech"'
#'da    "Danish"'
'de    "German"'
#'el    "Greek (Modern)"'
'en-GB "English (U.K.)"'
#'eo    "Esperanto"'
'es-AR "Spanish (Argentina)"'
'es-ES "Spanish (Spain)"'
'es-MX "Spanish (Mexico)"'
#'fi    "Finnish"'
'fr    "French"'
#'gl-ES "Galician (Spain)"'
#'hr    "Croatian"'
'hu    "Hungarian"'
#'is    "Icelandic"'
'it    "Italian"'
'ja    "Japanese"'
#'kn    "Kannada (India)"'
'ko    "Korean"'
'nl    "Dutch"'
'pl    "Polish"'
'pt-BR "Portuguese (Brazil)"'
'pt-PT "Portuguese (Portugal)"'
#'ro    "Romanian/Moldavian"'
'ru    "Russian"'
#'sk    "Slovak"'
#'sl    "Slovenian"'
#'sr    "Serbian"'
'sv-SE "Swedish"'
'tr    "Turkish"'
#'vi    "Vietnamese"'
'zh-CN "Chinese (Simplified)"'
#'zh-TW "Chinese (Traditional)"'
)

pkgname=()
source=()
_url="http://relmirror.palemoon.org/langpacks/${pkgver%.*.*}.x"

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=palemoon-i18n-${_locale,,}

  pkgname+=($_pkgname)
  source+=("palemoon-i18n-$pkgver-$_locale.xpi::$_url/$_locale.xpi")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

# Don't extract anything
noextract=(${source[@]%%::*})

_package() {
  pkgdesc="$2 language pack for Palemoon"
  install -Dm644 palemoon-i18n-$pkgver-$1.xpi \
    "$pkgdir/usr/lib/palemoon/browser/extensions/langpack-$1@palemoon.org.xpi"
}

md5sums=('4c59abdbc26ec7747bfa44e5b215dbca'
         '3e51c6ff1927f2361db37c32d30eaab0'
         '2aab79afc916e91c0ff2a03a76b9d628'
         '87072fed7050e7dafb7b0fd8fe23e736'
         '941351650854a2e463d7177ce26c763e'
         '94d327f39977ddd889da0e0f6ccabe57'
         'e92cb92a76907b0a69be5d2e3d33a246'
         '1ba1c9ff5677afd09f49c29793c898e9'
         'e76f5c998871f1ddb9920ae4548cbdc2'
         '38aa0ddbabaac5e267ad8bc962fae4b0'
         'b064766c658ebd536ed8ca809a84b0cb'
         '8d9f334f4247a93e753b508aec26dac5'
         '9d8efa7f9aa4e3e0bd120b8780643762'
         'e1be1e7421d0567ae7f6a5cb1557cb4f'
         '64463a6f99c5cee1b2dda0a416b10a94'
         '7f8a18988e6127b9e1d89e9dc18d1b9b'
         '30e60beab2ba868e6c92b3a76cc55d62'
         '3601ac14b8ebfe0b4394195a0381ad8b'
         'b032c102c791124d2b726672b69597b0')
