# Maintainer: Stefano Capitani <stefano@manjaro.org>

pkgbase=palemoon-i18n
pkgver=27.2.1
pkgrel=1
pkgdesc="Language pack for Palemoon"
arch=('any')
url="http://www.palemoon.org/langpacks.shtml"
license=('MPL' 'GPL')
depends=("palemoon>=27")

_languages=(
#'ar    "Arabic"'
'cs    "Czech"'
#'da    "Danish"'
'de    "German"'
#'el    "Greek (Modern)"'
'en-GB "English (U.K.)"'
#'eo    "Esperanto"'
'es-AR "Spanish (Argentina)"'
'es-ES "Spanish (Spain)"'
'es-MX "Spanish (Mexico)"'
#'fi    "Finnish"'
'fr    "French"'
#'gl-ES "Galician (Spain)"'
#'hr    "Croatian"'
'hu    "Hungarian"'
#'is    "Icelandic"'
'it    "Italian"'
'ja    "Japanese"'
#'kn    "Kannada (India)"'
'ko    "Korean"'
#'nl    "Dutch"'
'pl    "Polish"'
#'pt-BR "Portuguese (Brazil)"'
#'pt-PT "Portuguese (Portugal)"'
#'ro    "Romanian/Moldavian"'
'ru    "Russian"'
#'sk    "Slovak"'
#'sl    "Slovenian"'
#'sr    "Serbian"'
#'sv-SE "Swedish"'
#'tr    "Turkish"'
#'vi    "Vietnamese"'
'zh-CN "Chinese (Simplified)"'
#'zh-TW "Chinese (Traditional)"'
)

pkgname=()
source=()
_url="http://relmirror.palemoon.org/langpacks/${pkgver%.*.*}.x"

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=palemoon-i18n-${_locale,,}

  pkgname+=($_pkgname)
  source+=("palemoon-i18n-$pkgver-$_locale.xpi::$_url/$_locale.xpi")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

# Don't extract anything
noextract=(${source[@]%%::*})

_package() {
  pkgdesc="$2 language pack for Palemoon"
  install -Dm644 palemoon-i18n-$pkgver-$1.xpi \
    "$pkgdir/usr/lib/palemoon/browser/extensions/langpack-$1@palemoon.org.xpi"
}

md5sums=('66516407f07d6b597a7a9a7dbcf910be'
         'c5bca0712e2a0497d612c0227e5d66e0'
         'c7b605b580713215dfcdb5a3029afb78'
         '490b8d99a66012af9d49f630ea6a048e'
         'dbaddeb48bf97f04655fac268990e8ef'
         'ce2b5bcb9562fe5779449e7ce860e97e'
         '5a11d7756b41ad90c3402b44625c3607'
         'a34e293111b296d82236e04f02e58829'
         'eb00ec128e5bd639fa852b84dd4ff9bd'
         'daa9b55badf30e8d6c1e6780d04719e3'
         '47dbce672aa95acd0461d1b0aaf93360'
         '4c3a50550761d9d6c547d6d206ce5d9e'
         '284154e2c0e805057b9d90e35e117d46'
         'c7a112c5ae1809b62006451e11fffd0e')
