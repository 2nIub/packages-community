# Maintainer: Stefano Capitani <stefano@manjaro.org>

pkgbase=palemoon-i18n
pkgver=27.0.0
pkgrel=1
pkgdesc="Language pack for Palemoon"
arch=('any')
url="http://www.palemoon.org/langpacks.shtml"
license=('MPL' 'GPL')
depends=("palemoon>=$pkgver")

_languages=(	
#'ar    "Arabic"'
'cs    "Czech"'
#'da    "Danish"'
'de    "German"'
#'el    "Greek (Modern)"'
#'en-GB "English (U.K.)"'
#'eo    "Esperanto"'
'es-AR "Spanish (Argentina)"'
'es-ES "Spanish (Spain)"'
'es-MX "Spanish (Mexico)"'
#'fi    "Finnish"'
'fr    "French"'
#'gl-ES "Galician (Spain)"'
#'hr    "Croatian"'
'hu    "Hungarian"'
#'is    "Icelandic"'
'it    "Italian"'
'ja    "Japanese"'
#'kn    "Kannada (India)"'
#'ko    "Korean"'
#'nl    "Dutch"'
'pl    "Polish"'
#'pt-BR "Portuguese (Brazil)"'
#'pt-PT "Portuguese (Portugal)"'
#'ro    "Romanian/Moldavian"'
'ru    "Russian"'
#'sk    "Slovak"'
#'sl    "Slovenian"'
#'sr    "Serbian"'
#'sv-SE "Swedish"'
#'tr    "Turkish"'
#'vi    "Vietnamese"'
'zh-CN "Chinese (Simplified)"'
#'zh-TW "Chinese (Traditional)"'
)

pkgname=()
source=()
_url="http://relmirror.palemoon.org/langpacks/${pkgver%.*.*}.x"

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=palemoon-i18n-${_locale,,}

  pkgname+=($_pkgname)
  source+=("palemoon-i18n-$pkgver-$_locale.xpi::$_url/$_locale.xpi")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

# Don't extract anything
noextract=(${source[@]%%::*})

_package() {
  pkgdesc="$2 language pack for Palemoon"
  install -Dm644 palemoon-i18n-$pkgver-$1.xpi \
    "$pkgdir/usr/lib/palemoon/browser/extensions/langpack-$1@palemoon.org.xpi"
}

md5sums=('688ee4526687bb07743f7d086ff7ef69'
         'f6eaf9ea89c28a4e0b04f80a6dac2d3b'
         'f898a853588e8092bbc68242bee7a33c'
         '623575863ca4f01634b60e92dfd0a736'
         '1a50ad124a252c646b99f9c80182e1ac'
         'afe69dc69eccefc722ecfc7fdc5c58f9'
         '2a4fe1dc7b03f4907cbad54eb113ea68'
         '188b4467bcceca16a536a2c112962533'
         'a61aed35e86829cd596a96339f0ea169'
         'd19b077a5d5338e1bad50127923f8dd7'
         '0acb895bdf74604c5ecf0bba8468d17b'
         '903d1404634287de0bf1f6d1b20457b1')
