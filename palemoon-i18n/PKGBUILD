# Maintainer: Stefano Capitani <stefano@manjaro.org>

pkgbase=palemoon-i18n
pkgver=27.0.0
pkgrel=0.2
_rc=RC12
pkgdesc="Language pack for Palemoon"
arch=('any')
url="http://www.palemoon.org/langpacks.shtml"
license=('MPL' 'GPL')
depends=("palemoon>=$pkgver")

_languages=(	
#'ar    "Arabic"'
'cs    "Czech"'
#'da    "Danish"'
'de    "German"'
#'el    "Greek (Modern)"'
#'en-GB "English (U.K.)"'
'es-AR "Spanish (Argentina)"'
'es-ES "Spanish (Spain)"'
'es-MX "Spanish (Mexico)"'
#'fi    "Finnish"'
'fr    "French"'
#'gl-ES "Galician (Spain)"'
#'hr    "Croatian"'
'hu    "Hungarian"'
#'is    "Icelandic"'
'it    "Italian"'
'ja    "Japanese"'
'kn    "Kannada (India)"'
#'ko    "Korean"'
#'nl    "Dutch"'
'pl    "Polish"'
'pt-BR "Portuguese (Brazil)"'
#'pt-PT "Portuguese (Portugal)"'
#'ro    "Romanian/Moldavian"'
'ru    "Russian"'
#'sk    "Slovak"'
#'sl    "Slovenian"'
#'sr    "Serbian"'
#'sv-SE "Swedish"'
#'tr    "Turkish"'
'vi    "Vietnamese"'
'zh-CN "Chinese (Simplified)"'
#'zh-TW "Chinese (Traditional)"'
)

pkgname=()
source=()
#_url=http://relmirror.palemoon.org/langpacks/${pkgver%.*}.x
_url="https://github.com/JustOff/pale-moon-localization/releases/download/${pkgver}_$_rc"

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=palemoon-i18n-${_locale,,}

  pkgname+=($_pkgname)
  source+=("palemoon-i18n-$pkgver-$_locale.xpi::$_url/$_locale.xpi")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

# Don't extract anything
noextract=(${source[@]%%::*})

_package() {
  pkgdesc="$2 language pack for Palemoon"
  install -Dm644 palemoon-i18n-$pkgver-$1.xpi \
    "$pkgdir/usr/lib/palemoon/browser/extensions/langpack-$1@palemoon.org.xpi"
}

md5sums=('c670c73ae980b3b18d6e8235edc14e17'
         '3b581787ca445586fdf9e9f8c8b200f9'
         '20083f4a9c5725127813e8dda7bafe18'
         '83cb0ba929e003993ca7a6f32c49aedb'
         '8a50d86a645b8889f4982b3cc9756bd7'
         '70507403be86a09dddd3e6dd892f93b1'
         '194af69cfd6fa934e4e095e922b2f8a1'
         '862a77ff3ed495acaf2f15d735ef2984'
         'ba9bee914c5f54e87c9bcd8bd6160d16'
         '471c951054897db7a3d215ccbd0d5638'
         '66f930d5c0646b22aa47ccd4ce31e494'
         '27df278b4121e57cdc2d0d7c0c64a556'
         '654a8e6d0413e2f98e9566a5a4687e09'
         '40a1d44d750eef6671320bca0e2b6c0e'
         '56b13ea15644a2875e7beaae8669cac5')
