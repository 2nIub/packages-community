# Maintainer: Riley Trautman <asonix.dev@gmail.com>

pkgname=liri-browser
pkgver=0.3.r245.g71ccccf
pkgrel=2
pkgdesc="A Web Browser using the QML Material framework from the Papyros Project"
arch=("i686" "x86_64")
url="https://github.com/liri-browser/liri-browser"
license=("GPLv3")
depends=("qt5-base" "qt5-webengine" "qml-material" "qt5-multimedia")
makedepends=("git")
provides=("$pkgname-git")
conflicts=("$pkgname-git")
source=("$pkgname::git+https://github.com/liri-project/liri-browser.git"
        "liri-browser.sh" 
        "liri-browser.desktop")
sha256sums=('SKIP'
            '8b3d89676e36de8f8061632d15a58f239d782ae18e2fd0ba63f983fe05b5bea7'
            'b4d6d873065113430170981ade78f2a85a07c3764f4476c6c3bfe281de10f088')

pkgver() {
  cd "$pkgname"
  # cutting off 'foo-' prefix that presents in the git tag
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/$pkgname"
  qmake liri-browser.pro
  make
}

package() {
  cd "$srcdir/$pkgname"
  make INSTALL_ROOT="$pkgdir" install

  mkdir -p "$pkgdir"/usr/bin
  mkdir -p "$pkgdir"/usr/share/applications

  for i in 16x16 22x22 32x32 48x48 64x64 128x128 256x256; do
    install -Dm644 "$srcdir"/"$pkgname"/icons/liri-browser.png \
                   "$pkgdir"/usr/share/icons/hicolor/$i/apps/liri-browser.png
  done
  install -m755 "$srcdir"/liri-browser.sh \
                "$pkgdir"/usr/bin/liri-browser
  install -m755 "$srcdir"/liri-browser.desktop \
                "$pkgdir"/usr/share/applications/liri-browser.desktop
}

# Additional functions to generate a changelog

changelog() {
  cd "$pkgname"
  git log $1..HEAD --no-merges --format=" * %s"
}

gitref() {
  cd "$pkgname"
  git rev-parse HEAD
}
