# Maintainer: philm <philm@manjaro.org>

pkgname=kdeplasma-applets-homerun
_pkgname=homerun
pkgver=1.2.5
pkgrel=1
pkgdesc="Fullscreen application launcher for KDE Plasma desktop"
arch=('i686' 'x86_64')
url="http://userbase.kde.org/Homerun"
license=('GPL' 'LGPL' 'BSD')
depends=('kdebase-workspace' 'kdebase-lib')
makedepends=('cmake' 'automoc4' 'git')
source=("http://download.kde.org/stable/$_pkgname/src/$_pkgname-$pkgver.tar.xz")
install=$pkgname.install
sha256sums=('23f56511b3d64ed3ce979bd6a233aa3d864e9babd333524451d119ec6026c79e')

build() {
  cd "$srcdir"
  mkdir build
  cd build
  cmake ../$_pkgname-$pkgver \
    -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "$srcdir"/build
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/$_pkgname-$pkgver/LICENSE.BSD" "$pkgdir/usr/share/licenses/$pkgname/LICENCE"
}

