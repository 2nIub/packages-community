# Maintainer: philm <philm@manjaro.org>

pkgname=kdeplasma-applets-homerun
pkgver=1.1.96
pkgrel=1
pkgdesc="Fullscreen application launcher for KDE Plasma desktop"
arch=('i686' 'x86_64')
url="http://userbase.kde.org/Homerun"
license=('GPL' 'LGPL' 'BSD')
depends=('kdebase-workspace' 'kdebase-lib')
makedepends=('cmake' 'automoc4' 'git')
#source=("http://download.kde.org/stable/$pkgname/src/$pkgname-$pkgver.tar.xz"
source=("git://anongit.kde.org/homerun"
        'use-distro-icon.patch')
install=$pkgname.install
sha256sums=('SKIP'
            'a2b3f7adbc14d46639a269cf083e2c65fb0db461fd604729278cbb9a78b44d4c')

build() {
  cd "$srcdir"/homerun
  patch -p1 -i "${srcdir}/use-distro-icon.patch"
  
  cd "$srcdir"
  mkdir build
  cd build
  cmake ../homerun \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "$srcdir"/build
  make DESTDIR="$pkgdir" install
  
  install -Dm644 "$srcdir/homerun/LICENSE.BSD" "$pkgdir/usr/share/licenses/$pkgname/LICENCE"
}

