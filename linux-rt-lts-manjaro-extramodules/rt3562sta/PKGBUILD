# Based on the file created for Arch Linux by:
# Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Based on SUSE spec https://build.opensuse.org/package/files?package=rt3562sta&project=driver%3Awireless

# Maintainer: Philip MÃ¼ller <philm@manjaro.org>
# Co-Maintainer: Bernhard Landauer <oberon@manjaro.org>

pkgbase=rt3562sta
pkgname=linux-rt-lts-manjaro-rt3562sta
pkgver=2.4.1.1_r4
_patchrel=${pkgver/*_r/}
pkgrel=8
arch=('x86_64')
url="http://www.mediatek.com/en/Products/support.php?sn=501"
license=('GPL')
makedepends=('linux-rt-lts-manjaro' 'linux-rt-lts-manjaro-headers')
source=($pkgbase-$pkgver.tar.gz::https://github.com/mtorromeo/$pkgbase-linux/archive/r$_patchrel.tar.gz)
sha256sums=('6af6dd13ae1e1075974c8d0a54a0c9f73624cecf153f1f75c8c4f106f4719a1d')

build() {
    _extramodules=extramodules-4.4-rt-lts-MANJARO
    KERNEL_RELEASE="$(cat /usr/lib/modules/$_extramodules/version)"

    cd $srcdir/$pkgbase-linux-r$_patchrel

    EXTRA_CFLAGS="-DVERSION=$pkgver" \
      LINUX_SRC="/usr/lib/modules/$KERNEL_RELEASE/build" \
      make
}

package() {
    _extramodules=extramodules-4.4-rt-lts-MANJARO
    _kernver="$(cat /usr/lib/modules/$_extramodules/version)"
    provides=("$pkgbase=$pkgver")
    groups=('linux-rt-lts-manjaro-extramodules')
    pkgdesc="Ralink RT3562 PCI WLAN adaptors kernel module"
    depends=('linux-rt-lts-manjaro' 'rt3562sta-common')
    groups=('linux-rt-lts-manjaro-extramodules')
    install=$pkgbase.install

    cd $srcdir/$pkgbase-linux-r$_patchrel
    install -Dm 0644 os/linux/$pkgbase.ko "$pkgdir/usr/lib/modules/$_extramodules/$pkgname.ko"

    # set the kernel we've built for inside the install script
    sed -i -e "s/EXTRAMODULES=.*/EXTRAMODULES=${_extramodules}/g" "${startdir}/${pkgbase}.install"

    find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
}
