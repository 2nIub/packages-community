# Maintainer: Bernhard Landauer <oberon@manjaro.org>

pkgbase=numix-reborn-themes
arch=('any')
pkgname=('numix-reborn-icon-themes'
	'xfce-theme-numix-reborn'
	'numix-reborn-wallpapers'
	'numix-reborn-suite')

pkgver=20151002
_snapshot='97d2dd18e5b363d12fb25b5e5d981e2016e88d72'
pkgrel=1
url="https://github.com/oberon2007/artwork-numix-reborn-manjaro-themes"
license=('GPL3')
depends=('gtk-engine-murrine')
source=("$pkgbase-$pkgver.tar.gz::$url/archive/$_snapshot.tar.gz")
md5sums=('d1fe132eef6b1c6db4a651e59025bed5')

pkgver() {
	date +'%Y%m%d'
}

package_numix-reborn-icon-themes() {
	pkgdesc="Numix Reborn icon themes by UgoYak"

	cd "$srcdir/artwork-numix-reborn-manjaro-themes-$_snapshot/icon-theme"
	install -dm755 $pkgdir/usr/share/icons
	cp -r Numix-Reborn $pkgdir/usr/share/icons
	cp -r Numix-Reborn-Circle $pkgdir/usr/share/icons

	install -Dm644 "$srcdir/artwork-numix-reborn-manjaro-themes-$_snapshot/panel-menu-icon/manjaro-circle-icon.png" "$pkgdir/usr/share/icons/manjaro-circle-icon.png"
}

package_xfce-theme-numix-reborn() {
	pkgdesc="Green gtk-Xfce theme based on the original Numix Themes"

	cd "$srcdir/artwork-numix-reborn-manjaro-themes-$_snapshot/xfce-gtk-theme"
	install -dm755 $pkgdir/usr/share/themes
	cp -r Numix-Reborn $pkgdir/usr/share/themes
}

package_numix-reborn-wallpapers() {
	pkgdesc="Wallpapers for the Manjaro Numix Reborn Themes"

	cd $srcdir/artwork-numix-reborn-manjaro-themes-$_snapshot/wallpapers
	install -dm755 $pkgdir/usr/share/backgrounds
	cp Manjaro_ST3_{one,two,three}.png $pkgdir/usr/share/backgrounds
}

package_numix-reborn-suite() {
	pkgdesc="Numix Reborn icons, themes and wallpapers - the whole green enchilada."
	depends=('numix-reborn-icon-themes'
		'xfce-theme-numix-reborn'
		'numix-reborn-wallpapers')
}
