# Maintainer: Doug Newgard <scimmia at archlinux dot info>

_python=python
_pkgname=python-efl
pkgname=$_python-efl
pkgver=1.15.0
pkgrel=1
pkgdesc="${_python^} bindings for the Enlightenment Foundataion Libraries"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('LGPL3' 'GPL3')
depends=('elementary' "$_python-dbus")
source=("http://download.enlightenment.org/rel/bindings/python/$_pkgname-$pkgver.tar.xz")
sha1sums=('d5142fa4c5d28230b71f7a850ea821d033ea1121')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  $_python setup.py build
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  $_python setup.py install --root="$pkgdir" --optimize=1

  install -d "$pkgdir/usr/share/doc/$pkgname/"
  install -m644 -t "$pkgdir/usr/share/doc/$pkgname/" AUTHORS README ChangeLog
}
