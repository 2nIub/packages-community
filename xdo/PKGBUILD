# Maintainer: Thomas Berryhill <tb01110100@gmail.com>
# Contributor: Bastien Dejean <baskerville@lavabit.com>

pkgname=xdo
pkgver=0.r30.88651cc
pkgrel=3
pkgdesc='Small X utility to perform elementary actions on windows'
arch=('i686' 'x86_64')
url="https://github.com/baskerville/${pkgname}"
license=('custom:BSD')
depends=('libxcb' 'xcb-util-wm')
makedepends=('git')
source=("git://github.com/baskerville/${pkgname}.git")
md5sums=('SKIP')

pkgver() {
    cd "$srcdir/$pkgname"
    printf "0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "$srcdir/$pkgname"
    make PREFIX=/usr
}

package() {
    cd "$srcdir/$pkgname"
    make PREFIX=/usr DESTDIR="$pkgdir" install
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
