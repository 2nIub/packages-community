# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# adjusted to work with rt-patched kernels >=4.6 <4.8

pkgbase=virtualbox-dkms-modules
pkgname=('virtualbox-host-dkms'
         'virtualbox-guest-dkms')
pkgver=5.1.10
_pkgrel=1
pkgrel=${_pkgrel}.1
arch=('i686' 'x86_64')
url='http://virtualbox.org'
license=('GPL' 'custom')
source_x86_64=("http://mirror.f4st.host/archlinux/pool/community/virtualbox-host-dkms-$pkgver-$_pkgrel-x86_64.pkg.tar.xz"
		"http://mirror.f4st.host/archlinux/pool/community/virtualbox-guest-dkms-$pkgver-$_pkgrel-x86_64.pkg.tar.xz")
source_i686=("http://mirror.f4st.host/archlinux/pool/community/virtualbox-host-dkms-$pkgver-$_pkgrel-i686.pkg.tar.xz"
		"http://mirror.f4st.host/archlinux/pool/community/virtualbox-guest-dkms-$pkgver-$_pkgrel-i686.pkg.tar.xz")
md5sums_i686=('53a11462c5a2bc32aaf1f3affdc87d51'
              '60cc5a2cf2f3dd3a525c0ca2f7d24141')
md5sums_x86_64=('e93a5ffb3d1f8e2c16becc03bbb60e79'
                'c7af75b7023ec12b1a9843165e3b918a')

package_virtualbox-host-dkms() {
    pkgdesc='VirtualBox Host kernel modules sources'
    depends=('dkms' 'gcc' 'make')
    replaces=('virtualbox-source'
              'virtualbox-host-source'
              'virtualbox-host-modules-lts')
    conflicts=('virtualbox-source' 'virtualbox-host-source')
    provides=('VIRTUALBOX-HOST-MODULES')
    install=$pkgname.install

    # rm mod_timer_pinned() for rt-patched kernels >=4.6 <4.8
    sed -i 's/VERSION(4, 8, 0)/VERSION(4, 8, 0) || defined(RT_PATCHED)/g' \
      usr/src/vboxhost-${pkgver}_OSE/vboxdrv/r0drv/linux/timer-r0drv-linux.c
	  
    install -Dm644 $srcdir/usr/lib/modules-load.d/$pkgname.conf \
      $pkgdir/usr/lib/modules-load.d/$pkgname.conf
    install -Dm644 $srcdir/usr/share/licenses/$pkgname/LICENSE \
      $pkgdir/usr/share/licenses/$pkgname/LICENSE
    install -dm755 $pkgdir/usr/src
    cp -r "$srcdir/usr/src/vboxhost-${pkgver}_OSE" $pkgdir/usr/src/
}

package_virtualbox-guest-dkms() {
    pkgdesc='VirtualBox Guest kernel modules sources'
    depends=('dkms' 'gcc' 'make')
    replaces=('virtualbox-archlinux-source'
              'virtualbox-guest-source'
              'virtualbox-guest-modules-lts')
    provides=('VIRTUALBOX-GUEST-MODULES')
    conflicts=('virtualbox-archlinux-source' 'virtualbox-guest-source')
    install=$pkgname.install

    # rm mod_timer_pinned() for rt-patched kernels >=4.6 <4.8
    sed -i 's/VERSION(4, 8, 0)/VERSION(4, 8, 0) || defined(RT_PATCHED)/g' \
      usr/src/vboxguest-${pkgver}_OSE/vboxguest/r0drv/linux/timer-r0drv-linux.c
	  
    install -Dm644 $srcdir/usr/lib/modules-load.d/$pkgname.conf \
      $pkgdir/usr/lib/modules-load.d/$pkgname.conf
    install -Dm644 $srcdir/usr/share/licenses/$pkgname/LICENSE \
      $pkgdir/usr/share/licenses/$pkgname/LICENSE
    install -dm755 $pkgdir/usr/src
    cp -r "$srcdir/usr/src/vboxguest-${pkgver}_OSE" $pkgdir/usr/src/
}
