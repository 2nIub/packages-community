# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# adjusted to work with rt-patched kernels >=4.6 <4.8

pkgbase=virtualbox-dkms-modules
pkgname=('virtualbox-host-dkms'
         'virtualbox-guest-dkms')
pkgver=5.1.4
_pkgrel=1
pkgrel=${_pkgrel}.1
arch=('i686' 'x86_64')
url='http://virtualbox.org'
license=('GPL' 'custom')
source_x86_64=("http://mirror.f4st.host/archlinux/pool/community/virtualbox-host-dkms-$pkgver-$_pkgrel-x86_64.pkg.tar.xz"
		"http://mirror.f4st.host/archlinux/pool/community/virtualbox-guest-dkms-$pkgver-$_pkgrel-x86_64.pkg.tar.xz")
source_i686=("http://mirror.f4st.host/archlinux/pool/community/virtualbox-host-dkms-$pkgver-$_pkgrel-i686.pkg.tar.xz"
		"http://mirror.f4st.host/archlinux/pool/community/virtualbox-guest-dkms-$pkgver-$_pkgrel-i686.pkg.tar.xz")
md5sums_i686=('0ea6905cd966b5deb5781e27db505c96'
              'c67c08bc43657318b383e82ab5681b20')
md5sums_x86_64=('49a7a7d88057ea7146c526bc45b9d09c'
                '617183b266d79993608f54e92730af4f')

package_virtualbox-host-dkms() {
    pkgdesc='VirtualBox Host kernel modules sources'
    depends=('dkms' 'gcc' 'make')
    replaces=('virtualbox-source'
              'virtualbox-host-source'
              'virtualbox-host-modules-lts')
    conflicts=('virtualbox-source' 'virtualbox-host-source')
    provides=('VIRTUALBOX-HOST-MODULES')
    optdepends=('linux-headers: build modules against Arch kernel'
                'linux-lts-headers: build modules against LTS kernel'
                'linux-zen-headers: build modules against ZEN kernel')
    install=$pkgname.install

	# rm mod_timer_pinned() for rt-patched kernels >=4.6 <4.8
	sed -i 's/VERSION(4, 8, 0)/VERSION(4, 8, 0) || defined(RT_PATCHED)/g' \
	  usr/src/vboxhost-${pkgver}_OSE/vboxdrv/r0drv/linux/timer-r0drv-linux.c
	  
    install -dm755 $pkgdir/usr/src
    cp -r $srcdir/usr/{share,lib} $pkgdir/usr
    cp -r "$srcdir/usr/src/vboxhost-${pkgver}_OSE" $pkgdir/usr/src/vboxhost-${pkgver}_OSE
}

package_virtualbox-guest-dkms() {
    pkgdesc='VirtualBox Guest kernel modules sources'
    depends=('dkms' 'gcc' 'make')
    replaces=('virtualbox-archlinux-source'
              'virtualbox-guest-source'
              'virtualbox-guest-modules-lts')
    provides=('VIRTUALBOX-GUEST-MODULES')
    conflicts=('virtualbox-archlinux-source' 'virtualbox-guest-source')
    optdepends=('linux-headers: build modules against Arch kernel'
                'linux-lts-headers: build modules against LTS kernel'
                'linux-zen-headers: build modules against ZEN kernel')
    install=$pkgname.install

	# rm mod_timer_pinned() for rt-patched kernels >=4.6 <4.8
	sed -i 's/VERSION(4, 8, 0)/VERSION(4, 8, 0) || defined(RT_PATCHED)/g' \
	  usr/src/vboxguest-${pkgver}_OSE/vboxguest/r0drv/linux/timer-r0drv-linux.c
	  
    install -dm755 $pkgdir/usr/src
    cp -r $srcdir/usr/{share,lib} $pkgdir/usr
    cp -r "$srcdir/usr/src/vboxguest-${pkgver}_OSE" $pkgdir/usr/src/vboxguest-${pkgver}_OSE
}
