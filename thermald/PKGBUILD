# Maintainer: FadeMind <fademind@gmail.com>
# Contributor: wallnuss <v dot churavy at gmail dot com
# Contributor: WonderWoofy <sugar.and.scruffy@gmail.com>

_pkgname=thermal_daemon
pkgname=thermald
pkgver=1.5
pkgrel=1
pkgdesc="The Linux Thermal Daemon program from 01.org"
arch=('i686' 'x86_64')
url="https://github.com/01org/${_pkgname}"
license=('GPL2')
makedepends=('systemd')
depends=('dbus-glib>=0.94' 'libxml2>=2.4')
conflicts=('thermald-git')
backup=('etc/thermald/thermal-conf.xml')
source=("https://github.com/01org/thermal_daemon/archive/v1.5.tar.gz")
sha256sums=('48cec153097d19d4b1b5fba8bc7ee5801e8ff5d17c22714bbedd1f967ca1ece9')
          
build() {
    cd ${_pkgname}-${pkgver}
    ./autogen.sh
    ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin
    make
}

package() {
    cd ${_pkgname}-${pkgver}
    make DESTDIR="$pkgdir" install
    mkdir -p ${pkgdir}/etc/{systemd/system/thermald.service.d,modules-load.d}
    # required modules load during boot
    bash -c "{
    echo \"msr\"
    echo \"coretemp\"
    } > ${pkgdir}/etc/modules-load.d/thermald.conf"
    # disabling debug info in journal logs
    # Reference: https://www.reddit.com/r/archlinux/comments/3okrhl/thermald_anyone/cw0fqmb
    bash -c "{
    echo \"[Service]\"
    echo \"StandardOutput=null\"
    } > ${pkgdir}/etc/systemd/system/thermald.service.d/nostdout.conf"
    # remove upstart
    rm -rf "${pkgdir}/etc/init"
}
