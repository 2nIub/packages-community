# Maintainer: FadeMind <fademind@gmail.com>
# Contributor: wallnuss <v dot churavy at gmail dot com
# Contributor: WonderWoofy <sugar.and.scruffy@gmail.com>

_pkgname=thermal_daemon
_git="46fba61e0a13738a4f1b02959725dcf34e7d87dd" # thermal_daemon 1.4.3
pkgname=thermald
pkgver=1.4.3
pkgrel=1
pkgdesc="The Linux Thermal Daemon program from 01.org"
arch=('i686' 'x86_64')
url="https://github.com/01org/thermal_daemon"
license=('GPL2')
makedepends=('systemd')
depends=('dbus-glib>=0.94' 'libxml2>=2.4')
backup=('etc/thermald/thermal-conf.xml')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${_git}.tar.gz"
        'modules-load-thermald.conf')
sha256sums=('6cd0b90ac0bd53e8d4a09f78cdb19897896308c16e5dfe219d20a29008954eb5'
            '0155e1eb459306d251a5a049ffc6c11e144fa8caa75901ac5fa20bd52e05d515')
          
build() {
  cd "${srcdir}/${_pkgname}-${_git}"
  ./autogen.sh
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --sbindir=/usr/bin
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${_git}"
  make DESTDIR="${pkgdir}" install
  mkdir -p "${pkgdir}/usr/lib/modules-load.d"
  install -m644 "${srcdir}/modules-load-thermald.conf" "${pkgdir}/usr/lib/modules-load.d/thermald.conf"

  # Remove upstart
  rm -rf "${pkgdir}/etc/init"
}
