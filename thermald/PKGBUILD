# Maintainer: FadeMind <fademind@gmail.com>
# Contributor: wallnuss <v dot churavy at gmail dot com
# Contributor: WonderWoofy <sugar.and.scruffy@gmail.com>

_pkgname=thermal_daemon
_gitcommit=0b7a49e # https://github.com/01org/thermal_daemon/commit/0b7a49e02560374c5593a10189adc732e324cabf
pkgname=thermald
pkgver=1.4.3
pkgrel=3
pkgdesc="The Linux Thermal Daemon program from 01.org"
arch=('i686' 'x86_64')
url="https://github.com/01org/${_pkgname}"
license=('GPL2')
makedepends=('systemd')
depends=('dbus-glib>=0.94' 'libxml2>=2.4')
conflicts=('thermald-git')
backup=('etc/thermald/thermal-conf.xml')
source=("git+${url}.git#commit=${_gitcommit}")
md5sums=('SKIP')
          
build() {
    cd ${_pkgname}
    ./autogen.sh
    ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin
    make
}

package() {
    cd ${_pkgname}
    make DESTDIR="$pkgdir" install
    mkdir -p ${pkgdir}/etc/{systemd/system/thermald.service.d,modules-load.d}
    # modules load during boot
    bash -c "{
    echo \"msr\"
    echo \"coretemp\"
    } > ${pkgdir}/etc/modules-load.d/thermald.conf"
    # disable debug info in journal logs
    # Reference: https://www.reddit.com/r/archlinux/comments/3okrhl/thermald_anyone/cw0fqmb
    bash -c "{
    echo \"[Service]\"
    echo \"StandardOutput=null\"
    } > ${pkgdir}/etc/systemd/system/thermald.service.d/nostdout.conf"
    # remove upstart
    rm -rf "${pkgdir}/etc/init"
}
