# Maintainer: FadeMind <fademind@gmail.com>
# Contributor: wallnuss <v dot churavy at gmail dot com
# Contributor: WonderWoofy <sugar.and.scruffy@gmail.com>

_pkgname=thermal_daemon
_git="d9e676ceeb397596266efbd5fd24a2333f915ee1" # thermal_daemon 1.4.3 + d9e676ce
pkgname=thermald
pkgver=1.4.3
pkgrel=2
pkgdesc="The Linux Thermal Daemon program from 01.org"
arch=('i686' 'x86_64')
url="https://github.com/01org/thermal_daemon"
license=('GPL2')
makedepends=('systemd')
depends=('dbus-glib>=0.94' 'libxml2>=2.4')
backup=('etc/thermald/thermal-conf.xml')
source=("${pkgname}-${pkgver}-${pkgrel}.tar.gz::${url}/archive/${_git}.tar.gz"
        'modules-load-thermald.conf')
sha256sums=('355457df086cfb6c5735fa4907688ddd0316728c17c48898802a2ea7e990c8d5'
            '0155e1eb459306d251a5a049ffc6c11e144fa8caa75901ac5fa20bd52e05d515')
          
build() {
  cd "${srcdir}/${_pkgname}-${_git}"
  ./autogen.sh
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --sbindir=/usr/bin
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${_git}"
  make DESTDIR="${pkgdir}" install
  mkdir -p "${pkgdir}/usr/lib/modules-load.d"
  install -m644 "${srcdir}/modules-load-thermald.conf" "${pkgdir}/usr/lib/modules-load.d/thermald.conf"

  # Remove upstart
  rm -rf "${pkgdir}/etc/init"
}
