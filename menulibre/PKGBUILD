# Maintainer: Ner0

pkgname=menulibre
pkgver=2.0
pkgrel=1
epoch=1
pkgdesc="An advanced menu editor that provides modern features in a clean, easy-to-use interface. All without GNOME dependencies"
arch=('any')
url="https://launchpad.net/menulibre"
license=('GPL3')
depends=('gdk-pixbuf2' 'gnome-menus' 'gsettings-desktop-schemas' 'gtksourceview3' 'python' 'python-gobject' 'python-psutil')
makedepends=('python-distutils-extra')
source=("https://launchpad.net/$pkgname/$pkgver/$pkgver.0/+download/$pkgname-$pkgver.tar.gz"
        "xfce-fix.patch::http://bazaar.launchpad.net/~menulibre-dev/menulibre/master/diff/101")
sha256sums=('846d55002b5ad03f5806cf6b21b3b3d430dda988eb562784dd5ce0c3d7c3ee89'
            '499fc52e5b44c07e531d908778445e1c0fbd7502a97517bb9e07ba4d251b5638')
install=install

prepare() {
  cd "$srcdir/$pkgname-$pkgver/"
  patch -Np0 -i "${srcdir}/xfce-fix.patch"
}

package() {
  cd "$srcdir/$pkgname-$pkgver/"
  python setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
