# Maintainer: Bastien Dejean <nihilhill@gmail.com>

pkgname=sxhkd
pkgver=0.5.5.r12.g31501de
pkgrel=1
pkgdesc='A simple X hotkey daemon'
arch=('i686' 'x86_64' 'armv7h')
url="https://github.com/baskerville/${pkgname}"
license=('custom:BSD')
depends=('libxcb'
	'xcb-util-keysyms')
makedepends=('git'
	'xcb-util')
source=("git://github.com/baskerville/$pkgname.git")
md5sums=('SKIP')

pkgver() {
    cd "$srcdir/$pkgname"
    git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd "$srcdir/$pkgname"
    make PREFIX=/usr
}

package() {
    cd "$srcdir/$pkgname"
    make PREFIX=/usr DESTDIR="$pkgdir" install
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
