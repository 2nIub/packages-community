# maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# maintainer: Roland Singer <roland[at]manjaro[dog]org>

pkgbase=octopi
pkgname=('octopi-qt5' 'octopi-qt5-notifier')
pkgver=0.4.2
pkgrel=2
pkgdesc="A pacman frontend using Qt5 libs"
arch=('i686' 'x86_64')
url="http://octopiproject.wordpress.com"
license=('GPL')
depends=('qt5-declarative' 'pacman' 'xterm')
makedepends=('git' 'gdb' 'knotifications')
optdepends=('kdesu: for KDE'
            'gksu: for XFCE, Gnome, LXDE, Cinnamon'
            'gnome-keyring: for password management'
            'yaourt: for AUR support'
            'octopi-qt5-notifier: for notifications'
            'pacmanlogviewer: to view pacman log files'
            'pkgfile: to view uninstalled pkg contents in Manjaro Linux')
conflicts=('octopi')
_git=No
_snapshot=0cd9398b838ed38944a60619c6939a372a728728

if [ "$_git" == "Yes" ]; then
   source=(git+http://github.com/aarnt/octopi.git)
   sha256sums=('SKIP')
else
   #source=("octopi-$pkgver.tar.gz::https://github.com/aarnt/octopi/archive/v$pkgver.tar.gz")
   #sha256sums=('07021763c99dff8f1d986622f510ce040448c8f1e88ca615ae3e0a60f44e2dfd')
   source=("octopi-$pkgver-$pkgrel.tar.gz::https://github.com/aarnt/octopi/archive/$_snapshot.tar.gz")
   sha256sums=('373f383b27071f8153245a5b3884eb01c13caaa5d215662bb1b32d238c6e3e1f')
fi

source+=('kf5.patch')
sha256sums+=('3e729704cf4a28629f09e45aacb3f10500851a0e897fbf6e23b7533d4ea764cf')


pkgver() {
   if [ -e "$srcdir/$pkgbase-$pkgver" ]; then
      cd "$srcdir/$pkgbase-$pkgver"
   elif [ -e "$srcdir/$pkgbase-$_snapshot" ]; then
      cd "$srcdir/$pkgbase-$_snapshot"
   elif [ -e "$srcdir/$pkgbase" ]; then
      cd "$srcdir/$pkgbase"
   elif [ -e "$srcdir/$pkgbase.git" ]; then
      cd "$srcdir/$pkgbase.git"
   fi
   if [ "$_git" == "Yes" ]; then
      #echo "`git tag | tail -1 | sed 's|v||g'`.`git describe --always`"
      #echo "0.4.2.$(date +%m%d).`git describe --always`"
      #echo "0.4.2$(git describe --tags | sed 's|v||g' | sed 's|-|.|g')"
      echo 0.4.2.$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
   else
      echo "$pkgver"
   fi
   sed -i -e "s|0.4.2 (dev)|$pkgver|g" src/strconstants.h 
}

prepare() {
   if [ -e "$srcdir/$pkgbase-$pkgver" ]; then
      cd "$srcdir/$pkgbase-$pkgver"
   elif [ -e "$srcdir/$pkgbase-$_snapshot" ]; then
      cd "$srcdir/$pkgbase-$_snapshot"
   elif [ -e "$srcdir/$pkgbase" ]; then
      cd "$srcdir/$pkgbase"
   elif [ -e "$srcdir/$pkgbase.git" ]; then
      cd "$srcdir/$pkgbase.git"
   fi
   patch -Np1 -i ${srcdir}/kf5.patch
}

build() {
   cpucount=$(grep -c processor /proc/cpuinfo 2>/dev/null)
   jc=$((${cpucount:-1}))

   if [ -e "$srcdir/$pkgbase-$pkgver" ]; then
      cd "$srcdir/$pkgbase-$pkgver"
   elif [ -e "$srcdir/$pkgbase-$_snapshot" ]; then
      cd "$srcdir/$pkgbase-$_snapshot"
   elif [ -e "$srcdir/$pkgbase" ]; then
      cd "$srcdir/$pkgbase"
   elif [ -e "$srcdir/$pkgbase.git" ]; then
      cd "$srcdir/$pkgbase.git"
   fi

   msg "Starting build..."

   qmake-qt5 octopi.pro
   make -j $jc

   cd notifier/pacmanhelper
   msg "Building pacmanhelper..."
   qmake-qt5 pacmanhelper.pro
   make -j $jc
    
   cd ../octopi-notifier
   msg "Building octopi-notifier..."
   qmake-qt5 octopi-notifier.pro
   make -j $jc

   cd ../../repoeditor
   msg "Building octopi-repoeditor..."
   # fix build in qt5
   sed -i -e 's|QtGui|QtWidgets|g' main.cpp
   qmake-qt5 repoeditor.pro
   make -j $jc
}

package_octopi-qt5() {
   if [ -e "$srcdir/$pkgbase-$pkgver" ]; then
      cd "$srcdir/$pkgbase-$pkgver"
   elif [ -e "$srcdir/$pkgbase-$_snapshot" ]; then
      cd "$srcdir/$pkgbase-$_snapshot"
   elif [ -e "$srcdir/$pkgbase" ]; then
      cd "$srcdir/$pkgbase"
   elif [ -e "$srcdir/$pkgbase.git" ]; then
      cd "$srcdir/$pkgbase.git"
   fi

   #Octopi main files
   install -D -m755 bin/$pkgbase ${pkgdir}/usr/bin/$pkgbase
   install -D -m644 $pkgbase.desktop ${pkgdir}/usr/share/applications/$pkgbase.desktop
   install -D -m644 resources/images/${pkgbase}_green.png ${pkgdir}/usr/share/icons/$pkgbase.png
   install -D -m644 resources/images/${pkgbase}_green.png ${pkgdir}/usr/share/icons/gnome/32x32/apps/$pkgbase.png
   install -D -m644 resources/images/${pkgbase}_red.png ${pkgdir}/usr/share/icons/${pkgbase}_red.png
   install -D -m644 resources/images/${pkgbase}_yellow.png ${pkgdir}/usr/share/icons/${pkgbase}_yellow.png

   #Pacmanhelper service files
   install -D -m755 notifier/bin/pacmanhelper ${pkgdir}/usr/lib/octopi/pacmanhelper

   install -D -m644 notifier/pacmanhelper/polkit/org.octopi.pacman.policy ${pkgdir}/usr/share/polkit-1/actions/org.octopi.pacman.policy
   install -D -m644 notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.conf ${pkgdir}/etc/dbus-1/system.d/org.octopi.pacmanhelper.conf
   install -D -m644 notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.xml ${pkgdir}/usr/share/dbus-1/interfaces/org.octopi.pacmanhelper.xml
   install -D -m644 notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.service ${pkgdir}/usr/share/dbus-1/system-services/org.octopi.pacmanhelper.service

   #Pacmaneditor files
   install -D -m755 repoeditor/bin/octopi-repoeditor ${pkgdir}/usr/bin/octopi-repoeditor
}

package_octopi-qt5-notifier() {
   pkgdesc="Notifier for Octopi"
   depends=('octopi-qt5' 'libnotify')
   optdepends=('xfce4-notifyd: for notifications in XFCE'
               'knotifications: for notifications in KF5')
   conflicts=('octopi-notifier')

   if [ -e "$srcdir/$pkgbase-$pkgver" ]; then
      cd "$srcdir/$pkgbase-$pkgver"
   elif [ -e "$srcdir/$pkgbase-$_snapshot" ]; then
      cd "$srcdir/$pkgbase-$_snapshot"
   elif [ -e "$srcdir/$pkgbase" ]; then
      cd "$srcdir/$pkgbase"
   elif [ -e "$srcdir/$pkgbase.git" ]; then
      cd "$srcdir/$pkgbase.git"
   fi

   #Octopi-notifier file
   install -D -m755 notifier/bin/octopi-notifier ${pkgdir}/usr/bin/octopi-notifier
   install -D -m644 octopi-notifier.desktop ${pkgdir}/etc/xdg/autostart/octopi-notifier.desktop
}
