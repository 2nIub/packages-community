# Maintainer : Rob McCathie <rob@manjaro.org>
# Contributor: Pantelis Panayiotou <p.panayiotou@gmail.com>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: nesl247 <nesl247@gmail.com>
# Contributor: JJDaNiMoTh <jjdanimoth@gmail.com>

pkgname=fusion-icon
pkgver=0.1
pkgrel=4.2
pkgdesc="Simple tray icon for compiz"
arch=('any')
url="http://www.compiz.org/"
license=('GPL')
conflicts=('fusion-icon-fixed')
depends=('pygtk' 'compizconfig-python' 'hicolor-icon-theme' 'xorg-utils' 'mesa-demos' 'protobuf')
install=fusion-icon.install
source=("ftp://ftp.archlinux.org/other/community/$pkgname/$pkgname-$pkgver.tar.gz"
        'fix_glib_crash.patch'
        'fusion-icon.desktop')
md5sums=('38e7dfb47b1e7a50c3cd191f9b393842'
         '4e85873bf0dfdde2c56df4e2ec9861c4'
         '80f003fb73a49a6f9683754bfaadb182')

package() {
  cd "$srcdir/$pkgname"

  # python2 fix
  sed -i 's_@python_@python2_' Makefile
  for file in $(find . -name '*.py' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  # apply patches
  patch -p1 -i $srcdir/fix_glib_crash.patch
  
  make PREFIX=/usr DESTDIR=$pkgdir install
  
  install -Dm644 "$srcdir"/fusion-icon.desktop "$pkgdir"/etc/xdg/autostart/fusion-icon.desktop
}
