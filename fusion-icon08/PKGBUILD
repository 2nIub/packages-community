# Maintainer : Rob McCathie <rob@manjaro.org>
# Contributor: Pantelis Panayiotou <p.panayiotou@gmail.com>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: nesl247 <nesl247@gmail.com>
# Contributor: JJDaNiMoTh <jjdanimoth@gmail.com>

_name=fusion-icon
pkgname=fusion-icon08
pkgver=0.1
pkgrel=1
pkgdesc="Simple tray icon for compiz"
arch=('any')
url="http://www.compiz.org/"
license=('GPL')
depends=('compiz08-compizconfig-python' 'pygtk' 'hicolor-icon-theme' 'xorg-utils' 'mesa-demos' 'protobuf')
replaces=("${_name}")
provides=("${_name}=${pkgver}")
conflicts=("${_name}")
install="${_name}.install"
source=("ftp://ftp.archlinux.org/other/community/${_name}/${_name}-${pkgver}.tar.gz"
        'fix_glib_crash.patch'
        'fusion-icon.desktop')
md5sums=('38e7dfb47b1e7a50c3cd191f9b393842'
         '4e85873bf0dfdde2c56df4e2ec9861c4'
         '80f003fb73a49a6f9683754bfaadb182')

prepare() {
  cd "${srcdir}/${_name}"
  
  # python2 fix
  sed -i 's_@python_@python2_' Makefile
  for file in $(find . -name '*.py' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  # apply patches
  patch -p1 -i "${srcdir}/fix_glib_crash.patch"
}

package() {
  cd "${srcdir}/${_name}"
  
  make PREFIX=/usr DESTDIR="${pkgdir}" install
  
  install -Dm644 "${srcdir}/fusion-icon.desktop" "${pkgdir}/etc/xdg/autostart/fusion-icon.desktop"
}
