# Maintainer: Rob McCathie <rob@manjaro.org>
# Contributor: Charles Bos <charlesbos1 AT gmail>
# Contributor: Pantelis Panayiotou <p.panayiotou@gmail.com>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: nesl247 <nesl247@gmail.com>
# Contributor: JJDaNiMoTh <jjdanimoth@gmail.com>

_name=fusion-icon
pkgname=fusion-icon08

pkgver=0.1
pkgrel=2
# corresponds to AUR 1:0.1-6

pkgdesc="Simple tray icon for Compiz 0.8"
arch=('any')
url="http://www.compiz.org/"
license=('GPL')
replaces=("${_name}")
provides=('fusion-icon=0.1')
conflicts=('fusion-icon0.9' 'fusion-icon')
depends=('pygtk' 'compiz08-compizconfig-python' 'hicolor-icon-theme' 'xorg-utils' 'mesa-demos')
install=$_name.install
source=("http://archlinux.c3sl.ufpr.br/other/community/$_name/$_name-0.1.tar.gz"
        "fix_glib_crash.patch"
        'fusion-icon.desktop')
sha256sums=('da23acad5bc6d2d062744faafdfe5f71d5f8a787bd464642d763fb1559253c3d'
            'a93dadb6e78cc5e5281ec60a06b62e3d2a07b1c5683abaefb633a5fa35a25881'
            '3f16d6b3bbb429b16cecf88563e3c730a1b11fb95ad1488f4dcedc0c1d2c09d0')

prepare() {
  cd $srcdir/$_name

  # python2 fix
  sed -i 's_@python_@python2_' Makefile
  for file in $(find . -name '*.py' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  # apply patches
  patch -p1 -i $srcdir/fix_glib_crash.patch
}

package() {
  cd $srcdir/$_name
  make PREFIX=/usr DESTDIR=$pkgdir install
  
  install -Dm644 "${srcdir}/fusion-icon.desktop" "${pkgdir}/etc/xdg/autostart/fusion-icon.desktop"
}
