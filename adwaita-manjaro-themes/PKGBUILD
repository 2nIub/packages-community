# Maintainer : Rob McCathie <rob@manjaro.org>

pkgname=adwaita-manjaro-themes
pkgver=20160412
pkgrel=2
_basegtkver=3.20
_fullgtkver=${_basegtkver}.2
_axcommit=cc3ffa01f27aa0b6e2f431034832d62fd65aac14
_metacityver=3.18.3
pkgdesc="Adwaita-Manjaro theme"
arch=('any')
url="https://www.gnome.org/"
license=('GPL')
makedepends=('imagemagick')
depends=('gtk-engine-murrine')
provides=('adwaita-manjaro-theme')
replaces=('adwaita-manjaro-theme')
source=("https://git.gnome.org/browse/gtk+/snapshot/gtk+-${_fullgtkver}.tar.xz"
        "http://mirror.netzspielplatz.de/manjaro/packages/unstable/extra/x86_64/gnome-themes-standard-${_basegtkver}-1-x86_64.pkg.tar.xz"
        "https://github.com/thearakattack/adwaita-xfce/archive/${_axcommit}.zip"
        "https://git.gnome.org/browse/metacity/snapshot/metacity-${_metacityver}.tar.xz"
        "index.theme"
        "panel.rc"
        "add-xfce-panel.patch"
        "README.md")
sha256sums=('ccfa0bf5509e120b3e249f20cedaa72d47b03ac998a5a98b6dc5ce9c846681dd'
            '940b6d8d501f55a60279b4c0a1b591300e4c7a4083bbf4454b1820f42cd05703'
            'b45e219135add294284a40eb3f72466334f3634d23d3e92a5635e15857ea534d'
            '2c71b9e157bdbcf5da03913e048c81de4b22ce71f8345ce5bcd03405e529ee7a'
            '32d051214ab29df129f62a44dc49973f40be131078796182da7619c0cfb58c4a'
            '5f980512053a1694b3a5dc1ac32ad2fa3f67db4eed9d481b8804fc59394c20ee'
            '35fff2f7341c989ad8a18c372e616abd33f6b8e030662183e65081ff175d44a1'
            'f7b10d87635bc30e655fa702c9cf825841a05f50fd2e304983b8f2ca06cbfb62')
options=('!strip')

package() {
  mkdir -p "${pkgdir}/usr/share/themes/Adwaita-Manjaro"
  cp "${srcdir}/index.theme" "${pkgdir}/usr/share/themes/Adwaita-Manjaro/"
  cp -r "${srcdir}/usr/share/themes/Adwaita/gtk-2.0" "${pkgdir}/usr/share/themes/Adwaita-Manjaro/"
  cp -r "${srcdir}/gtk+-${_fullgtkver}/gtk/theme/Adwaita" "${pkgdir}/usr/share/themes/Adwaita-Manjaro/gtk-3.0"
  
  cp "${srcdir}/panel.rc" "${pkgdir}/usr/share/themes/Adwaita-Manjaro/gtk-2.0/"
  echo 'include "panel.rc"' >> "${pkgdir}/usr/share/themes/Adwaita-Manjaro/gtk-2.0/gtkrc"
  
  cd "${pkgdir}/usr/share/themes/Adwaita-Manjaro/gtk-3.0"
  find . -maxdepth 1 -type f -not -name '*contained*' -exec rm {} \;
  rm *.scss
  mv gtk-contained.css gtk.css
  mv gtk-contained-dark.css gtk-dark.css
  patch -p1 -i "${srcdir}/add-xfce-panel.patch"
  
  cd ..
  
  ## Main highlight colour  blue->green
  sed -i "s/4a90d9/16a085/g" gtk-2.0/gtkrc
  sed -i "s/4a90d9/16a085/g" gtk-3.0/gtk.css
  sed -i "s/4a90d9/16a085/g" gtk-3.0/gtk-dark.css
  
  ## Lower half of GTK3 progress bars  blue->green
  sed -i "s/2a76c6/008672/g" gtk-3.0/gtk.css
  sed -i "s/2a76c6/008672/g" gtk-3.0/gtk-dark.css
  
  ## Button borders  blue->green
  sed -i "s/184472/00541e/g" gtk-3.0/gtk.css
  sed -i "s/184472/00541e/g" gtk-3.0/gtk-dark.css
  
  ## All the below are from the "button.suggested-action" section  blue->green
  sed -i "s/1c5187/006133/g" gtk-3.0/gtk.css
  sed -i "s/1c5187/006133/g" gtk-3.0/gtk-dark.css
  
  sed -i "s/3986d5/059681/g" gtk-3.0/gtk.css
  sed -i "s/3986d5/059681/g" gtk-3.0/gtk-dark.css
  
  sed -i "s/63a0de/2fb08a/g" gtk-3.0/gtk.css
  sed -i "s/63a0de/2fb08a/g" gtk-3.0/gtk-dark.css


  sed -i "s/276fba/007f66/g" gtk-3.0/gtk.css
  sed -i "s/276fba/007f66/g" gtk-3.0/gtk-dark.css

  sed -i "s/3583d5/019381/g" gtk-3.0/gtk.css
  sed -i "s/3583d5/019381/g" gtk-3.0/gtk-dark.css

  sed -i "s/5094db/1ca487/g" gtk-3.0/gtk.css
  sed -i "s/5094db/1ca487/g" gtk-3.0/gtk-dark.css

  sed -i "s/8db9e8/59c994/g" gtk-3.0/gtk.css
  sed -i "s/8db9e8/59c994/g" gtk-3.0/gtk-dark.css

  ## For all PNG and SVG images swap the blue and green channels, making everything green instead of blue.
  find . -name "*.png" -exec mogrify -channel rbga -separate -swap 1,2 -combine {} \;
  find . -name "*.svg" -exec mogrify -background none -channel rbga -separate -swap 1,2 -combine {} \;
  
  ## Xfwm4 and Metacity decorations
  cp -r "${srcdir}/adwaita-xfce-${_axcommit}/Adwaita-Xfce Alt v1/xfwm4" ./
  rm "xfwm4/themerc~"
  install -Dm644 "${srcdir}/metacity-${_metacityver}/src/themes/Adwaita/metacity-theme-3.xml" metacity-1/metacity-theme-3.xml
  sed -i "s|<name>Adwaita</name>|<name>Adwaita-Manjaro</name>|g" metacity-1/metacity-theme-3.xml
  
  ## Place README
  cp "${srcdir}/README.md" "${pkgdir}/usr/share/themes/Adwaita-Manjaro/README"
}
