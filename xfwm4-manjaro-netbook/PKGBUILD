# Maintainer : Rob McCathie <rob@manjaro.org>
# Contributor: Cedric Leporcq <cedric at gmail dot com>

pkgname=xfwm4-manjaro-netbook
_pkgname=xfwm4-titleless-dev
_name=${_pkgname%-*}
pkgver=4.11.3
pkgrel=2
pkgdesc="Xfce window manager with hide title on maximized features, corner tiling and fixes"
arch=('i686' 'x86_64')
url="https://github.com/cedl38/xfwm4-titleless"
license=('GPL2')
depends=('libxfce4ui>=4.11.0' 'libwnck' 'hicolor-icon-theme')
makedepends=('intltool' 'xfce4-dev-tools' 'exo')
provides=("xfwm4=${pkgver}")
conflicts=('xfwm4')
options=('!libtool')
install=xfwm4.install
source=("${_pkgname}-$pkgver.tar.gz::https://github.com/cedl38/${_name}/archive/v${pkgver}-t.tar.gz"
        'maximized_placement.patch')
sha256sums=('0e5a9a3e5776ff78c845f44f10f9f7c345cf2bc371a346eca6d5fa2b00bf1bd3'
            'c4a911ab79234bf1887e4e151a4ced9c6c28da59d387d9ddd7caf8faf5518121')

prepare() {
  cd "$srcdir/${_name}-${pkgver}-t"
  patch -Np1 -i "$srcdir"/maximized_placement.patch
}

build() {
  cd "$srcdir/${_name}-${pkgver}-t"

    ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --enable-startup-notification \
    --enable-randr \
    --enable-compositor \
    --enable-xsync \
    --disable-debug

  make
}

package() {
  cd "$srcdir/${_name}-${pkgver}-t"
  make DESTDIR="$pkgdir" install
}
