# Maintainer : Rob McCathie <rob@manjaro.org>
# Contributor: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfwm4-manjaro-netbook
_name=xfwm4
pkgver=4.12.3
pkgrel=1
pkgdesc="Xfce window manager with auto-maximize patch and compositing disabled, used in the Manjaro Netbook edition"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
depends=('libxfce4ui' 'libwnck' 'libdrm' 'hicolor-icon-theme')
makedepends=('intltool')
optdepends=('compton: for OpenGL screen compositing')
install="${_name}.install"
provides=("${_name}=${pkgver}")
conflicts=("${_name}")
source=("http://archive.xfce.org/src/xfce/${_name}/${pkgver%.*}/${_name}-${pkgver}.tar.bz2"
        "maximized_placement.patch"
        "remove_composite_tab.patch")
sha256sums=('f4a988fbc4e0df7e8583c781d271559e56fd28696092f94ae052e9e6edb09eac'
            'b90e4c6a4014eec9447bed058b3eb3519d58b676267167ce5811649e99b94ef6'
            '5bbae2fb2907232fa8c2144754307c91e3a523e69fd44931455c8a99fa77b4c7')

prepare() {
  cd "${srcdir}/${_name}-${pkgver}"
  patch -p1 -i "${srcdir}/maximized_placement.patch"
  patch -p1 -i "${srcdir}/remove_composite_tab.patch"
}

build() {
  cd "${srcdir}/${_name}-${pkgver}"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --enable-startup-notification \
    --enable-randr \
    --disable-compositor \
    --enable-xsync \
    --disable-debug
  make
}

package() {
  cd "${srcdir}/${_name}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
