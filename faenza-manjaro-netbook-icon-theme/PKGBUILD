# Maintainer: Rob McCathie <rob@manjaro.org>

pkgname=faenza-manjaro-netbook-icon-theme
pkgver=20150113
pkgrel=1
pkgdesc="Faenza-Green with a few changes for the Netbook edition"
arch=('any')
url="http://www.manjaro.org/"
license=('GPL')
depends=('faenza-green-icon-theme' 'gnome-icon-theme-symbolic')
install="${pkgname}.install"
source=("index.theme")
sha1sums=('f32995541c688129d42629965ac327ba667382c7')

package() {
  install -Dm644 "${srcdir}/index.theme" "${pkgdir}/usr/share/icons/Faenza-Manjaro-Netbook/index.theme"
  mkdir "${pkgdir}/usr/share/icons/Faenza-Manjaro-Netbook/apps"
  cd "${pkgdir}/usr/share/icons/Faenza-Manjaro-Netbook/apps"
  
  for i in 16 22 24 32 48 64 96
  do
    mkdir ${i}
    ln -s ../../../Faenza/apps/${i}/system-file-manager.png ${i}/Thunar.png
    ln -s ../../../Faenza/apps/${i}/system-file-manager.png ${i}/thunar.png
    ln -s ../../../Faenza/apps/${i}/web-browser.png ${i}/palemoon.png
  done
  
  for i in 16 22 24 32 48
  do
    ln -s ../../../gnome/${i}x${i}/actions/edit-find.png ${i}/gtk-find.png
  done
  
  mkdir scalable
  ln -s ../../../Faenza/apps/scalable/system-file-manager.svg scalable/Thunar.svg
  ln -s ../../../Faenza/apps/scalable/system-file-manager.svg scalable/thunar.svg
  ln -s ../../../Faenza/apps/scalable/web-browser.svg scalable/palemoon.svg
  ln -s ../../../gnome/scalable/actions/edit-find-symbolic.svg scalable/gtk-find.svg
}
