# Maintainer: Stefano Capitani <stefanoatmanjarodotorg>

pkgname=inxi
pkgver=2.9.06
pkgrel=1
pkgdesc="script to get system information"
arch=('any')
url="https://github.com/smxi/inxi"
license=('GPL')
depends=('coreutils' 'gawk' 'grep' 'pciutils' 'perl' 'procps-ng' 'sed')
_commit=89333ff370e037c511f5bcba4ca5aca71dceb065
optdepends=(
  "dmidecode: inxi -M if no sys machine data"
  "file: inxi -o unmounted file system"
  "hddtemp: inxi -Dx show hdd temp"
  "net-tools: inxi -i ip lan-deprecated"
  "iproute2: inxi -i ip lan"
  "lm_sensors: inxi -s sensors output"
  "usbutils: inxi -A usb audio;-N usb networking"
  "kmod: inxi -Ax,-Nx module version"
  "systemd-sysvcompat: inxi -I runlevel"
  "sudo: inxi -Dx hddtemp-user;-o file-user"
  "mesa-demos: inxi -G glx info"
  "xorg-xdpyinfo: inxi -G multi screen resolution"
  "xorg-xprop: inxi -S desktop data"
  "xorg-xrandr: inxi -G single screen resolution"
)
options=('zipman')
source=("$pkgname-$pkgver-$pkgrel.tar.gz::$url/archive/$_commit.tar.gz"
        https://github.com/smxi/inxi/commit/manjaro.patch)
sha256sums=('9ad40eb1845be446a79216da7319f7c2557e6ee9d93f36c017685e8ef5307196'
            'd67652c87416265919ca18fe9d5a831bc9a89942c05a87d18d87846c85fdb94a')

pkgver() {
    cd $pkgname-$_commit

    # change version
    awk '/self_version=/ {print $2}' inxi | cut -c 16-21
}

prepare() {
    cd $pkgname-$_commit
    
    # temp move inxi to pinxi
    mv inxi pinxi

    # patches here
    patch -p1 -i $srcdir/manjaro.patch

    # temp move pinxi back to inxi
    mv pinxi inxi
}

package() {
  cd "$srcdir/$pkgname-$_commit"
  install -D -m755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -D -m755 $pkgname.1 "$pkgdir/usr/share/man/man1/$pkgname.1"
}
