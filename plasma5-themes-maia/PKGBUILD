#Maintainer: Ramon Buld√≥ <ramon@manjaro.org>

pkgbase=plasma5-themes-maia
pkgname=('plasma5-themes-maia' 'sddm-maia-theme')
pkgver=0.3.0
pkgrel=1
_gitcommit=2d4c1d94ae236e08bfb7f76ef196dbbea0b23675
url=('https://github.com/manjaro/artwork-maia/')
arch=('any')
license=('LGPL')
makedepends=('extra-cmake-modules' 'plasma-framework' 'git')

source=("maia-$pkgver-$pkgrel.tar.gz::$url/archive/$_gitcommit.tar.gz")
sha256sums=('SKIP')

prepare() {
  mv $srcdir/artwork-maia-$_gitcommit $srcdir/maia
  mkdir -p build  
}

build() {
  cd build  
  cmake ../maia \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON  
  make
}

package_plasma5-themes-maia() {
  pkgdesc='Maia theme for KDE Plasma 5'
  depends=('breeze')
  replaces=('maia-themes')
  cd build
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr/share/sddm"
}

package_sddm-maia-theme() {
  pkgdesc="Maia theme for SDDM"
  cd build
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr/share/color-schemes"
  rm -rf "${pkgdir}/usr/share/icons"
  rm -rf "${pkgdir}/usr/share/kservices5"
  rm -rf "${pkgdir}/usr/share/plasma"
  rm -rf "${pkgdir}/usr/share/wallpapers"
} 
