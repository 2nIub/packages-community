# Maintainer : Ramon Buld√≥ ramon@manjaro.org
# Contributor: Antonio Rojas

pkgname=lxqt-openssh-askpass
pkgver=0.9.0
pkgrel=0.1
pkgdesc="Tool used with openssh to prompt the user for password"
arch=('i686' 'x86_64')
url='http://lxqt.org/'
license=('LGPL')
groups=('lxqt')
depends=('liblxqt')
makedepends=('cmake' 'qt5-tools')
source=("http://lxqt.org/downloads/lxqt/$pkgver/$pkgname-$pkgver.tar.xz")
sha256sums=('95c9646c8583add54e49b31c6c8dd04d9d09a8836fa8ea289e0adce403aaa72b')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
