# Based on the file created for Arch Linux by:
# Maintainer : Thomas Baechler <thomas@archlinux.org>

# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Philip MÃ¼ller <philm@manjaro.org>

_linuxprefix=linux-rt-manjaro
_extramodules=extramodules-4.11-rt-MANJARO
pkgname=$_linuxprefix-nvidia-304xx
_pkgname=nvidia
pkgver=304.134
pkgrel=24
epoch=1
pkgdesc="NVIDIA drivers for linux."
arch=('x86_64')
url="http://www.nvidia.com/"
depends=("$_linuxprefix" "nvidia-304xx-utils=${epoch}:${pkgver}")
makedepends=("$_linuxprefix-headers")
provides=("$_pkgname=$pkgver" "$_linuxprefix-nvidia-legacy")
replaces=("$_linuxprefix-nvidia-legacy")
groups=("$_linuxprefix-extramodules")
conflicts=('nvidia-96xx' 'nvidia-173xx' "$_linuxprefix-nvidia")
license=('custom')
install=nvidia.install
options=(!strip)
source=("http://us.download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/NVIDIA-Linux-x86_64-${pkgver}-no-compat32.run"
        'nvidia-3.18.patch'
        'disable-mtrr.patch'
        'linux49.patch'
        'linux410.patch'
        'linux411.patch')
md5sums=('2ecfa6bd145f673264f7977e0366e259'
         '79671a27131da619a33eb02ed0c2c031'
         'c4becf1145a139cc0121be9ad340bcd8'
         'f1003d21dcadde47d532a27bdfc79b55'
         'a2518ee171134b4d0aa2d96483746a34'
         'b44ca6919a2001c977f0aeb3e9af4484')

_pkg="NVIDIA-Linux-x86_64-${pkgver}-no-compat32"

prepare() {
  cd "$srcdir"
  sh "$_pkg.run" --extract-only
  cd "${_pkg}"
  # patches here
  # https://devtalk.nvidia.com/default/topic/783364/343-22-driver-incompatible-with-linux-3-18-/
  patch -p0 --no-backup-if-mismatch -i ${srcdir}/nvidia-3.18.patch
  # FS#47092
  (cd kernel; patch -p1 --no-backup-if-mismatch -i "$srcdir"/disable-mtrr.patch)
  # Fix "already registered" problem with 4.9
  (cd kernel; patch -p1 --no-backup-if-mismatch -i "$srcdir"/linux49.patch)
  # Fix compile problem with 4.10
  (cd kernel; patch -p2 --no-backup-if-mismatch -i "$srcdir"/linux410.patch)
  # Fix compile problem with 4.11
  (cd kernel; patch -p1 --no-backup-if-mismatch -i "$srcdir"/linux411.patch)
}

build() {
    _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

    cd "${_pkg}/kernel"
    make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
    gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
}
