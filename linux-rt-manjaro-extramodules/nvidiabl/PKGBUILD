# Maintainer: Philip MÃ¼ller <philm@manjarolinux.org>
# Co-Maintainer: Guinux <nuxgui@gmail.com>

_linuxprefix=linux-rt-manjaro
_extramodules=extramodules-4.8-rt-MANJARO
pkgname=$_linuxprefix-nvidiabl
_pkgname=nvidiabl
pkgver=0.88
pkgrel=17
epoch=
pkgdesc="Driver to adjust display backlight on modern mobile NVidia graphics adapters."
arch=('x86_64')
url="https://github.com/guillaumezin/nvidiabl"
license=('GPL')
makedepends=("$_linuxprefix-headers")
conflicts=(nvidia-bl)
provides=("$_pkgname=$pkgver")
groups=("$_linuxprefix-extramodules")
install=$_pkgname.install
source=(https://github.com/guillaumezin/nvidiabl/archive/master.zip 
        nvidiabl-master.patch
        kernel-4.8.patch)
md5sums=('5fdda33fedcb78320c1581e84b395d39'
         '79e5699da970908d6ec8dba114df87cb'
         'd94abebc5f40586fbed015d9fab29a37')

prepare() {
  cd "${srcdir}/nvidiabl-master/"
  patch -p1 -i "${srcdir}/nvidiabl-master.patch"
  patch -p1 -i "${srcdir}/kernel-4.8.patch"
}

build() {
  _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

  cd "${srcdir}/nvidiabl-master/"
  make KVER="${_kernver}"
}

package() {
  _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

  cd "${srcdir}/nvidiabl-master/"
  MODPATH="${pkgdir}/usr/lib/modules/${_extramodules}/"
  install -d $MODPATH
  install -m 644 -c nvidiabl.ko $MODPATH
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidiabl.install"
  sed -i -e "s/VERSION='.*'/VERSION='${_linuxprefix}'/" "${startdir}/nvidiabl.install"
}
