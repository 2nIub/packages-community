# Maintainer: Philip MÃ¼ller <philm@manjaro.org>
# Contributor : Martin Wimpress <code@flexion.org>

_ver=1.14
_pkgbase=atril
pkgname=(${_pkgbase}-gtk3)
pkgver=${_ver}.2
pkgrel=0.1
pkgdesc="MATE document viewer (GTK3 version)"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('mate-desktop-gtk3' 'djvulibre' 'libspectre' 'libgxps' 'mathjax' 'poppler-glib' 'webkit2gtk')
makedepends=('caja-gtk3' 'gobject-introspection' 'intltool' 'itstool' 'texlive-bin')
optdepends=('caja-gtk3: Document Properties extension'
            'texlive-bin: DVI support')
groups=('mate-extra-gtk3')
conflicts=("${_pkgbase}")
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('7eb2ba97290b9a8cba7b93f10efa0b00d1454488')

prepare() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"

    # Fix mathjax path
    sed -i 's|/usr/share/javascript/mathjax|/usr/share/mathjax|' backend/epub/epub-document.c
}

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${_pkgbase} \
        --with-gtk=3.0 \
        --enable-djvu \
        --enable-dvi \
        --enable-epub \
        --enable-t1lib \
        --enable-pixbuf \
        --enable-comics \
        --enable-xps \
        --enable-introspection

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
