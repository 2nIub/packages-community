# Maintainer: Philip MÃ¼ller <philm@manjaro.org>
# Maintainer: Stefano Capitani <stefanoatmanjarodotorg>
# Contributor : Martin Wimpress <code@flexion.org>

_ver=1.18
_pkgbase=eom
pkgname=(${_pkgbase})
pkgver=${_ver}.0
pkgrel=0.1
pkgdesc="An image viewing and cataloging program for MATE"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'gobject-introspection-runtime' 'exempi' 'lcms2' 'libexif' 'libjpeg-turbo' 'mate-desktop' 'libpeas')
makedepends=('gobject-introspection' 'intltool' 'itstool')
groups=('mate-extra')
conflicts=("${_pkgbase}-gtk3")
replaces=("${_pkgbase}-gtk3")
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('5dbc90dc8a695d2ee8d8595171145fd23b001927')

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --localstatedir=/var \
        --with-librsvg \
        --disable-python

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
