# Maintainer: Philip MÃ¼ller <philm@manjaro.org>
# Maintainer: Stefano Capitani <stefanoatmanjarodotorg>
# Contributor : Martin Wimpress <code@flexion.org>

_ver=1.18
_pkgbase=pluma
pkgname=${_pkgbase}
pkgver=${_ver}.0
pkgrel=0.1
pkgdesc="A powerful text editor for MATE"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('iso-codes' 'mate-desktop' 'pygtksourceview2' 'zenity')
makedepends=('intltool' 'itstool')
groups=('mate-extra')
conflicts=("${_pkgbase}-gtk3")
replaces=("${_pkgbase}-gtk3")
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('8a6e5c6f445d23e7dd2f0a2e8de39f648f0d8dfd')

prepare() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    sed -i 's/python/python2/' plugins/externaltools/data/switch-c.tool.in
}

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${_pkgbase} \
        --enable-gtk-doc=no \
        --enable-python

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}

