# Maintainer: Philip MÃ¼ller <philm@manjaro.org>
# Contributor : Martin Wimpress <code@flexion.org>

_ver=1.14
_pkgbase=eom
pkgname=(${_pkgbase}-gtk3)
pkgver=${_ver}.2
pkgrel=0.1
pkgdesc="An image viewing and cataloging program for MATE (GTK3 version)"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'gobject-introspection-runtime' 'exempi' 'libexif' 'libjpeg-turbo' 'mate-desktop-gtk3')
makedepends=('gobject-introspection' 'intltool' 'itstool')
groups=('mate-extra-gtk3')
conflicts=("${_pkgbase}")
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('32afdaaa7fde94c935f4fe621d67bbf4acdc132a')

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --localstatedir=/var \
        --with-gtk=3.0 \
        --with-librsvg \
        --disable-python

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}

