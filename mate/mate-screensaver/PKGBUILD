# Maintainer: Philip MÃ¼ller <philm@manjaro.org>
# Maintainer: Stefano Capitani <stefanoatmanjarodotorg>
# Contributor : Martin Wimpress <code@flexion.org>

_ver=1.18
_pkgbase=mate-screensaver
pkgname=(${_pkgbase})
pkgver=${_ver}.0
pkgrel=0.1
pkgdesc='Screensaver for MATE'
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'libmatekbd>=1.12' 'libnotify' 'libxss' 'mate-desktop>=1.12'
         'mate-menus>=1.12' 'mate-session-manager>=1.12')
makedepends=('mate-common' 'xmlto')
optdepends=('mate-power-manager: Add power management support.'
            'mate-screensaver-hacks: Additional screensavers.'
            'rss-glx: Really slick screensavers.')
groups=('mate-extra')
conflicts=("${_pkgbase}-gtk3")
replaces=("${_pkgbase}-gtk3")
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('baa1a532d3ef963473d1307daa3a78238f58f3f0')

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${_pkgbase} \
        --sysconfdir=/etc \
        --with-xscreensaverdir=/usr/share/xscreensaver/config \
        --with-xscreensaverhackdir=/usr/lib/xscreensaver \
        --with-mit-ext \
        --with-libnotify \
        --enable-locking \
        --without-console-kit \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
