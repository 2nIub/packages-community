# Maintainer: Philip MÃ¼ller <philm@manjaro.org>
# Maintainer: Stefano Capitani <stefanoatmanjarodotorg>
# Contributor : Martin Wimpress <code@flexion.org>

_ver=1.18
pkgname=mate-menus
pkgver=${_ver}.0
pkgrel=0.1
pkgdesc="MATE menu specifications"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('glib2' 'python2')
makedepends=('gobject-introspection' 'mate-common')
groups=('mate')
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('c21b23b8cb2899de1a50d9bf50ca17a2148ba3fb')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-python \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
