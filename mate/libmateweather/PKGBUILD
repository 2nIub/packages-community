# Contributor: Martin Wimpress <code@flexion.org>
# Maintainer: Stefano Capitani <stefanoatmanjarodotorg>

_ver=1.18
pkgbase=libmateweather
pkgname=(${pkgbase})
pkgver=${_ver}.0
pkgrel=0.1
pkgdesc="Provides access to weather information from the Internet."
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('gtk3' 'dconf' 'libsoup')
conflicts=("${pkgbase}-gtk3")
replaces=("${pkgbase}-gtk3")
makedepends=('intltool')
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('b0f158847c02a22ca2fc802a855a681f5970ce9a')

build() {
    cd "${srcdir}/${pkgbase}-$pkgver"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-python \
        --enable-locations-compression

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "${srcdir}/${pkgbase}-$pkgver"
    make DESTDIR="${pkgdir}" install
}
