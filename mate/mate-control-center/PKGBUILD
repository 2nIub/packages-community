# Contributor: Martin Wimpress <code@flexion.org>
# Maintainer: Stefano Capitani <stefanoatmanjarodotorg>

_ver=1.18
_pkgbase=mate-control-center
pkgname=${_pkgbase}
pkgver=${_ver}.0
pkgrel=0.1
pkgdesc="The Control Center for MATE (GTK2 version)"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('mate-menus' 'mate-settings-daemon' 'marco' 'libunique' 'libxss')
makedepends=('intltool' 'itstool')
groups=('mate')
conflicts=("${_pkgbase}-gtk3")
replaces=("${_pkgbase}-gtk3")
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('ca318c960ec84835d62c596a035ae5929249ae73')

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --sbindir=/usr/bin \
        --localstatedir=/var \
        --disable-update-mimedb

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    rm -f "${pkgdir}/usr/share/applications/mimeinfo.cache"
}
