# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Stefano Capitani <stefanoatmanjarodotorg>
# Maintainer: Brenton Horne <brentonhorne77 at gmail dot com>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=mate-tweak
pkgver=17.10.9
pkgrel=2
pkgdesc="Tweak tool for MATE, a fork of MintDesktop."
arch=('any')
url="https://github.com/ubuntu-mate/mate-tweak"
license=('GPL')
depends=('gtk3' 'libnotify' 'mate-applets' 'python-configobj' 'python-gobject' 'python-psutil' 'python-setproctitle')
makedepends=('python-distutils-extra' 'python-setuptools')
optdepends=('brisk-menu: for Manjaro panel layout'
            'mate-applet-dock: for Mutiny panel layout'
            'mate-menu: to enable advanced menu'
            'mate-netbook: for Netbook panel layout'
            'plank: for Cupertino panel layout'
            'tilda: to enable pull-down terminal')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ubuntu-mate/$pkgname/archive/$pkgver.tar.gz"
        "mate-tweak.patch")
sha256sums=('e4fcf699cccdb01f7bd8b00b5f4ba25ab943c59b254c3ac36d2622ea1820b352'
            'bd2b39db22d7ca4f00bed78857820831b9b8f96f605f182eed4ebf40b582cc34')

prepare() {
	cd $pkgname-$pkgver
	patch -Np0 -i "${srcdir}/mate-tweak.patch"
}

package() {
  cd "$pkgname-$pkgver"
  python3 setup.py install --root="$pkgdir" --optimize=1
}
