# Contributor: Martin Wimpress <code@flexion.org>
# Maintainer: Stefano Capitani <stefanoatmanjarodotorg>

_ver=1.18
_pkgbase=mate-settings-daemon
pkgname=(${_pkgbase})
pkgver=${_ver}.0
pkgrel=0.1
pkgdesc="The MATE Settings daemon"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'libcanberra' 'libmatekbd' 'libmatemixer' 'libnotify'
         'mate-desktop' 'nss' 'polkit')
makedepends=('intltool')
optdepends=('libcanberra-pulse: PulseAudio support'
            'pulseaudio-alsa: PulseAudio support')
groups=('mate')
conflicts=("${_pkgbase}-gtk3")
replaces=("${_pkgbase}-gtk3")
options=('!emptydirs')
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('bdfec7268f967f3fc129f9661c383fd7666b0ebb')

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${_pkgbase} \
        --sysconfdir=/etc \
        --enable-polkit \
        --enable-pulse

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
