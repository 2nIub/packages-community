# Contributor: Martin Wimpress <code@flexion.org>
# Maintainer: Stefano Capitani <stefanoatmanjarodotorg>

_ver=1.18
_pkgbase=mate-utils
pkgname=(${_pkgbase})
pkgver=${_ver}.0
pkgrel=0.1
pkgdesc="Common MATE utilities for viewing disk usage, logs and fonts, taking screenshots, managing dictionaries and searching files"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk2' 'libcanberra' 'libgtop' 'libsm')
makedepends=('intltool' 'itstool' 'mate-panel')
optdepends=('mate-panel: Look up words in dictionaries from the panel')
groups=('mate-extra')
conflicts=("${_pkgbase}-gtk3")
replaces=("${_pkgbase}-gtk3")
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('3149438d15a297e7f4581793ef7b8d1344cdc64e')

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${_pkgbase} \
        --sysconfdir=/etc \
        --disable-maintainer-flags

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
