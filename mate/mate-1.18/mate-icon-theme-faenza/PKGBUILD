# Maintainer: Philip MÃ¼ller <philm@manjaro.org>
# Contributor : Martin Wimpress <code@flexion.org>

_ver=1.18
pkgname=mate-icon-theme-faenza
pkgver=${_ver}.0
pkgrel=0.1
pkgdesc="Faenza icon theme for MATE"
url="http://mate-desktop.org"
arch=('any')
license=('LGPL')
depends=('gtk-update-icon-cache')
makedepends=('git' 'icon-naming-utils' 'mate-common')
options=(!strip)
groups=('mate-extra')
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('1def73aaa5e5ee3588ae6bb2315c833adddcc322')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    rm -f "${pkgdir}/usr/share/icons/matefaenza/icon-theme.cache"
}
