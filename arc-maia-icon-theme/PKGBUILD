# Maintainer: Stefano Capitani <stefanoatmanjarodotorg>
# Author: horst3180 @ deviantart
# This PKGBUILD provide both versions of Arc icon theme

pkgname=('arc-maia-icon-theme' 'arc-icon-theme')
_pkgbase=arc-icon-theme
pkgver=20161122
pkgrel=3
arch=('any')
url="https://github.com/horst3180/arc-icon-theme"
license=('GPL3')
depends=('gtk-update-icon-cache' 'hicolor-icon-theme' 'moka-icon-theme')
makedepends=('inkscape' 'optipng' 'libcanberra') 
optdepends=('faba-icon-theme: Smaller version of moka with no application icons'
            'elementary-icon-theme: Secondary icon theme fallback'
            'arc-themes-maia: gtk theme of arc-maia')
options=('!emptydirs' '!strip')
source=("$_pkgbase-$pkgver.tar.gz::https://github.com/horst3180/$_pkgbase/archive/$pkgver.tar.gz")

sha512sums=('5d0f1417358eb81994868949acefe146537d8f3cc2fd7f529f9e6ba9c264845e50962f94427bac1262a76d3ca98d05795819d7c4a6ecd3139b0b57a6e9fdfad1')


_prepare-arc-icon-theme() {
cd "$srcdir/$_pkgbase-$pkgver"
    sed -i "s/Inherits=.*/Inherits=Moka,Faba,elementary,Adwaita,gnome,hicolor/" Arc/index.theme
}


_prepare-arc-maia-icon-theme() {
# Rebuild the Arc icon Theme to provide the Maia version
	
	cd $srcdir	
    cd "$srcdir/$_pkgbase-$pkgver-maia"
    
    # Remove Arc folder ( this contain the arc icons )
    rm -Rf Arc
    
    # Change the Arc Blue color into any file in Maia variant
    find . -type f -name '*.*' -exec sed -i "s/6ba4e7/44E2BC/g" {}  \;
    find . -type f -name '*.*' -exec sed -i "s/5294e2/16A085/g" {}  \;
    find . -type f -name '*.*' -exec sed -i "s/64a0e6/44E2BC/g" {}  \;

    cd "$srcdir/$_pkgbase-$pkgver-maia/src"
    
    # Start icons rendering 
    echo
    msg "Create arc-icon-theme-maia:this next bit might take a little while..."
    echo
    sh render_icons.sh

    # Add moka, faba and elementary theme as fallback theme
    cd "$srcdir/$_pkgbase-$pkgver-maia"
    sed -i "s/Inherits=.*/Inherits=Moka,Faba,elementary,Adwaita,gnome,hicolor/" Arc/index.theme
    
    # Change Theme name in Arc Maia
    sed -i "s/Name=.*/Name=Arc-Maia/" Arc/index.theme
    sed -i "s/Comment=.*/Comment=Arc Maia Icon theme/" Arc/index.theme
}

prepare() {
cp -R "$srcdir/$_pkgbase-$pkgver" "$srcdir/$_pkgbase-$pkgver-maia"
}

_build_arc-icon-theme() {
	_prepare-arc-icon-theme
    cd "$srcdir/$_pkgbase-$pkgver"
    ./autogen.sh --prefix=/usr
    make
}

_build_arc-maia-icon-theme() {
	_prepare-arc-maia-icon-theme
    cd "$srcdir/$_pkgbase-$pkgver-maia"
    ./autogen.sh --prefix=/usr
    make
}

package_arc-icon-theme() {
pkgdesc='Arc icon theme Official.'
	_build_arc-icon-theme
    cd "$srcdir/$_pkgbase-$pkgver"
    make DESTDIR="${pkgdir}" install
}

package_arc-maia-icon-theme() {
pkgdesc='Arc icon theme Manjaro variant.'
	_build_arc-maia-icon-theme
    cd "$srcdir/$_pkgbase-$pkgver-maia"
    make DESTDIR="${pkgdir}" install

    # Move the Theme into new folder Arc-Maia
    cd "$pkgdir/usr/share/icons"
    mv Arc Arc-Maia
}
