# Maintainer: Stefano Capitani <stefanoatmanjarodotorg>
# Author: horst3180 @ deviantart
# This PKGBUILD provide a maia version of Arc icon theme

pkgname=arc-maia-icon-theme
_pkgname=arc-icon-theme
pkgver=20161122
pkgrel=2
pkgdesc='Arc icon theme Manjaro variant.'
arch=('any')
url="https://github.com/horst3180/arc-icon-theme"
license=('GPL3')
depends=('gtk-update-icon-cache' 'hicolor-icon-theme' 'moka-icon-theme')
makedepends=('inkscape' 'optipng' 'libcanberra') 
optdepends=('faba-icon-theme: Smaller version of moka with no application icons'
            'elementary-icon-theme: Secondary icon theme fallback'
            'arc-themes-maia: gtk theme of arc-maia')
options=('!emptydirs' '!strip')
source=("$_pkgname-$pkgver.tar.gz::https://github.com/horst3180/$_pkgname/archive/$pkgver.tar.gz")

sha512sums=('5d0f1417358eb81994868949acefe146537d8f3cc2fd7f529f9e6ba9c264845e50962f94427bac1262a76d3ca98d05795819d7c4a6ecd3139b0b57a6e9fdfad1')

prepare() {
    # Rebuild the Arc icon Theme to provide the Maia version
    cd "$srcdir/$_pkgname-$pkgver"
    
    # Remove Arc folder ( this contain the arc icons )
    rm -Rf Arc
    
    # Change the Arc Blue color into any file in Maia variant
    find . -type f -name '*.*' -exec sed -i "s/6ba4e7/44E2BC/g" {}  \;
    find . -type f -name '*.*' -exec sed -i "s/5294e2/16A085/g" {}  \;
    find . -type f -name '*.*' -exec sed -i "s/64a0e6/44E2BC/g" {}  \;

    cd "$srcdir/$_pkgname-$pkgver/src"
    
    # Start icons rendering 
    echo
    msg "Create arc-icon-theme-maia:this next bit might take a little while..."
    echo
    sh render_icons.sh

    # Add moka, faba and elementary theme as fallback theme
    cd "$srcdir/$_pkgname-$pkgver"
    sed -i "s/Inherits=.*/Inherits=Moka,Faba,elementary,Adwaita,gnome,hicolor/" Arc/index.theme
    
    # Change Theme name in Arc Maia
    sed -i "s/Name=.*/Name=Arc-Maia/" Arc/index.theme
    sed -i "s/Comment=.*/Comment=Arc Maia Icon theme/" Arc/index.theme
}

build() {
    cd "$srcdir/$_pkgname-$pkgver"
    ./autogen.sh --prefix=/usr
    make
}

package() {
    cd "$srcdir/$_pkgname-$pkgver"
    make DESTDIR="${pkgdir}" install

    # Move the Theme into new folder Arc-Maia
    cd "$pkgdir/usr/share/icons"
    mv Arc Arc-Maia
}
