# Maintainer: Philip MÃ¼ller <philm at manjaro dot org>
# Contributor: Chad "crossroads1112" Sharp <crossroads1112@riseup.net>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>

pkgname=lumina-desktop
_patch=
pkgver=1.4.0
pkgrel=2
pkgdesc="A Lightweight QT5 Desktop for FreeBSD"
arch=('x86_64' 'i686')
url="https://github.com/trueos/lumina"
license=('BSD')
depends=('qt5-base' 'qt5-svg' 'qt5-multimedia' 'qt5-x11extras' 'fluxbox'
        'andromeda-icon-theme' 'xscreensaver' 'desktop-file-utils' 'poppler-qt5')
optdepends=('xorg-xbacklight: required for changing screen brightness'
            'alsa-utils: required for adjusting audio volume'
            'acpi: required for monitoring battery life'
            'numlockx: required for changign state of numlock at login'
            'pavucontrol: required for detatched audio mixer'
            'fluxmod-styles: A good set of Fluxbox themes to improve the appearence of window decorations'
            'andromeda-wallpaper: Andromeda wallpaper')
makedepends=('qt5-tools')
options=('!makeflags')
conflicts=("${pkgname}-git" "lumina-de-git")
source=("${pkgname}-${pkgver}-${pkgrel}.tar.gz::${url}/archive/v${pkgver}${_patch}.tar.gz")
md5sums=('2c06ac4af5a72f3c22532afc74d3d158')


build(){
    cd $srcdir/lumina-${pkgver}${_patch}
    qmake-qt5 QMAKE_CFLAGS_ISYSTEM= PREFIX="/usr" QT5LIBDIR=/usr/lib/qt
    make
}

package() {
    cd $srcdir/lumina-${pkgver}${_patch}
    make INSTALL_ROOT="${pkgdir}" install
    mv "${pkgdir}"/usr/etc "${pkgdir}"/etc
}
