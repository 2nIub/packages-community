# Maintainer : Rob McCathie <rob@manjaro.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: JJDaNiMoTh <jjdanimoth@gmail.com>
# Contributor: nesl247 <nesl247@gmail.com>

pkgname=compiz08-compizconfig-python
_pkgname=compizconfig-python
pkgver=0.8.10
pkgrel=1
pkgdesc="Compizconfig bindings for python"
arch=('i686' 'x86_64')
url="http://opencompositing.org"
license=('GPL')
depends=("compiz08-core>=${pkgver}" 'compiz08-libcompizconfig' 'glib2' 'python2' 'libxrandr' 'libxdamage' 'libxslt' 'libsm')
makedepends=('intltool' 'pkgconfig' 'pyrex')
replaces=("${_pkgname}")
provides=("${_pkgname}=${pkgver}")
conflicts=("${_pkgname}")
source=("http://www.northfield.ws/projects/compiz/releases/${pkgver}/${_pkgname}.tar.gz")
sha1sums=('81c9c22a11395badabc69e5d217d41291d4c1f75')

build() {
  cd "${srcdir}/${_pkgname}"
  PYTHON=python2 ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_pkgname}"
  make DESTDIR="${pkgdir}" install
}
