# Maintainer : Rob McCathie <archaur at rmcc dot com dot au
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Hussam Al-Tayeb ht990332@gmail.com
# Contributor: Khashayar Naderehvandi <khashayar [at] naderehvandi [dot] net>
# Contributor: JJDaNiMoTh <jjdanimoth.aur@gmail.com>

_basename=compiz
_name=compiz-gtk-standalone
pkgname=compiz08-gtk-standalone
pkgdesc="Compiz compositing window manager with gtk decorator. No specific DE integration. Sane defaults. Good for minimal Compiz setups."
pkgver=0.8.9
pkgrel=1
arch=('i686' 'x86_64')
url="http://cgit.compiz.org/"
license=('GPL' 'LGPL' 'MIT')
depends=('startup-notification' 'librsvg' 'libgl' 'dbus' 'glu' 'libxslt' 'fuse' 'libsm' 'gconf' 'libwnck' 'metacity2' 'zenity' 'procps-ng' 'desktop-file-utils')
makedepends=('intltool')
optdepends=("compiz08-fusion-plugins-main: Compiz Main plugins"
            "compiz08-fusion-plugins-extra: Compiz Extra plugins"
            "compiz08-fusion-plugins-unsupported: Unsupported Compiz plugins"
            "compiz08-ccsm: CompizConfig Settings Manager in Python2")
replaces=("${_name}")
provides=("${_name}=${pkgver}" "compiz08-core=${pkgver}" "compiz-core=${pkgver}" 'compiz08-decorator-gtk' 'compiz-decorator-gtk')
conflicts=("${_name}" 'compiz08-core' 'compiz-core' 'compiz08-decorator-gtk' 'compiz-decorator-gtk')
install="${_name}.install"

source=("http://cgit.compiz.org/compiz/core/snapshot/core-compiz-0.8.9.tar.bz2"
        'compiz-gcc-4.7.patch'
        'compiz-standalone-defaults.patch'
        'compiz-gtk-decorator-theme-selector'
        'compiz-gtk-decorator-theme.desktop')

sha1sums=('f48b7cb7e98efd4e6967f07967ac9078127123c0'
          '2138342ae9f253bae003e96e91fdd0bc9918291a'
          '59f37feafedd0cb949498fcbaebe3a298aab3af6'
          '82e9985cdba1e96c20b618bb3bcaa5a94a0741f1'
          '9b3dd487e9142e826848dc6b5d95a98fbb648f3c')

prepare() {
  cd "${srcdir}/core-${_basename}-${pkgver}"
  patch -p0 -i "${srcdir}/compiz-gcc-4.7.patch"
  # Some sane defaults
  patch -p1 -i "${srcdir}/compiz-standalone-defaults.patch"
}

build() {
  cd "${srcdir}/core-${_basename}-${pkgver}"
  ./autogen.sh   --prefix=/usr \
	    --enable-gtk \
	    --enable-metacity \
	    --enable-gconf \
            --with-gconf-schema-file-dir=/etc/gconf/schemas \
	    --enable-dbus \
	    --enable-librsvg \
	    --disable-gnome \
	    --disable-kde \
	    --disable-kde4 \
	    --with-default-plugins="ccp,decoration,resize,place,move,regex,fade,expo,switcher"
  make QDBUSXML2CPP=/usr/bin/qdbusxml2cpp-qt4
}

package() {  
  cd "${srcdir}/core-${_basename}-${pkgver}"
  pushd gtk
  install -Dm644 window-decorator/gwd.schemas "${pkgdir}/usr/share/gconf/schemas/gwd.schemas"
  popd
  for i in dbus gconf ini inotify png regex svg glib kconfig
  do
    rm "${srcdir}"/core-compiz-0.8.9/metadata/compiz-$i.schemas
  done
  gconf-merge-schema "${pkgdir}"/usr/share/gconf/schemas/compiz-decorator-gtk.schemas "${srcdir}"/core-compiz-0.8.9/metadata/*.schemas
  make DESTDIR="${pkgdir}" install
  # install MIT license
  install -Dm644 "${srcdir}/core-compiz-0.8.9/COPYING.MIT" "${pkgdir}/usr/share/licenses/compiz/COPYING.MIT"
  
  install -Dm755 "${srcdir}/compiz-gtk-decorator-theme-selector" "${pkgdir}/usr/bin/compiz-gtk-decorator-theme-selector"
  install -Dm644 "${srcdir}/compiz-gtk-decorator-theme.desktop" "${pkgdir}/usr/share/applications/compiz-gtk-decorator-theme.desktop"
}
