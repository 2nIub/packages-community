# Maintainer : Rob McCathie <rob@manjaro.org>
# Contributor: Dan Fuhry <dan@fuhry.com>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Hussam Al-Tayeb ht990332@gmail.com
# Contributor: Khashayar Naderehvandi <khashayar [at] naderehvandi [dot] net>
# Contributor: JJDaNiMoTh <jjdanimoth.aur@gmail.com>

pkgname=compiz08-mate
_pkgname=compiz-mate
pkgver=0.8.10
pkgrel=1
pkgdesc="Compiz compositing window manager - patched with full MATE support"
url="http://www.compiz.org/"
license=('GPL' 'LGPL' 'MIT')
arch=('i686' 'x86_64')
makedepends=('intltool')
depends=('startup-notification' 'librsvg' 'libgl' 'dbus' 'glu' 'libxslt' 'fuse' 'mate-control-center' 'gconf' 'libwnck' 'procps-ng')
optdepends=('marco: for MATE compiled with GTK2'
            'marco-gtk3: for MATE compiled with GTK3')
install="compiz-mate.install"
options=(!emptydirs)
replaces=("${_pkgname}" 'compiz-core-mate')
provides=("${_pkgname}=${pkgver}" "compiz08-core=${pkgver}" "compiz-core=${pkgver}" 'compiz08-decorator-gtk' 'compiz-decorator-gtk')
conflicts=("${_pkgname}" 'compiz08-core' 'compiz-core' 'compiz08-decorator-gtk' 'compiz-decorator-gtk')
source=("http://www.northfield.ws/projects/compiz/releases/${pkgver}/core.tar.gz"
        "mate-migrate.sh")
sha1sums=('8487aa83ff46d0fcef6a21591b56f4acdd8016df'
          '654ba5e540aa839791dc4c8aed1ddede8dec2033')

prepare() {
  cd "${srcdir}/core"
  "${srcdir}/mate-migrate.sh" .
  mv gtk/gnome gtk/mate
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "${srcdir}/core"
  export CFLAGS="-DHAVE_MARCO_2_17_0"
  ./configure \
    --prefix=/usr \
    --enable-gnome \
    --enable-gtk \
    --enable-marco \
    --enable-gconf \
    --with-gconf-schema-file-dir=/etc/gconf/schemas \
    --enable-dbus \
    --enable-librsvg \
    --disable-kde \
    --disable-kde4 \
    --with-default-plugins="decoration,resize,place,move,switcher"
  make QDBUSXML2CPP=/usr/bin/qdbusxml2cpp-qt4
}



package() {
  cd "${srcdir}/core"
  make DESTDIR="${pkgdir}" install

  pushd gtk
  make DESTDIR="${pkgdir}" install
  install -Dm644 window-decorator/gwd.schemas "${pkgdir}/usr/share/gconf/schemas/gwd.schemas"
  popd

  for i in dbus gconf ini inotify png regex svg glib kconfig
  do
    rm "${srcdir}"/core/metadata/compiz-$i.schemas
  done
  gconf-merge-schema "${pkgdir}"/usr/share/gconf/schemas/compiz-decorator-gtk.schemas "${srcdir}"/core-compiz-"${pkgver}"/metadata/*.schemas
  make DESTDIR="${pkgdir}" install

  # install MIT license
  install -Dm644 "${srcdir}/core/COPYING.MIT" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING.MIT"
}
