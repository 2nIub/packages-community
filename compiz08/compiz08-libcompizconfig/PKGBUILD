# Maintainer : Rob McCathie <rob@manjaro.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: JJDaNiMoTh <jjdanimoth@gmail.com>
# Contributor: nesl247 <nesl247@gmail.com>

_name=libcompizconfig
pkgname=compiz08-libcompizconfig
pkgver=0.8.8
pkgrel=1
pkgdesc="Compiz configuration system library"
arch=('i686' 'x86_64')
url="http://www.compiz.org"
license=('GPL')
depends=('compiz08-core' 'libxml2' 'libxcomposite' 'libxinerama' 'startup-notification')
makedepends=('intltool' 'libxrandr' 'libice' 'libsm' 'mesa')
replaces=("${_name}")
provides=("${_name}=${pkgver}")
conflicts=("${_name}")
options=('!emptydirs')
source=("http://releases.compiz.org/${pkgver}/${_name}-${pkgver}.tar.bz2")
sha1sums=('8ea1f15fd9e1e2eb37a9814646659a31a8018fd8')

build() {
  cd "${srcdir}/${_name}-${pkgver}"

  CPPFLAGS="$CPPFLAGS -I/usr/include/compiz -I/usr/include/startup-notification-1.0 \
	-I/usr/include/libxml2" ./configure --prefix=/usr --sysconfdir=/etc 

  make
}

package() {
  cd "${srcdir}/${_name}-${pkgver}"
  
  make DESTDIR="${pkgdir}" install
}
