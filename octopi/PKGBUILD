# maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# maintainer: Roland Singer <roland[at]manjaro[dog]org>

pkgbase=octopi
pkgname=('octopi' 'octopi-notifier')
pkgver=0.2.902
pkgrel=1
pkgdesc="A pacman frontend using Qt libs"
arch=('i686' 'x86_64')
url="http://octopiproject.wordpress.com"
license=('GPL')
depends=('qt4' 'pacman')
_git=no
makedepends=('git' 'gdb')
optdepends=('kdesu: for KDE'
            'gksu: for XFCE, Gnome, LXDE, Cinnamon'
            'gnome-keyring: for password management'
            'yaourt: for AUR support'
            'octopi-notifier: for notifications')

#install=install
source=(git://github.com/aarnt/octopi.git)
sha256sums=('SKIP')

pkgver() {
  cd ${srcdir}/${pkgname}
  if [ "$_git" == "yes" ]; then
     #echo "`git tag | tail -1 | sed 's|v||g'`.`git describe --always`"
     #echo "0.2.0.$(date +%m%d).`git describe --always`"
     #echo "0.2.0$(git describe --tags | sed 's|v||g' | sed 's|-|.|g')"
     echo 0.2.901.$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
  else
     echo "$pkgver"
  fi
}

build() {
    cpucount=$(grep -c processor /proc/cpuinfo 2>/dev/null)
    jc=$((${cpucount:-1}))

    cd "${srcdir}/${pkgbase}"
    msg "Starting build..."

    qmake-qt4 $pkgname.pro
    make -j $jc

    cd notifier/pacmanhelper
    msg "Building pacmanhelper..."
    qmake-qt4 pacmanhelper.pro
    make -j $jc
    
    cd ../octopi-notifier
    msg "Building octopi-notifier..."
    qmake-qt4 octopi-notifier.pro
    make -j $jc
}

package_octopi() {
   cd "${srcdir}/${pkgbase}"
   #Octopi main files
   install -D -m755 bin/$pkgname ${pkgdir}/usr/bin/$pkgname
   install -D -m644 $pkgname.desktop ${pkgdir}/usr/share/applications/$pkgname.desktop
   install -D -m644 resources/images/${pkgname}_green.png ${pkgdir}/usr/share/icons/$pkgname.png
   install -D -m644 resources/images/${pkgname}_red.png ${pkgdir}/usr/share/icons/${pkgname}_red.png
   install -D -m644 resources/images/${pkgname}_yellow.png ${pkgdir}/usr/share/icons/${pkgname}_yellow.png

   #Pacmanhelper service files
   install -D -m755 notifier/bin/pacmanhelper ${pkgdir}/usr/lib/octopi/pacmanhelper

   install -D -m644 notifier/pacmanhelper/polkit/org.octopi.pacman.policy ${pkgdir}/usr/share/polkit-1/actions/org.octopi.pacman.policy
   install -D -m644 notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.conf ${pkgdir}/etc/dbus-1/system.d/org.octopi.pacmanhelper.conf
   install -D -m644 notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.xml ${pkgdir}/usr/share/dbus-1/interfaces/org.octopi.pacmanhelper.xml
   install -D -m644 notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.service ${pkgdir}/usr/share/dbus-1/system-services/org.octopi.pacmanhelper.service
}

package_octopi-notifier() {
   pkgdesc="Notifier for Octopi"
   depends=('octopi' 'libnotify')
   optdepends=('xfce4-notifyd: for notifications in XFCE')

   cd "${srcdir}/${pkgbase}"
   #Octopi-notifier file
   install -D -m755 notifier/bin/octopi-notifier ${pkgdir}/usr/bin/octopi-notifier
   install -D -m644 octopi-notifier.desktop ${pkgdir}/etc/xdg/autostart/octopi-notifier.desktop
}
