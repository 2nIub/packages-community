# Maintainer: Philip Müller <philm[at]manjaro[dot]org>
# Maintainer: Rob McCathie <rob[at]manjaro[dot]org>
# Maintainer: Alexandre Arnt <arnt[at]manjaro[dot]org>
# Maintainer: Roland Singer <roland[at]manjaro[dot]org>
# Maintainer: Ramon Buldó <ramon[at]manaro[dot]org>

## Why there are 3 notifier packages:
## https://forum.manjaro.org/index.php?topic=17673.msg163296#msg163296
## https://forum.manjaro.org/index.php?topic=17673.msg171500#msg171500

pkgbase=octopi
pkgname=('octopi' 'octopi-notifier-qt4' 'octopi-notifier-qt5' 'octopi-notifier-frameworks')
pkgver=0.5.0
pkgrel=9
# This is the release package so the below _gitcommit variable should (usually) be commented out.
_gitcommit="a4c155f3e18dc0d1c283745ed42b6dfd0455be0b"
arch=('i686' 'x86_64')
url="http://octopiproject.wordpress.com"
license=('GPL2')
makedepends=('qt5-declarative' 'qt4' 'knotifications' 'libnotify')

if [ "${_gitcommit}" != "" ]; then
  source=("octopi-${pkgver}-${pkgrel}.tar.gz::https://github.com/aarnt/octopi/archive/${_gitcommit}.tar.gz")
else
  source=("https://github.com/aarnt/octopi/archive/v${pkgver}.tar.gz")
fi

sha256sums=('b873d8f9a26c301fac92e270cdcb66a376d3d4d6b39215b0f58a55727d22ab74')

prepare() {
  if [ "${_gitcommit}" != "" ]; then
    cd "${srcdir}/${pkgbase}-${_gitcommit}"
  else
    cd "${srcdir}/${pkgbase}-${pkgver}"
  fi

  # sed version
  sed -i -e "s|0.5.1-dev|${pkgver}-${pkgrel}|g" src/strconstants.h

  cp -r notifier notifier-qt4
  cp -r notifier notifier-qt5
  cp -r notifier notifier-frameworks
  sed -i 's|#DEFINES += KSTATUS|DEFINES += KSTATUS|' notifier-frameworks/octopi-notifier/octopi-notifier.pro
}

build() {
  if [ "${_gitcommit}" != "" ]; then
    cd "${srcdir}/${pkgbase}-${_gitcommit}"
  else
    cd "${srcdir}/${pkgbase}-${pkgver}"
  fi

  msg "Starting build..."
  qmake-qt5 octopi.pro
  make

  cd notifier/pacmanhelper
  msg "Building pacmanhelper..."
  qmake-qt5 pacmanhelper.pro
  make
   
  cd ../../notifier-qt4/octopi-notifier
  msg "Building octopi-notifier-qt4..."
  qmake-qt4 octopi-notifier.pro
  make
  
  cd ../../notifier-qt5/octopi-notifier
  msg "Building octopi-notifier-qt5..."
  qmake-qt5 octopi-notifier.pro
  make

  cd ../../notifier-frameworks/octopi-notifier
  msg "Building octopi-notifier-frameworks..."
  qmake-qt5 octopi-notifier.pro
  make

  cd ../../repoeditor
  msg "Building octopi-repoeditor..."
  qmake-qt5 octopi-repoeditor.pro
  make
}

package_octopi() {
  pkgdesc="A powerful Pacman frontend using Qt5 libs"
  install="octopi.install"
  depends=('qt5-declarative' 'hicolor-icon-theme' 'desktop-file-utils' 'xterm')
  optdepends=('kdesu: for KDE'
              'gksu: for XFCE, Gnome, LXDE, Cinnamon'
              'gnome-keyring: for password management'
              'yaourt: for AUR support'
              'octopi-notifier: for notifications'
              'octopi-notifier-frameworks: for notifications with Knotifications support'
              'pacmanlogviewer: to view pacman log files')
  replaces=('octopi-qt5')

  if [ "${_gitcommit}" != "" ]; then
    cd "${srcdir}/${pkgbase}-${_gitcommit}"
  else
    cd "${srcdir}/${pkgbase}-${pkgver}"
  fi
  
  # Octopi main files
  install -Dm755 "bin/octopi" "${pkgdir}/usr/bin/octopi"
  install -Dm644 "octopi.desktop" "${pkgdir}/usr/share/applications/octopi.desktop"
  install -Dm644 "resources/images/octopi_green.png" "${pkgdir}/usr/share/icons/octopi.png"
  install -Dm644 "resources/images/octopi_green.png" "${pkgdir}/usr/share/icons/hicolor/48x48/apps/octopi.png"
  install -Dm644 "resources/images/octopi_red.png" "${pkgdir}/usr/share/icons/octopi_red.png"
  install -Dm644 "resources/images/octopi_yellow.png" "${pkgdir}/usr/share/icons/octopi_yellow.png"

  # Pacmanhelper service files
  install -Dm755 "notifier/bin/pacmanhelper" "${pkgdir}/usr/lib/octopi/pacmanhelper"

  install -Dm644 "notifier/pacmanhelper/polkit/org.octopi.pacman.policy" "${pkgdir}/usr/share/polkit-1/actions/org.octopi.pacman.policy"
  install -Dm644 "notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.conf" "${pkgdir}/etc/dbus-1/system.d/org.octopi.pacmanhelper.conf"
  install -Dm644 "notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.xml" "${pkgdir}/usr/share/dbus-1/interfaces/org.octopi.pacmanhelper.xml"
  install -Dm644 "notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.service" "${pkgdir}/usr/share/dbus-1/system-services/org.octopi.pacmanhelper.service"

  # Repoeditor files
  install -Dm755 "repoeditor/bin/octopi-repoeditor" "${pkgdir}/usr/bin/octopi-repoeditor"
}

package_octopi-notifier-qt4() {
  pkgdesc="Notifier for Octopi using Qt4 libs"
  depends=('octopi' 'libnotify' 'qt4')
  optdepends=('xfce4-notifyd: for notifications in XFCE')
  conflicts=('octopi-notifier')
  provides=("octopi-notifier=${pkgver}")
  replaces=('octopi-notifier')

  if [ "${_gitcommit}" != "" ]; then
    cd "${srcdir}/${pkgbase}-${_gitcommit}"
  else
    cd "${srcdir}/${pkgbase}-${pkgver}"
  fi

  # Octopi-notifier-qt4 files
  install -Dm755 "notifier-qt4/bin/octopi-notifier" "${pkgdir}/usr/bin/octopi-notifier"
  install -Dm644 "octopi-notifier.desktop" "${pkgdir}/etc/xdg/autostart/octopi-notifier.desktop"
}

package_octopi-notifier-qt5() {
  pkgdesc="Notifier for Octopi using Qt5 libs"
  depends=('octopi' 'libnotify')
  optdepends=('xfce4-notifyd: for notifications in XFCE')
  conflicts=('octopi-notifier')
  provides=("octopi-notifier=${pkgver}")
  replaces=('octopi-qt5-notifier')

  if [ "${_gitcommit}" != "" ]; then
    cd "${srcdir}/${pkgbase}-${_gitcommit}"
  else
    cd "${srcdir}/${pkgbase}-${pkgver}"
  fi

  # Octopi-notifier-qt5 files
  install -Dm755 "notifier-qt5/bin/octopi-notifier" "${pkgdir}/usr/bin/octopi-notifier"
  install -Dm644 "octopi-notifier.desktop" "${pkgdir}/etc/xdg/autostart/octopi-notifier.desktop"
}

package_octopi-notifier-frameworks() {
  pkgdesc="Notifier for Octopi with Knotifications support"
  depends=('octopi' 'knotifications')
  conflicts=('octopi-notifier')
  provides=("octopi-notifier=${pkgver}")

  if [ "${_gitcommit}" != "" ]; then
    cd "${srcdir}/${pkgbase}-${_gitcommit}"
  else
    cd "${srcdir}/${pkgbase}-${pkgver}"
  fi

  # Octopi-notifier-frameworks files
  install -Dm755 "notifier-frameworks/bin/octopi-notifier" "${pkgdir}/usr/bin/octopi-notifier"
  install -Dm644 "octopi-notifier.desktop" "${pkgdir}/etc/xdg/autostart/octopi-notifier.desktop"
}
